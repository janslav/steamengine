[SCRIPTEDSKILLDEF 47]
DEFNAME=Skill_Stealth
KEY=Stealth
//TITLE="Rogue"
//PROMPTMSG=""
//DELAY=2.0
//STAT_STR=20
//STAT_INT=60
//STAT_DEX=90
//BONUS_STR=0
//BONUS_DEX=80
//BONUS_INT=20
//BONUSSTATS=25
ADVRATE=10.0,200.0,800.0

on=@Select
if (Flag_Hidden)
	this.StartSkill(Skill_Stealth)
else
	ClilocSysMessage(502725) //You must hide first
endif
return 1

on=@Start
if (Skill_Stealth.CheckSuccess(this, {0 700}))
	Skill_Stealth.Success(this)
else
	Skill_Stealth.Fail(this)
endif
CurrentSkill = null;
	
on=@Success
arg.p = GetPlugin(@@stealthstep)
if (arg.p == null)
	arg.p = AddNewPlugin(@@stealthstep, p_stealthStep)
endif
arg.p.stealthStepsLeft = (Stealth / 50) + 1
ClilocSysMessage(502730) //You begin to move quietly.

on=@Fail
ClilocSysMessage(502731) //You fail in your attempt to move unnoticed.
UnHide()

[StealthStepPluginDef p_stealthStep]
//todo: looting others should also unhide

On=@SkillStart
//according to uo stratics, these skills do not unhide...
switch (argv(1))
	case SkillName.DetectHidden
	case SkillName.ItemID
	case SkillName.Anatomy
	case SkillName.ArmsLore
	case SkillName.AnimalLore
	case SkillName.EvalInt
	case SkillName.Forensics
	case SkillName.Poisoning
	case SkillName.Stealth
		break
	case default
		Cont.UnHide()
		break
endswitch

	
On=@Step
if (Cont.Flag_Hidden)
	if (<stealthStepsLeft> < 1)
		Cont.SelectSkill(Skill_Stealth);
	endif
	if (argv(1)) //running?
		stealthStepsLeft = stealthStepsLeft - 2
	else
		stealthStepsLeft = stealthStepsLeft - 1
	endif
else
	Delete()
endif

//502725	You must hide first                                                           
//502726	You are not hidden well enough.  Become better at hiding.                     
//502727	You could not hope to move quietly wearing this much armor.                   
//502728	It will be tremendouly difficult to move quietly with this many people around.
//502729	Better hide under a tree and get some rest first.                             
//502730	You begin to move quietly.                                                    
//502731	You fail in your attempt to move unnoticed.                            