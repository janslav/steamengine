[function pages]
pageMan(<args>)

[function pageMan]
if(GMPage.GmPages.Count == 0)
	src.sysmessage("Zadne GM page k zobrazeni")
 	return 1
endif
if(args == "")
 	dialog(d_Pages,SortingCriteria.TimeAsc,0)
else
 	dialog(d_Pages,<args>,0)
endif

//pozor: src - ten kdo dialog vyvolal, argv(1) - ten komu se dialog zobrazil, this - default object gumpscriptu
[dialog d_Pages] //argv2 - sorting criterion, argv3 - number of the page (argv1 - gumpinstance.cont (tj ten komu se dialog zobrazil))
//the GMPage object has these fields
//Sender - the player who sent the page
//Handler - GM who handled the page
//Time - the time when the page was sent
//P - the point where the page was created
//Reason - the players description of the problem
//Reply - the GM's reply to the problem
//Replied - if any GM has already replied to the page or not
//---------
//posible sorting criteria are
//SortingCriteria.NameAsc/Desc - players name
//SortingCriteria.TimeAsc/Desc - time of the GM page creation
//SortingCriteria.AccountAsc/Desc - players accounts
arg dialog = ImprovedDialog(argv(0))

arg pagesList = <GMPage.GetSortedBy(<argv(2)>)> //seznam pagi setridenej zadanym kriteriem
argv(0).tag.pages = pagesList //ulozime do tagu pro pouziti u buttonu

arg firstiVal = <argv(3)> //cislo prvni page zobrazene na strance (od nuly)
arg imax = Min(firstiVal + ImprovedDialog.PAGE_ROWS, pagesList.Count) //maximalni index na strance (dle poctu pagi na str.)

dialog.CreateBackground(<d_def_Pages_sirka>)
dialog.SetLocation(10,30)

//nadpis tabulky
dialog.Add(GUTATable(1,0,ButtonFactory.D_BUTTON_WIDTH)) 
dialog.LastTable.AddToCell(0,0,TextFactory.CreateText("Seznam GMPagí")) //prvni text, uplne vpravo
dialog.LastTable.AddToCell(0,0,TextFactory.CreateText(400,0,<Hues.WriteColor>,"ONLINE")) //text o 400 bodù vpravo 
dialog.LastTable.AddToCell(0,0,TextFactory.CreateText(500,0,<Hues.NAColor>,"OFFLINE")) //text o 500 bodù vpravo
dialog.LastTable.AddToCell(0,1,ButtonFactory.CreateButton(LeafComponentTypes.ButtonCross,0)) //exit button
dialog.MakeTableTransparent()

//popis sloupecku
dialog.Add(GUTATable(1,160,140,ButtonFactory.D_BUTTON_WIDTH,180,130,ButtonFactory.D_BUTTON_WIDTH,0,ButtonFactory.D_BUTTON_WIDTH))
//cudlik s hracem
dialog.LastTable.AddToCell(0,0,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSortUp,1)) //tridit dle jmena hrace asc
dialog.LastTable.AddToCell(0,0,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSortDown,0,ButtonFactory.D_SORTBUTTON_LINE_OFFSET,4)) //tridit dle jmena hrace desc
dialog.LastTable.AddToCell(0,0,TextFactory.CreateText(ButtonFactory.D_SORTBUTTON_COL_OFFSET,0,"Hráè")) //x posunuto kousek o sortovaci button
//cudlik s pozici
dialog.LastTable.AddToCell(0,1,TextFactory.CreateText("Pozice"))
//cudlik na smazani
dialog.LastTable.AddToCell(0,2,TextFactory.CreateText("Del"))
//cas
dialog.LastTable.AddToCell(0,3,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSortUp,2)) //tridit podle casu asc
dialog.LastTable.AddToCell(0,3,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSortDown,0,ButtonFactory.D_SORTBUTTON_LINE_OFFSET,5)) //tridit podle casu desc
dialog.LastTable.AddToCell(0,3,TextFactory.CreateText(ButtonFactory.D_SORTBUTTON_COL_OFFSET,0, "Èas odeslání"))
//cudlik s accountem
dialog.LastTable.AddToCell(0,4,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSortUp,3)) //tridit dle accountu asc
dialog.LastTable.AddToCell(0,4,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSortDown,0,ButtonFactory.D_SORTBUTTON_LINE_OFFSET,6)) //tridit dle accountu desc
dialog.LastTable.AddToCell(0,4,TextFactory.CreateText(ButtonFactory.D_SORTBUTTON_COL_OFFSET,0,"Account"))
//cudlik na odpoved
dialog.LastTable.AddToCell(0,5,TextFactory.CreateText("Re:"))
//zbytek - text
dialog.LastTable.AddToCell(0,6,TextFactory.CreateText("Popis problému"))
//button na odpoved
dialog.LastTable.AddToCell(0,7,TextFactory.CreateText("Odpoved"))

dialog.MakeTableTransparent() //zpruhledni nadpisovy radek

//vlastni seznam pagi
dialog.Add(GUTATable(ImprovedDialog.PAGE_ROWS))
dialog.CopyColsFromLastTable()

arg rowCntr = 0
arg idx = <firstiVal>

while (<idx> < <imax>)
	arg iPage = pagesList.get_Item(idx)
	arg pcolor = <Qval(iPage.sender.Account.Online,<Hues.WriteColor>,<Hues.NAColor>)>

 	if(iPage.Replied)
  		arg mscolor = <Hues.PageRepliedColor> //nejaka oranzova sracka
 	else 
  		arg mscolor = <Hues.WriteColor> //default
 	endif
		//cislo sloupce, cislo radku ve sloupci, objekt co se tam frkne
	dialog.LastTable.AddToCell(<rowCntr>,0,ButtonFactory.CreateButton(LeafComponentTypes.ButtonPaper,(7*idx)+100)) //players info
	dialog.LastTable.AddToCell(<rowCntr>,0,TextFactory.CreateText(ButtonFactory.D_BUTTON_WIDTH, 0, pColor, "<iPage.sender.Name>")) //jmeno odesilatele
	dialog.LastTable.AddToCell(<rowCntr>,1,ButtonFactory.CreateButton(LeafComponentTypes.ButtonTick,(7*idx)+101)) //jdi na pozici
	dialog.LastTable.AddToCell(<rowCntr>,1,TextFactory.CreateText(ButtonFactory.D_BUTTON_WIDTH,0, pColor, "<iPage.p.ToNormalString()>")) //pozice
	dialog.LastTable.AddToCell(<rowCntr>,2,ButtonFactory.CreateButton(LeafComponentTypes.ButtonCross,(7*idx)+102)) //smaz page
	dialog.LastTable.AddToCell(<rowCntr>,3,TextFactory.CreateText(pColor, "<iPage.time.ToString()>")) //cas napsani
	dialog.LastTable.AddToCell(<rowCntr>,4,ButtonFactory.CreateButton(LeafComponentTypes.ButtonTick,(7*idx)+103)) //ukaz account
	dialog.LastTable.AddToCell(<rowCntr>,4,TextFactory.CreateText(ButtonFactory.D_BUTTON_WIDTH,0, pColor, "<iPage.sender.Account.Name>")) //account
	dialog.LastTable.AddToCell(<rowCntr>,6,ButtonFactory.CreateButton(LeafComponentTypes.ButtonPaper,(7*idx)+105)) //zobrazit text zpravy
	dialog.LastTable.AddToCell(<rowCntr>,6,TextFactory.CreateText(ButtonFactory.D_BUTTON_WIDTH, 0, mscolor, "<iPage.reason>")) //text page 	 	
	if(iPage.Replied)
  		//nepujde odpovedet vickrat
		dialog.LastTable.AddToCell(<rowCntr>,7,ButtonFactory.CreateButton(LeafComponentTypes.ButtonPaper,(7*idx)+106)) //zobrazit text opdpovedi			
 	else
		dialog.LastTable.AddToCell(<rowCntr>,5,ButtonFactory.CreateButton(LeafComponentTypes.ButtonSend,(7*idx)+104)) //cudlik pro odpoved  		
 	endif
	arg rowCntr = <rowCntr>+1
 	arg idx = <idx>+1
endwhile

dialog.MakeTableTransparent() //zpruhledni zbytek dialogu

//strankovani
dialog.CreatePaging(pagesList.Count, firstiVal)
dialog.WriteOut()

//ulozit info o prave vytvorenem dialogu pro navrat (argv(1) - ten kteremu se dialog zobrazil (tomu se pak zobrazi znova)
//argv(2) - trideni
DialogStackItem.EnstackDialog(<argv(1)>,<this>,d_pages,<argv(2)>,firstiVal)

[DIALOG d_Pages BUTTON]
on=0 //exitting dialog by right click
DialogStackItem.PopStackedDialog(argv(1).Conn)	//odstranit ze stacku aktualni dialog
DialogStackItem.ShowPreviousDialog(argv(1).Conn) //zobrazit pripadny predchozi dialog						
return 0

on=1
//src.sysmessage("tridit dle jmena asc")
arg dsi = DialogStackItem.PopStackedDialog(argv(1).Conn) //vem ulozene info o dialogu
dsi.SetArgValue(0,SortingCriteria.NameAsc) //uprav info o sortovani
dsi.Show()

on=2
//src.sysmessage("tridit dle casu asc")
arg dsi = DialogStackItem.PopStackedDialog(argv(1).Conn) //vem ulozene info o dialogu
dsi.SetArgValue(0,SortingCriteria.TimeAsc) //uprav info o sortovani
dsi.Show()

on=3
//src.sysmessage("tridit dle uctu asc")
arg dsi = DialogStackItem.PopStackedDialog(argv(1).Conn) //vem ulozene info o dialogu
dsi.SetArgValue(0,SortingCriteria.AccountAsc) //uprav info o sortovani
dsi.Show()

on=4
//src.sysmessage("tridit dle jmena desc")
arg dsi = DialogStackItem.PopStackedDialog(argv(1).Conn) //vem ulozene info o dialogu
dsi.SetArgValue(0,SortingCriteria.NameDesc) //uprav info o sortovani
dsi.Show()

on=5
//src.sysmessage("tridit dle casu desc")
arg dsi = DialogStackItem.PopStackedDialog(argv(1).Conn) //vem ulozene info o dialogu
dsi.SetArgValue(0,SortingCriteria.TimeDesc) //uprav info o sortovani
dsi.Show()

on=6
//src.sysmessage("tridit dle uctu desc")
arg dsi = DialogStackItem.PopStackedDialog(argv(1).Conn) //vem ulozene info o dialogu
dsi.SetArgValue(0,SortingCriteria.AccountDesc) //uprav info o sortovani
dsi.Show()

ON=@AnyButton 
arg btnNumber = (argv(2)-100) % 7 //on one line we have numbers 100,101,102,103,104,105,106. next line is 107,108,109,110,111,112 etc...
arg line = (argv(2) - (100+btnNumber)) div 7 //e.g. 104 - (100+4) / 7 = 0; 107 - (100+0) / 7 = 7/7 = 0; 115 - (100 + 1) / 7 = 2 etc...
//src.sysmessage("cudlik: <argv(2)>, butt : <btnNumber> radek: <line>")
arg iPage = argv(0).tag.pages.get_Item(line)

//adresat, button, pripadna hodnota iputfieldu pro paging,    index informace o pagingu mezi parametry , pocet pagi celkem
if (ImprovedDialog.PagingButtonsHandled(<argv(1)>, <argv(2)>, <argnum[ImprovedDialog.ID_PAGE_NO_INPUT]>, 1, argv(0).tag.pages.Count))
	return 1 //neni to nahoou neco z pagingu?, jestli jo tak return hned
endif

if (btnNumber == 0) //player info
	//accountmgr.findname(<arg(iPage).account.name>).lastcharuid.try srccome
elseif (btnNumber==1) //go to the pages creating position
	src.sysmessage("skacu na <iPage.p> : <iPage.p.ToString()>")
  	src.go(iPage.P)
	//src.update
elseif (btnNumber==2) //delete the page from the list
  	GMPage.DeletePage(iPage)
  	src.sysmessage("Page byla smazana")
	DialogStackItem.ShowPreviousDialog(argv(1).Conn);
elseif (btnNumber==3) //display the information about the account 
  	//try accountinfo(<arg(iPage).account.name>)
elseif (btnNumber==4) //reply to the page (if not replied, otherwise the button will not exist
  	dialog(inpt_page_reply,iPage) //send the page instance and the dialog information	
elseif (btnNumber==5) //display the page text in the better window
	dialog(d_display_text,"Puvodní text page",iPage.reason)
elseif (btnNumber==6) //display the page reply text in the better window
	dialog(d_display_text,"Odpoved na page",iPage.reply)	
endif

[InputDef inpt_page_reply]
label="Odpoved na page"
default="Page byla vyresena"

on=@response //argv0 - the reply text, argv1 - the page object, argv2 - sortby info, 
	     //argv3 - firsti (the number of the first page to be displayed in the list
arg thePage = <argv(1)>
thePage.replied = true
thePage.reply = "<src.Name>(<src.Account.Name>): <argv(0)>" //text odpovedi
thePage.handler = <src> //the GM who replied
thePage.sender.Delayedmessage(src,"Odpoved na page: <argv(0)>") //poslat delayed message
DialogStackItem.ShowPreviousDialog(src.Conn) //zavolat predchozi dialog

//player-available dialog for sending a page
[InputDef inpt_page_post]
label="Nova GM page"
default="Zde vepis duvod tve page"

on=@response //args - the page text
src.sysmessage("argv: <argv(0)>")
src.Page(<argv(0)>)

[function sendgmpage] //players function for sending a GM page
Dialog(inpt_page_post)

[eof]