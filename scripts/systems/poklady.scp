
[itemdef i_mem_poklad]
ID = i_memory
TYPE = t_eq_script
LAYER = layer_special
name=generator pokladu

on=@create
timer=<cont.tag(mytimer)>//nastavi timer memory itemu podle udaje na pokladnikovi
attr=010

on=@timer
timer=cont.tag(mytimer)//nastavi timer memory itemu podle udaju na pokladnikovi plus rozhodi nahodu
if (finduid(cont.tag(mylastnew)))//truhla existuje
 if (finduid(cont.tag(mylastnew)).timer < 0)//neni otevrena
  if (finduid(cont.tag(mylastnew)).id==i_poklad)
   return 1
  endif
 endif
endif
cont.newitem(i_poklad)
cont.act.p=<cont.p>
cont.tag(mylastnew,<cont.act>)//zapamatuje si uid truhly
cont.act.tag(vytvoreno,<serv.time>)//pro kontrolu
cont.act.attr(010)
cont.act.tag(pokladnik,<cont>)//zapise na truhlu uid pokladnika
if (cont.tag(lockpick))
 cont.act.type=t_container_locked
 cont.act.tag(lockpick,<cont.tag(lockpick)>)
endif
//if (tag(trap))
// cont.act.
//endif

return 1

[itemdef i_poklad_fake]
id=i_chest_metal
name=poklad
attr=010
category=poklady
subsection=truhly_s_poklady
description=fake vygenerovaneho pokladu zjistuje zda je spawnuty strazce

on=@UserDClick
if (src.isGM)
  target("Zamer spawn ktery ma byt vybity")
  return 1
else
  if (<link>)
    if (<link.more2>) // je neco spawnutyho ?
      src.jail
      src.newitem(i_pergamen).bounce
      lastnew.tag(done,1)
      lastnew.tag(radek_1,Copak smrade co ! ?)
      lastnew.tag(radek_2,Otevrel jsi truhlu pokladu, jejiz strazce byl stale nazivu.)
      lastnew.tag(radek_3,Zneuzil jsi tedy bugu a tvuj trest bude tvrdy !)
      lastnew.tag(radek_4,Napis page a upozorni GM na tento svitek)
      lastnew.color=color_o_mithril
      lastnew.attr=010
      return 1
    endif
  endif
endif

on=@TargOn_Item
if (src.targ.type==t_spawn_char)
	src.sysMessage("Zamerujes spawn strazce")
	link=<src.targ>
	return 1
endif

[itemdef i_poklad]
id=i_chest_metal
name=poklad
category=poklady
subsection=truhly_s_poklady
description=vygenerovany poklad nepouzivat

on=@userdclick
if(<finduid(tag(pokladnik)).tag(provereno)>==1)
else
 finduid(tag(pokladnik)).tag(lastopen,<eval (<serv.time> - (<age>*10) )>)
 finduid(tag(pokladnik)).tag(provereno,1)
endif
tag(klikos,<src>)
if (src.isgm)
 src.sysmessage(nesahej na to, rozbijes to!)
 say (nesahat ! kazdej dvojklik zpusobi, ze poklad do hodiny zmizi a novej bude mit mene penez, protoze se to pocita podle stari truhly)
 return 1
elseif (type!=t_container_locked)&&(timer==-1)//je odemcena ale jeste neotevrena
 if (tag(pokladnik).findlayer(29).rescount)
  if (tag(odemceno))
  else
   tag(pokladnik).findlayer(29).contents(f_hlidaci)
   tag(kdyhlidac,<serv.time>)
   src.sysmessage("Truhla je hlidana!")
   tag(odemceno,1)
   return 1
  endif
 endif
 if(tag(kdyhlidac) <  <eval (<serv.time>-40)> )
  timer=3600//aby vyprazdnena truhla za hodinu zmizela
  tag(pokladnik).findlayer(21).contents(f_poklad)//na vsechno v baglu pokladnika provede funkci 
  finduid(<tag(pokladnik)>).f_dejprachy
  finduid(<tag(pokladnik)>).tag(lastopen,<serv.time>)
 endif
 return 1
endif




[function f_poklad]
if (<tag(periodic)>)//jedna se o item jehoz mnozstvi se casem zvysuje
  runxtimes(<eval <eval ( <serv.time>-<topobj.tag(lastopen)> )*<tag(periodic)>>/846000>,f_dejpoklad)
else
  f_dejpoklad
endif

[function f_dejpoklad]
if ({0 100} < <tag(chance)>)||(!<tag(chance)>)//resi procenta a tam kde neni uvedeno pusti rovnou
  topobj.newitem(<id>)
  topobj.act.cont=<topobj.tag(mylastnew)>
  topobj.act.amount=<amount>
  topobj.act.more1=<more1>
  topobj.act.color=<color>
  if(tag(my_id))
    topobj.act.name=<name>
    topobj.act.tag(my_id,<tag(my_id)>)
    topobj.act.type=t_figurine_2
    topobj.act.tag(myhits,<tag(myhits)>)
    topobj.act.tag(myfood,<tag(myfood)>)
  elseif(type==t_figurine_2)
    topobj.act.type=t_figurine
  endif
endif
//say "vidis <topobj.act.name>"//shazuje klienty kdyz je toho moc

[function f_dejprachy]
//f_vytvor_mesec(<eval (<tag(mylastnew).age>/<tag(prachy)>)>,<tag(mylastnew)>) //prvni arg == kolik penez, druhej arg == kam ma ladovat truhly
f_vytvor_mesec(<eval ( ( <serv.time> - <tag(lastopen)> ) / (<tag(prachy)> * 10) )>,<tag(mylastnew)>) //prvni arg == kolik penez, druhej arg == kam ma ladovat truhly
if (tag(sek))
  f_vytvor_mesec(<eval {<tag(sek)>}>,<tag(mylastnew)>)
endif 

[function f_hlidaci]
src.f_spawninit(1,<more1>,60,60,12)
if (tag(guarded))//bude hlidat
 topobj.tag(mylastnew).link=<var(spawnuid)>
 topobj.tag(mylastnew).type=t_container_guarded
endif
topobj.tag(mylastnew).type=t_container_guarded
topobj.newitem(i_randompotvora)
topobj.act.p=<topobj.p>
topobj.act.tag(mrchaspawn,<var(spawnuid)>)
topobj.act.timer=3600

[function showTreasureTime]
arg(tm,<serv.time>-<tag(lastopen)>)
src.sysMessage("<dsToRTime(<arg(tm)>)>")

[eof]