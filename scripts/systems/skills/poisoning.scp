
[itemdef i_memory_jed]//dela efekt
ID=i_rune_poison
TYPE=t_eq_script
name=jed

on=@equip
timer=<tag(jed_rychlost)>//jak rychle bude skytat
if (cont.resist_poison)//ma resist ? kdyz jo, tak prepocitat silu
// cont.say mam resists <cont.resist_poison>
// cont.say sila jedu pred je <tag(jed_sila)>
 tag(jed_sila,<eval tag(jed_sila)-<eval <eval tag(jed_sila)*<cont.resist_poison> >/1000>>)
// cont.say sila jedu po je <cont.tag(jed_sila)>
endif
if (tag(jed_sila)>0)
 cont.events=+e_poisoned//aby fungovaly cure
 cont.sysmessage "You have been poisoned"
 cont.flag_poisoned=1
else
 remove
endif

on=@timer
timer=<tag(jed_rychlost)>
if (tag(jed_sila) < 1)//neni sila, likvidovat
 remove
endif
cont.<tag(jed_typ)>(<tag(jed_sila)>)//efekt
tag(jed_minus,<eval tag(jed_sila)/10>)
if (tag(jed_minus)<1)
 tag(jed_minus,1)
endif
tag(jed_sila,#-<tag(jed_minus)>)
return 1

on=@unequip
if (topobj.rescount(i_memory_jed)<2)
 cont.flag_poisoned=0
 cont.events=-e_poisoned
endif



[function sniz_jed]//zmensuje jedu na zbrani silu pouzitim
if (tag(jed_vydrz))&&(tag(jed_sila) > 0)
 tag(jed_sila,#-<eval (<tag(jed_sila)> / <tag(jed_vydrz)>)>)
 tag(jed_vydrz,#-1)
else//uz neni jedovata, zlikviduj to
 tag.remove(jed_vydrz)
 tag.remove(jed_typ)
 tag.remove(jed_sila)
 events=-e_poisonitem
 cont.events=-e_poison
endif


[function f_poisoning]
src.reveal
if (src.nolightfizz(1))
 return 1
endif
src.action=-1
src.stopscriptedskills
src.newitem i_potion_poison_timer
src.act.timer=20
src.act.more2=<uid>//ulozi flasku na memory item
src.act.equip

[itemdef i_potion_poison_timer]
ID=i_memory
TYPE=t_eq_script
name=poisoning

on=@equip
target Co chces otravit?
return 1

on=@userdclick
target Co chces otravit?
return 1

on=@targon_item
if (src.targ.tag(jed_sila))
 if  (src.targ.type==t_weapon_arrow) || (src.targ.type==t_weapon_bolt)
  src.sysmessage("Tyhle sipky uz jsou otravene.")
  return 0
 endif
endif
if (<src.targ.topobj>!=<src>)
 src.sysmessage Vec musis mit u sebe.
 dclick
 return 1
elseif (src.targ.amount>50)
 src.sysmessage("Na takove mnozstvi jedna lahvicka nestaci.")
 dclick
 return 1
elseif (src.targ.isarrow)||(src.targ.isbolt)
 if !(src.targ.typedef.tdata2)
  src.sysmessage Na toto nemuzes aplikovat jed.
  dclick
  return 1
 endif
elseif (nastaveni_weappoison_<src.targ.typedef.dispid>)
else
 src.sysmessage Na toto nemuzes aplikovat jed.
 dclick
 return 1
endif
link=<src.targ>//linkne se na zbran
if (src.targ.isarrow)||(src.targ.isbolt)
 if (cont.poisoning > {0 1000})
  morex=1
 endif
elseif (cont.poisoning > <eval <return_nth(1,<nastaveni_weappoison_<src.targ.typedef.dispid>>)> + {0 100}>) //uspesne otraveni
 morex=1
endif
timerdelay(<promilerange(<cont.poisoning>,<skill_poisoning.delay>)>)
more=1
return 1

on=@targon_char
dclick
return 1

on=@timer
if (more2)
else
 remove
 return 1
endif
if (more)
 if (morex)
  if (link.isarrow)||(link.isbolt)
   cont.newitemsafe(<link.id>_poison)
   lastnew.amount=<link.amount>
   lastnew.cont=<link.cont>
   lastnew.color=<finduid(<more2>).color>
   link.remove
   link=<lastnew>
   link.tag(jed_sila,(<eval {<nastaveni_poison_<finduid(<more2>).id>>}> * <nastaveni_sip_jed>) /100)
  else
   link.events=+e_poisonitem//event na zbran
   //sila zbrane z nastaveni a pripadnyho postihu/bonusu na zbran
   link.tag(jed_sila,( <eval {<nastaveni_poison_<finduid(<more2>).id>>}> * <return_nth(2,<nastaveni_weappoison_<link.typedef.dispid>>)>) /100)
   link.tag(jed_vydrz,<return_nth(3,<nastaveni_weappoison_<link.typedef.dispid>>)>)//kolikrat s ni lze otravit
   if (link.id==i_kudla)
    link.color=<finduid(<more2>).color>
   endif
  endif
  link.tag(jed_name,<finduid(<more2>).name>)
  link.tag(jed_typ,<finduid(<more2>).tag(jed_typ)>)//predej typ
  link.tag(jed_rychlost,<nastaveni_poison_<finduid(<more2>).tag(jed_typ)>>)//rychlost skytani
  cont.sysmessage("Uspesne jsi aplikoval jed.")
  if (6 == {0 10})
   if(finduid(<more2>).amount==1)
    finduid(<more2>).remove
   else
    finduid(<more2>).amount=<finduid(<more2>).amount>-1
   endif
   cont.sysmessage("Dosel jed")
  endif
 else
  cont.sysmessage Nepovedlo se ti aplikovat jed.
  if (<eval (1200-<cont.poisoning>)/12>>{0 1000})//otraveni sebe
   cont.sysmessage Pri manipulaci jsi se priotravil pouzitym jedem.
   cont.newitemsafe(i_memory_jed)
   cont.lastnew.tag(jed_sila,<eval {<nastaveni_poison_<finduid(<more2>).id>>}>)
   cont.lastnew.tag(jed_rychlost,<nastaveni_poison_<finduid(<more2>).tag(jed_typ)>>)
   cont.lastnew.tag(jed_typ,<<finduid(<more2>).tag(jed_typ)>)
   cont.equip(<lastnew>)
  endif
 endif
 if (link.isarrow)||(link.isbolt)//pokud je to sip, tak uz to bylo stejne smazany
 elseif (cont.poisoning < {0 1400})//flaska vyprazdnena
  cont.sysmessage Pri manipulaci jsi .znehodnotil lektvar.
  if(finduid(<more2>).amount==1)
   finduid(<more2>).remove
  else
   finduid(<more2>).amount=<finduid(<more2>).amount>-1
  endif
 endif
endif
remove
return 1

//effekty

[function poison_para]
if (resist_para)
 if (resist_para > argv(0))//sila
  return 1
 endif
endif
events -e_spelleffect
newitemsafe(i_parafield_delay)
lastnew.timer=1
lastnew.cont=<uid>
events +e_spelleffect

[function poison_fizz]
stopmagery
reveal
stopscriptedskills
damage 1
action=-1

[function poison_c]
arg(poison_dam,<eval (maxhits * argv(0))>)
damage(<eval arg(poison_dam)/100>,dam_god|dam_poison)
//say sila jedu <argv(0)>
//say ubiram  <eval arg(poison_dam)/100>

[function poison_stamina]
arg(poison_dam,<eval (maxstam * argv(0))>)
stamina=stamina-<eval arg(poison_dam)/100>

[function poison_feeblemind]
f_eqrune(i_a_feeblemind,nastaveni_poison_poison_feeblemind,<argv(0)>,3)

[function poison_weaken]
f_eqrune(i_a_weaken,nastaveni_poison_poison_weaken,<argv(0)>,3)

[function poison_clumsy]
f_eqrune(i_a_clumsy,nastaveni_poison_poison_clumsy,<argv(0)>,3)


[eof]