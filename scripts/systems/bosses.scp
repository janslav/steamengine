[events e_boss] 
on=@aftergetswing
hits=<maxhits>

on=@death //pro pøípad instantu
hits=<maxhits>
return 1 //neumøe

on=@beforegeteffect
on=@afterswing

[events e_bosses_guard]
on=@death
trigger(stopguarding)

on=@stopguarding
arg(boss,<finduid(<tag(myboss)>)>)
tag.remove(myboss)
events=-e_bosses_guard
arg(i,1)
while(i <= <eval boss.tag(guardscount)>)
 if(!strcmpi(<boss.tag(guardedby_<eval i>)>,<uid>))
  boss.tag.remove(guardedby_<eval i>)
  boss.tag(guardscount,<eval (<eval boss.tag(guardscount)>-1)>)
  //ostatni tagy vetsi nez i posuneme dolu :)
  arg(j,<i>)
  while(j <= <eval boss.tag(guardscount)>)
   boss.tag(guardedby_<eval j>,<boss.tag(guardedby_<eval j+1>)>)
   boss.tag.remove(guardedby_<eval j+1>)
   arg(j,#+1)
  endwhile
  arg(i,<eval boss.tag(guardscount)>+1) //aby se uz nepokracovalo ve whilu  
 endif
 arg(i,#+1)
endwhile
if(<eval boss.tag(guardscount)>==0) //nìco nenuloveho
 boss.tag.remove(guardscount)
 boss.events=-e_boss
endif

on=@beforegeteffect
on=@afterswing
on=@aftergetswing

[function stopguarding]
if(tag(myboss)) //je to spawn guarda nebo guard
 if(ischar)
  if(tag(myboss).flag_spawned) //je ze spawnu
   tag(myboss).makeboss //probehne pripadna kontrola neexistujicich guardskych spawnu
  endif
  kermel.sysmessage(guardscount: <tag(myboss).tag(guardscount)>, prvni: <tag(myboss).tag(guardedby_1)>, druhY: <tag(myboss).tag(guardedby_2)>)
  safe trigger(stopguarding) //a ukonci se guarding :)
 else //spawn
  arg(boss,<finduid(<tag(myboss)>)>)
  tag.remove(myboss)
  if(tag(lastspawned)) 
   if(tag(lastspawned).ischar) //odstranime guarda primo ze seznamu jeho soucasneho bosse
    arg(charboss,<tag(lastspawned).tag(myboss)>)
    if(charboss.flag_spawned)
     charboss.makeboss //opravi pripadne chyby u spawnuteho bosse 
    endif
    finduid(<tag(lastspawned)>).tag.remove(myboss)
    finduid(<tag(lastspawned)>).events=-e_bosses_guard
    arg(i,1)
    while(i <= <eval charboss.tag(guardscount)>)
     if(!strcmpi(<charboss.tag(guardedby_<eval i>)>,<tag(lastspawned)>))
      charboss.tag.remove(guardedby_<eval i>)
      charboss.tag(guardscount,<eval (<boss.tag(guardscount)>-1)>)      
      arg(j,<i>)
      while(j <= <eval charboss.tag(guardscount)>) //posunou se ostatni charbossovy tagy o jedno dolu na uprazdnene misto
       charboss.tag(guardedby_<eval j>,<boss.tag(guardedby_<eval j+1>)>)
       charboss.tag.remove(guardedby_<eval j+1>)
       arg(j,#+1)
      endwhile
      arg(i,<eval charboss.tag(guardscount)>) //aby se uz nepokracovalo  
     endif
     arg(i,#+1)
    endwhile
    if(<eval charboss.tag(guardscount)> == 0)
     charboss.tag.remove(guardscount)
     charboss.events=-e_boss
    endif
   endif
  endif
  arg(i,1)
  while(i <= <eval boss.tag(guardscount)>)
   if(!strcmpi(<boss.tag(guardedby_<eval i>)>,<uid>))
    boss.tag.remove(guardedby_<eval i>)
    boss.tag(guardscount,<eval (<boss.tag(guardscount)>-1)>)
    arg(j,<i>)
    while(j <= <eval boss.tag(guardscount)>) //ostatni tagy posunout o jedno dolu na uprazdnene misto 
     boss.tag(guardedby_<eval j>,<boss.tag(guardedby_<eval j+1>)>)
     boss.tag.remove(guardedby_<eval j+1>)
     arg(j,#+1)
    endwhile
    arg(i,<eval boss.tag(guardscount)>) //aby se uz nepokracovalo  
   endif
   arg(i,#+1)
  endwhile
  if(<eval boss.tag(guardscount)> == 0)
   boss.tag.remove(guardscount)
   boss.events=-e_boss
  endif  
 endif
 src.sysmessage(Strazce prestal hlidat sveho bosse)
endif
if(tag(guardscount)) //boss nebo spawn bosse
 if(ischar) //primo boss
  if(flag_spawned)
   makeboss //opravit pripadne chybky (provede se i na spawnu)
  endif
 else //spawn bosse
  if(tag(lastspawned))
   if(tag(lastspawned).ischar)
    tag(lastspawned).makeboss //pripadna oprava :)
   endif
  endif
 endif
 arg(i,1) 
 while(i <= <eval tag(guardscount)>)
  if(finduid(<tag(guardedby_<eval i>)>))
   finduid(<tag(guardedby_<eval i>)>).tag.remove(myboss) //toto odstranime vzdy (ze spawnu strazce nebo primo ze strazce)  
   if(!ischar) //spawn bosse konci bossovani - odstranit event a tag i ze spawnuteho strazce
    if(tag(guardedby_<eval i>).tag(lastspawned))
     if(tag(guardedby_<eval i>).tag(lastspawned).ischar)
      tag(guardedby_<eval i>).tag(lastspawned).tag.remove(myboss)
      tag(guardedby_<eval i>).tag(lastspawned).events=-e_bosses_guard
     endif    
    endif
    //a z bose taky tagy
    tag(lastspawned).tag.remove(guardedby_<eval i>)
   endif
   if(finduid(<tag(guardedby_<eval i>)>).isevent(e_bosses_guard)) //toto projde jen kdyz se odstranuje primo z bosse a ne spawnu
    finduid(<tag(guardedby_<eval i>)>).events=-e_bosses_guard
   endif
   tag.remove(guardedby_<eval i>)
  else //jen odstranit
   tag.remove(guardedby_<eval i>)
  endif
  arg(i,#+1)
 endwhile
 if(ischar)  
  events=-e_boss
 else
  tag(lastspawned).events=-e_boss
  tag(lastspawned).tag.remove(guardscount)
 endif
 tag.remove(guardscount) //toto v obou pøípadech
 src.sysmessage(Boss prestal byt bossem)
endif


[function guardboss]
newitemsafe(i_boss_guard_targ)
lastnew.timer=30
lastnew.equip

[itemdef i_boss_guard_targ]
id=i_memory
name=Zamerovac pro udelani monstra strazcem bosse
type=t_eq_script

on=@timer
remove
return 1

on=@create
more1=1

on=@equip
target(Zamer monstrum nebo spawn monstra ktere bude strazcem)

on=@targon_ground
target(Zamer monstrum nebo spawn monstra ktere bude strazcem)

on=@targon_item
if(src.targ.type==t_spawn_char)
 if(<eval more1>==2) //zamereni spawnu bosse
  if(!strcmpi(<src.targ>,<tag(guardingspawn)>)) //zameril sebe sama
   src.sysmessage(Kdyz s tim neumis tak na to nesahej! Zamer znova!)
   target(Zamer spawn noveho bosse)
   return 1
  endif
  if(src.targ.tag(guardscount))
   src.targ.tag(guardscount,< <eval src.targ.tag(guardscount)>+1>)
  else
   src.targ.tag(guardscount,1)  
  endif
  src.targ.tag(guardedby_<eval src.targ.tag(guardscount)>,<tag(guardingspawn)>) //ulozime guarduv spawn
  //nyni osetrime guarduv spawn a pripadne spawnute monstra 
  arg(guardspawn,<finduid(<tag(guardingspawn)>)>)
  guardspawn.tag(myboss,<src.targ>) //vazba guardova spawnu na bossuv spawn
  if(guardspawn.tag(lastspawned))
   if(guardspawn.tag(lastspawned).ischar) //mame neco spawnuteho
    if(src.targ.tag(lastspawned))
     if(src.targ.tag(lastspawned).ischar) //mame spawnuteho bosse
      guardspawn.tag(lastspawned).makeguard(<src.targ.tag(lastspawned)>)
      src.targ.tag(lastspawned).makeboss(<guardspawn.tag(lastspawned)>)
     endif
    endif
   endif
  endif    
  src.sysmessage(Boss uspesne zameren!)
  remove
  return 1
 endif
 if(<eval more1>==1) //zamereni spawnu
  if(src.targ.tag(myboss)) //uz neco hlida
   src.sysmessage(Spawn uz neco hlida! Zamer znova!)
   target(Zamer monstrum nebo spawn monstra ktere bude strazcem)
   return 1
  endif
  more1=2
  tag(guardingspawn,<src.targ>)
  target(Zamer spawn noveho bosse) 
  return 1
 else //ma tam treba nulu
  target(Asi zamerujes blbe! Zamer bosse!)
  return 1
 endif 
else
 target(Zamer monstrum nebo spawn monstra ktere bude <qval(<eval <more1>-1>,bossem,strazcem)>)
 return 1
endif

on=@targon_char
if(src.targ.npc)
 if(<more1>==0) //zamereni noveho bosse
  if(src.targ==<tag(newguard)>) //zameril sebe sama
   src.sysmessage(Kdyz s tim neumis tak na to nesahej! Zamer znova!)
   target(Zamer noveho bosse)
   return 1
  endif
  finduid(<tag(newguard)>).makeguard(<src.targ>) //vazba guarda na bosse
  src.targ.makeboss(<finduid(<tag(newguard)>)>) //vazba bosse na guarda
  src.sysmessage(Boss uspesne zameren!)
  remove
  return 1
 endif
 if(<more1>==1) //zamereni strazce
  if(src.targ.tag(myboss)) //uz hlida
   src.sysmessage(Monsturm uz hlida! Zamer znova!)
   target(Zamer monstrum nebo spawn monstra ktere bude strazcem)
   return 1
  endif
  more1=0
  tag(newguard,<src.targ>)
  target(Zamer noveho bosse)
  return 1
 else //ma tam treba tu dvojku
  target(Asi zamerujes blbe! Zamer spawn bosse!)
  return 1
 endif 
else
 target(Zamer monstrum nebo spawn monstra ktere bude strazcem)
 return 1
endif

[function makeboss]//arg - guard
//projedeme pro jistotu ostatni spawny (kdyz se spawne guards driv nez boss tak nehlida)
if(flag_spawned)
 arg(gem,<memoryfindtype(MEMORY_ISPAWNED).link>)
 tag(guardscount,0)
 arg(i,1)
 while(i <= <eval gem.tag(guardscount)>) //pro jistotu resetujeme vse z bosse
  tag.remove(guardedby_<eval i>)
  arg(i,#+1)
 endwhile
 arg(i,1)
 while(i <= <eval gem.tag(guardscount)>) //nyni samotna oprava
  if(gem.tag(guardedby_<eval i>).isitem) //existuje vubec ten spawn?
   if(gem.tag(guardedby_<eval i>).tag(lastspawned))
    if(gem.tag(guardedby_<eval i>).tag(lastspawned).ischar)
     if(gem.tag(guardedby_<eval i>).tag(lastspawned).hits!=0) //zrovna neumira (i kdyz jo tak muze existovat porad vazba)      
      tag(guardedby_<eval i>,<gem.tag(guardedby_<eval i>).tag(lastspawned)>)      
      tag(guardscount,<eval <tag(guardscount)>+1>)      
     endif
    endif
   endif
  else
   arg(j,<i>) //posuneme zbyle tagy o jeden zpet (tim zmizi ten neexistujici)
   while(j < <gem.tag(guardscount)>) 
    gem.tag(guardedby_<eval j>,<gem.tag(guardedby_<eval j+1>)>)
    if(gem.tag(guardedby_<eval j+1>).tag(lastspawned))
     if(gem.tag(guardedby_<eval j+1>).tag(lastspawned).ischar) //je tma neco spawnuto      
      if(gem.tag(guardedby_<eval j+1>).tag(lastspawned).hits!=0) //zrovna neumira (i kdyz jo tak muze existovat porad vazba)
       tag(guardedby_<eval j>,<gem.tag(guardedby_<eval j+1>).tag(lastspawned)>)  //opravime bosse            
       tag(guardscount,<eval <tag(guardscount)>+1>)       
      endif
     endif
    endif
    //gem.tag(guardedby_<eval j>,<gem.tag(guardedby_<eval j+1>)>)
    arg(j,#+1)
   endwhile   
   gem.tag.remove(guardedby_<eval j>) //ten posledni co zbyl
   gem.tag(guardscount,<eval <eval gem.tag(guardscount)>-1>)
  endif
  arg(i,#+1)
 endwhile 
 if(tag(guardscount))
  if(!isevent(e_boss))
   events=+e_boss
  endif  
 else
  tag.remove(guardscount)
  events=-e_boss
 endif
else //jenom pridame posledniho
 if(!isevent(e_boss))
  events=+e_boss
 endif
 if(tag(guardscount))
  tag(guardscount,<eval <eval tag(guardscount)>+1>)
 else
  tag(guardscount,1)  
 endif
 tag(guardedby_<tag(guardscount)>,<argv(0)>)
endif

[function makeguard]//arg - boss
events=+e_bosses_guard //nastavit guarda
tag(myboss,<argv(0)>)