[function zprava]
if(src.findid(i_chatovadlo))//pro jistotu
 //src.sysmessage(Nejprve jsi mel dopsat predchozi zpravu) // tohle tady asi bejt nemusi zejo... odstranime to ale pokracujem ve scriptu - precejenom neco napsat chceme! a to ze uz ten dialog otevrenej je nam prece taky nevadi - pozn. Yavanna
 src.findid(i_chatovadlo).remove
endif
src.newitemsafe(i_chatovadlo)
lastnew.timer=1
lastnew.equip
src.input("Zadej zpravu:",tag.message,"")


[itemdef i_chatovadlo]
name=Odesilac message na chat
id=i_memory
type=t_eq_script

on=@create
attr_movenever=1
attr_invis=1

on=@timer
if(strcmpi("<cont.tag(message)>","")) 
 //cont.sysmessage(Zpravu mame: "<cont.tag(message)>")
 arg(zpr,"<cont.tag(message)>")
 cont.tag.remove(message)
 if (<strmatch("<zpr>","*(*")>)||(<strmatch("<zpr>","*[*")>)||(<strmatch("<zpr>","*]*")>)||(<strmatch("<zpr>","*<*")>)||(<strmatch("<zpr>","*{*")>)
  //cont.redmessage(Nepovolene znaky)
  cont.tag.remove(message)
  cont.accmsg(Nepovolene znaky zpravy: "<zpr>")
  remove
  return 1
 else
  if(cont.tag(realm)==1)&&(!cont.isgm) //gondor
   cont.goch("<zpr>")
   //cont.smsg(removing goch)
   remove
   return 1 
  endif
  if(cont.tag(realm)==2)&&(!cont.isgm) //mordor
   cont.evch("<zpr>")
   //cont.smsg(removing evch)
   remove
   return 1
  endif
 endif 
else
 cont.tag.remove(message)
 //cont.sysmessage(timer)
 timer=1
 return 1
endif
cont.tag.remove(message)
remove

[DIALOG d_chat_options]
0,0
//resizepic 0 0 5054 640 480 
//resizepic 10 10 3000 620 460 
resizepic 80 80 2620 400 350
page 0
text 230 105 1000 0
text 107 127 3 1
text 107 149 1000 2
button 210 149 2443 2443 1 0 1
text 220 151 1000 3
text 107 171 1000 4
button 210 171 2443 2443 1 0 2
text 220 173 1000 5
text 107 193 1000 6
button 210 193 2443 2443 1 0 3
text 220 195 1000 7
text 107 215 1000 8
button 210 215 2443 2443 1 0 4
text 220 217 1000 9
text 107 259 37 10
button 107 281 2440 2440 1 0 5
text 117 283 1000 11
button 107 303 2440 2440 1 0 6
text 117 305 1000 12
button 107 325 2440 2440 1 0 7
text 117 327 1000 13
button 107 347 2440 2440 1 0 8
text 117 349 1000 14
button 107 369 2440 2440 1 0 9
text 117 371 1000 15
button 277 281 2440 2440 1 0 10
text 287 283 1000 16
button 277 303 2440 2440 1 0 11
text 287 305 1000 17
button 277 325 2440 2440 1 0 12
text 287 327 1000 18
button 277 347 2440 2440 1 0 13
text 287 349 1000 19
button 277 369 2440 2440 1 0 14
text 287 371 1000 20
text 350 149 36 21
text 110 390 70 22
//button 300 395 0fa5 0fa7 1 0 15  //  k Citadele       
//text 360 395 152 23

[DIALOG d_chat_options TEXT]
Nastaveni chatu
Kanaly:
GONDOR Realm
<?qval(<eval TAG(chaton)>&chflag_good,On,Off)?>
MORDOR Realm
<?qval(<eval TAG(chaton)>&chflag_evil,On,Off)?>
slyset vse
<?qval(<eval TAG(chaton)>&chflag_all,On,Off)?>
GMChat
<?qval(<eval TAG(chaton)>&chflag_gm,On,Off)?>
Umlcovaci List: (Uved jmeno postavy kterou nechces slyset.)
<?TAG.MUTED1?>
<?TAG.MUTED2?>
<?TAG.MUTED3?>
<?TAG.MUTED4?>
<?TAG.MUTED5?>
<?TAG.MUTED6?>
<?TAG.MUTED7?>
<?TAG.MUTED8?>
<?TAG.MUTED9?>
<?TAG.MUTED10?>
<?TAG.MUTEDTEXT?>
Zprava se pise prikazem ".zprava"
//napis zpravu..

[DIALOG d_chat_options BUTTON] 
ONBUTTON=1
IF (<eval TAG(chaton)>&chflag_good)
  TAG(chaton,<eval TAG(chaton)>&~chflag_good)
else
  TAG(chaton,<eval TAG(chaton)|chflag_good>)
ENDIF
DIALOG=d_chat_options

ONBUTTON=2
IF (<eval TAG(chaton)>&chflag_evil)
  TAG(chaton,<eval TAG(chaton)>&~chflag_evil)
else
  TAG(chaton,<eval TAG(chaton)|chflag_evil>)
ENDIF
DIALOG=d_chat_options

ONBUTTON=3
IF (<eval TAG(chaton)>&chflag_all)
  TAG(chaton,<eval TAG(chaton)>&~chflag_all)
else
  TAG(chaton,<eval TAG(chaton)|chflag_all>)
ENDIF
DIALOG=d_chat_options

ONBUTTON=4
IF (<eval TAG(chaton)>&chflag_gm)
  TAG(chaton,<eval TAG(chaton)>&~chflag_gm)
else
  TAG(chaton,<eval TAG(chaton)|chflag_gm>)
ENDIF
DIALOG=d_chat_options

ONBUTTON=5
INPDLG TAG.MUTED1 20
RETURN 1

ONBUTTON=6
INPDLG TAG.MUTED2 20
RETURN 1

ONBUTTON=7
INPDLG TAG.MUTED3 20
RETURN 1

ONBUTTON=8
INPDLG TAG.MUTED4 20
RETURN 1

ONBUTTON=9
INPDLG TAG.MUTED5 20
RETURN 1

ONBUTTON=10
INPDLG TAG.MUTED6 20
RETURN 1

ONBUTTON=11
INPDLG TAG.MUTED7 20
RETURN 1

ONBUTTON=12
INPDLG TAG.MUTED8 20
RETURN 1

ONBUTTON=13
INPDLG TAG.MUTED9 20
RETURN 1

ONBUTTON=14
INPDLG TAG.MUTED10 20
RETURN 1

//ONBUTTON=15
//zprava
//RETURN 0

[FUNCTION chat]
DIALOG d_chat_options
RETURN 1

[FUNCTION All]
findid(i_chatovadlo).remove //pro jistotu
IF !(<eval TAG(chaton)>&chflag_all)
 SYSMESSAGE You are not currently on that channel.
 RETURN 1
ELSEIF !(<strmatch("<args>","*(*")>)//&&!(<strmatch("<args>","*,")>)
 var(chatmessage,"*<?src.name?>* <?args?>")
 f_chatsend(1)
ELSE
 src.redmessage(Nepovolene znaky)
ENDIF

[FUNCTION gmch]
findid(i_chatovadlo).remove //pro jistotu
IF !(<eval TAG(chaton)>&chflag_gm)
	SYSMESSAGE You are not currently on that channel.
	RETURN 1
ELSEIF !(<strmatch("<args>","*(*")>)//&&!(<strmatch("<args>","*,*")>)
 var(chatmessage,"*<?src.name?>* <?args?>")
 f_chatsend(2)
ELSE
 src.redmessage(Nepovolene znaky)
ENDIF

[FUNCTION goch]
findid(i_chatovadlo).remove //pro jistotu
accmsg(GOCH: <args>)
if (tag(realm)!=1)&&(isgm==0)
  return 1
elseIF !(<eval TAG(chaton)>&chflag_good)
	SYSMESSAGE You are not currently on that channel.
	RETURN 1
ENDIF
if(<src.tag(hypno_mystik)>)
 src.sysmessage(Ve svem stavu nemuzes pouzivat masovou komunikaci)
 return 1
endif
if (f_chatcheck)
  return 0
endif
if !(<strmatch("<args>","*(*")>)
 var(chatmessage,"*<?src.name?>* <?args?>")
 f_chatsend(3)
ELSE
 src.redmessage(Nepovolene znaky)
endif

[FUNCTION evch]
findid(i_chatovadlo).remove //pro jistotu
accmsg(EVCH: <args>)
if (tag(realm)!=2)&&(isgm==0)
  return 1
elseIF !(<eval TAG(chaton)>&chflag_evil)
	SYSMESSAGE You are not currently on that channel.
	RETURN 1
ENDIF
if(<src.tag(hypno_mystik)>)
 src.sysmessage(Ve svem stavu nemuzes pouzivat masovou komunikaci)
 return 1
endif
if (f_chatcheck)
  return 0
endif
if !(<strmatch("<args>","*(*")>)
 var(chatmessage,"*<?src.name?>* <?args?>")
 f_chatsend(4)
ELSE
 src.redmessage(Nepovolene znaky)
ENDIF

[function f_chatcheck]
IF !(TAG(MUTED))
	TAG(MUTED,"")
	return 0
endif
IF (TAG(MUTED))
	SYSMESSAGE You try to chat, but find yourself mute!
//elseIF (FINDID(i_chat_timer))
//	SYSMESSAGE You must wait <FINDID(i_chat_timer)TIMER> seconds before sending another chat message.
ENDIF
return 1

[function f_chatsend]
NEWITEMsafe(i_chat_timer)
lastnew.p=<p>
lastnew.TIMERd=1
lastnew.removefromview
arg(color,<chatcolor[<argv(0)>]>)
arg(length,<StrLen(<chatmessage>)>+45)
if (arg(length)>83+45)
 arg(length,<eval 83+45>)
endif
//logmsg(<chatmessage>)
lastnew.tag(packet,01c <?HVAL (<arg(length)>&0ff00)/0100?> <?HVAL <arg(length)>&0ff?> 00 00 00 00 00 00 02 <?HVAL (<arg(color)>&0ff00)/0100?> <?HVAL <arg(color)>&0ff?> 00 03 053 079 073 074 065 06d 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 <?strtoascii(<eval <arg(length)>-45>,"<chatmessage>")?> 00)
//logmsg(<packet>)
var(chatmode,<args>)
var(chatsrcname,<name>)

[ITEMDEF i_chat_timer]
ID=i_memory
TYPE=t_eq_script
NAME="Chat Timer"

ON=@TIMER
var(packet,"<tag(packet)>")
serv.ALLCLIENTS(f_chatrecieve)
remove
RETURN 1

[FUNCTION f_chatrecieve]
IF (STRCMPi(<var(chatsrcname)>,<TAG(MUTED1)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED2)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED3)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED4)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED5)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED6)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED7)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED8)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED9)>)==0)
elseif (STRCMPi(<var(chatsrcname)>,<TAG(MUTED10)>)==0) 
else
  if (var(chatmode)==1)&&(<eval TAG(chaton)>&chflag_all)
  elseif (var(chatmode)==2)&&(account.plevel>=4) && (<eval TAG(chaton)>&chflag_gm)
  elseif (var(chatmode)==3)&&(<eval TAG(chaton)>&chflag_good)&&((tag(realm)==1)||(account.plevel>=4))
  elseif (var(chatmode)==4)&&(<eval TAG(chaton)>&chflag_evil)&&((tag(realm)==2)||(account.plevel>=4))
  else
   return 1
  ENDIF
  sendpacket(<var(packet)>)
endif      


[FUNCTION mute]
newitem i_muteitem
equip(act)

[FUNCTION mutemenu]
IF (0<TAG.MUTED> == 1)
	TAG(MUTED,0)
	RETURN 1
ENDIF
TAG(MUTED,1)
RETURN 1

[itemdef i_muteitem] 
id=i_memory
name=muteitem
type=t_eq_script 
layer=30 

on=@timer
remove
return 0

on=@equip 
target Vyber cil.

on=@targon_char
IF (0<SRC.TARG.TAG.MUTED> == 1)
	src.TARG.TAG(MUTED,0)
	src.SYSMESSAGE <SRC.TARG.NAME> unmuted.
	REMOVE
	RETURN 1
ENDIF
SRC.SYSMESSAGE <SRC.TARG.NAME> muted.
src.targ.TAG(MUTED,1)
REMOVE
RETURN 1

[defnames chatcolors] 
chatcolor[1]	044 //zelena
chatcolor[2]	021f //hneda jak hovno, neviditelna
chatcolor[3]	0480//0487//064//0480//05d//064//0983//099e//0481//06a//084a //modra
chatcolor[4]	026 //cervena

chflag_good	001
chflag_evil	002
chflag_all	004
chflag_gm	008    
chflag_guild	001

[eof]


