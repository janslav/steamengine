[function f_gq002_checkOldTags]
if ( (src.tag(gq002) == 1) && (<src.rescount i_gq002_radlice0> >= 1) )
  src.qtag(gq002,3)
elseif (src.tag(gq002) == 1)
  src.qtag(gq002,1)
elseif (src.tag(gq002) == 2)
  src.qtag(gq002,3)  
  src.qtag(gq002_kovar,1)
elseif (src.tag(gq002) == 3)
  src.qtag(gq002,3)  
  src.qtag(gq002_kovar,2)
elseif (src.tag(gq002) == 4)
  src.qtag(gq002,3)
  src.qtag(gq002_kovar,3)
elseif (src.tag(gq002) == 5)
  src.qtag(gq002,4)
elseif (src.tag(gq002) == 6)
  src.qtag(gq002,5)
elseif (src.tag(gq002) == 7)
  src.qtag(gq002,6)
endif
src.tag.remove(gq002)
if (src.tag(gq002a))
  src.tag.remove(gq002a)
  src.qtag(gq002,2)
endif



////////////////////////////////////////////////
//BOREK START
////////////////////////////////////////////////

[dialog d_gq002_borek]
5,5
page 0
resizepic 20 200 2620 640 350 
gumppic 30 210 03182 // podklad pod ksichtiky	
gumppic 40 215 03118 // ksichtik   3000-3164
text 220 210 52 0     // Jmeno a titul pozdraveneho
button 160 330 0988 0988 0 1 0  // Kdo jsi ?     
text 200 332 1152 1   // 
button 325 330 0988 0988 0 2 0  // Muzu pomoct ? 
text 350 330 1152 2    //
button 490 330 0988 0988 0 3 0  // Obchodovat ?  
text 510 330 1152 3   //
button 160 360 0988 0988 0 4 0  // Hledam praci  
text 190 360 1152 4   //
button 325 360 0988 0988 0 5 0  // Nesu ti ...   
text 350 360 1152 5   //
button 490 360 0988 0988 0 6 0  // Prohlednout ...      
text 520 360 1152 6   //


page 1
HTMLGUMP(170,230,440,100,7,0,36)


page 2
HTMLGUMP(170,230,440,100,8,0,36)
button 25 395 0fa5 0fa7 1 0 1  //      
text 60 395 152 13
button 25 415 0fa5 0fa7 0 7 0  //         
text 60 415 152 14

page 3
HTMLGUMP(170,230,440,100,9,0,36)


page 4
HTMLGUMP(170,230,440,100,10,0,36)


page 5
HTMLGUMP(170,230,440,100,11,0,36)


page 6
HTMLGUMP(170,230,440,100,12,0,36)


page 7
HTMLGUMP(170,230,440,100,15,0,36)


[dialog d_gq002_borek text]
Chudy Zemedelec Borek
Kdo jsi?
Muzu pomoci?
Obchodovat?
Hledam praci
Nesu ti
Prohlednout
Vitej! Jmenuju se Borek a jsem zemedelec. Mam tady male policko a snazim se nejak uzivit. Nekdy je to ale tezke... sakra tezke.
Ani ne. Moje policko je male, stacim na nej sam. I kdyz... snad bys mi preci jen mohl pomoci. Podivej se na muj pluh...<BR> Byl skoro novy a ted je k nepotrebe. Praskla radlice. Potreboval bych ji zanest ke kovari, aby mi ji spravil. Ma svoji dilnu dole pod Hurkou, ale je to prilis daleko a ja tu nemohu svouj pozemek nechat jen tak bez dozoru.<BR> Ty vypadas, ze se svetem toulas jen tak nazdarbuh a casu mas dost. Mozna bys mi mohl pomoci, co rikas?
Nemam co bych ti nabidl. Vse co sklidim ihned prodavam na trhu.
Mno ja ti praci bohuzel nenabidnu. Jsem rad, ze uzivim sam sebe...
Copak pro me mas?
Borek je mensiho vzrustu, podsadity, svalnaty. Tvar strhanou vecnou praci a ruce plne mozolu. V jeho ocich vsak presto nachazis jiskricku hravosti a veseli ze zivota.
Dobre, rad to pro tebe udelam.
Nene, tohle po me nechtej!
Dobra cizince, ale tvou vstricnost si budu pamatovat...


[dialog d_gq002_borek button]
on=1
if (<src.rescount i_gq002_radlice0> == 1)
 SAY("Mam jen jeden pluh na opravu.")
elseif (<src.rescount i_gq002_radlice1> == 1)
 SAY("Mam jen jeden pluh na opravu.")
elseif (0<src.qtag(gq002)> ==0)
 src.qtag(gq002,1)
 SAY("Diky moc, vymontuj prosim radlici z pluhu.")
elseif (0<src.qtag(gq002)> >0)
 SAY("Uz jsem ti prece rikal, ze mas vymontovat radlici z pluhu.")
endif


////////////////////////////////////////////////
//BOREK GUMP2
////////////////////////////////////////////////


[dialog d_gq002_borek2]
5,5
page 0
resizepic 20 200 2620 640 350 
gumppic 30 210 03182 // podklad pod ksichtiky	
gumppic 40 215 03118 // ksichtik   3000-3164
text 220 210 52 0     // Jmeno a titul pozdraveneho
button 160 330 0988 0988 0 1 0  // Kdo jsi ?     
text 200 332 1152 1   // 
button 325 330 0988 0988 0 2 0  // Muzu pomoct ? 
text 350 330 1152 2    //
button 490 330 0988 0988 0 3 0  // Obchodovat ?  
text 510 330 1152 3   //
button 160 360 0988 0988 0 4 0  // Hledam praci  
text 190 360 1152 4   //
button 325 360 0988 0988 0 5 0  // Nesu ti ...   
text 350 360 1152 5   //
button 490 360 0988 0988 0 6 0  // Prohlednout ...      
text 520 360 1152 6   //


page 1
HTMLGUMP(170,230,440,100,7,0,36)


page 2
HTMLGUMP(170,230,440,100,8,0,36)
button 25 395 0fa5 0fa7 0 7 0  // kde je kovar?
text 60 395 152 15
button 25 415 0fa5 0fa7 0 8 0  // jak vymontovat radlici?
text 60 415 152 17


page 3
HTMLGUMP(170,230,440,100,9,0,36)


page 4
HTMLGUMP(170,230,440,100,10,0,36)


page 5
HTMLGUMP(170,230,440,100,11,0,36)


page 6
HTMLGUMP(170,230,440,100,12,0,36)


page 7
HTMLGUMP(170,230,440,100,16,0,36)


page 8
HTMLGUMP(170,230,440,100,18,0,36)
button 25 395 0fa5 0fa7 1 0 1  //         
text 60 395 152 19


[dialog d_gq002_borek2 text]
Chudy Zemedelec Borek
Kdo jsi?
Muzu pomoci?
Obchodovat?
Hledam praci
Nesu ti
Prohlednout
Tak co moje radlice? uz jsi ji spravil?
Jo. Prines mi zpet tu opravenou radlici.
Nemam co bych ti nabidl. Vse co sklidim ihned prodavam na trhu.
Mno ja ti praci bohuzel nenabidnu. Jsem rad, ze uzivim sam sebe...
Copak pro me mas?
Borek je mensiho vzrustu, podsadity, svalnaty. Tvar strhanou vecnou praci a ruce plne mozolu. V jeho ocich vsak presto nachazis jiskricku hravosti a veseli ze zivota.
Dobre, rad to pro tebe udelam.
Nene, tohle po me nechtej!
Kde jsi rikal, ze sidli ten kovar?
Ma svou dilnu jizne od Hurky. Je to trochu samotar, ale sve remeslo ovlada skvele! Jinemu bych sve naradi nesveril.
Jak vymontuji tu radlici ?
Pluh lezi tady kousek na poli. Je tam na strane mala pojistka staci ji vytahnout a pak uz ta radlice pujde s trochou usili ven.
Aha! Diky ted uz to snad pujde...


[dialog d_gq002_borek2 button]
on=1
if (<src.rescount i_gq002_radlice0> == 1)
 SRC.SYSMESSAGE="Sice uz jsi radlici vymontoval, ale aspon vis, ze jsi to udelal spravne"
elseif (<src.rescount i_gq002_radlice1> == 1)
 SRC.SYSMESSAGE="Sice uz jsi radlici vymontoval, ale aspon vis, ze jsi to udelal spravne"
elseif (0<src.qtag(gq002)> ==1)
 src.qtag(gq002,2)
 SAY A neboj se za to trosku zabrat. Vic uz to stejne rozbit nejde.
elseif (0<src.qtag(gq002)> ==2)
 SRC.SYSMESSAGE="Uz vis, jak na to."
ENDIF


////////////////////////////////////////////////
//BOREK GUMP3
////////////////////////////////////////////////


[dialog d_gq002_borek3]
5,5
page 0
resizepic 20 200 2620 640 350 
gumppic 30 210 03182 // podklad pod ksichtiky	
gumppic 40 215 03118 // ksichtik   3000-3164
text 220 210 52 0     // Jmeno a titul pozdraveneho
button 160 330 0988 0988 0 1 0  // Kdo jsi ?     
text 200 332 1152 1   // 
button 325 330 0988 0988 0 2 0  // Muzu pomoct ? 
text 350 330 1152 2    //
button 490 330 0988 0988 0 3 0  // Obchodovat ?  
text 510 330 1152 3   //
button 160 360 0988 0988 0 4 0  // Hledam praci  
text 190 360 1152 4   //
button 325 360 0988 0988 0 5 0  // Nesu ti ...   
text 350 360 1152 5   //
button 490 360 0988 0988 0 6 0  // Prohlednout ...      
text 520 360 1152 6   //


page 1
HTMLGUMP(170,230,440,100,7,0,36)


page 2
HTMLGUMP(170,230,440,100,8,0,36)


page 3
HTMLGUMP(170,230,440,100,9,0,36)


page 4
HTMLGUMP(170,230,440,100,10,0,36)


page 5
HTMLGUMP(170,230,440,100,11,0,36)
button 25 395 0fa5 0fa7 0 7 0  //  nechal jsem zpravit radlici
text 60 395 152 13
button 25 415 0fa5 0fa7 0 1 0  //  vlastne nic
text 60 415 152 14


page 6
HTMLGUMP(170,230,440,100,12,0,36)


page 7
HTMLGUMP(170,230,440,100,15,0,36)



[dialog d_gq002_borek3 text]
Chudy Zemedelec Borek
Kdo jsi?
Muzu pomoci?
Obchodovat?
Hledam praci
Nesu ti
Prohlednout
Tak co moje radlice? uz jsi ji spravil?
Jo. Prines mi zpet tu opravenou radlici.
Nemam co bych ti nabidl. Vse co sklidim ihned prodavam na trhu.
Mno ja ti praci bohuzel nenabidnu. Jsem rad, ze uzivim sam sebe...
Copak pro me mas?
Borek je mensiho vzrustu, podsadity, svalnaty. Tvar strhanou vecnou praci a ruce plne mozolu. V jeho ocich vsak presto nachazis jiskricku hravosti a veseli ze zivota.
Uz jsem <?src.sex(nechal,nechala)?> spravit tu radlici.
Vlastne nic...
Tak ji prosim te namontuj zpet na pluh.


[dialog d_gq002_borek3 button]


////////////////////////////////////////////////
//BOREK GUMP4
////////////////////////////////////////////////


[dialog d_gq002_borek4]
5,5
page 0
resizepic 20 200 2620 640 350 
gumppic 30 210 03182 // podklad pod ksichtiky	
gumppic 40 215 03118 // ksichtik   3000-3164
text 220 210 52 0     // Jmeno a titul pozdraveneho
button 160 330 0988 0988 0 1 0  // Kdo jsi ?     
text 200 332 1152 1   // 
button 325 330 0988 0988 0 2 0  // Muzu pomoct ? 
text 350 330 1152 2    //
button 490 330 0988 0988 0 3 0  // Obchodovat ?  
text 510 330 1152 3   //
button 160 360 0988 0988 0 4 0  // Hledam praci  
text 190 360 1152 4   //
button 325 360 0988 0988 0 5 0  // Nesu ti ...   
text 350 360 1152 5   //
button 490 360 0988 0988 0 6 0  // Prohlednout ...      
text 520 360 1152 6   //


page 1
HTMLGUMP(170,230,440,100,7,0,36)


page 2
HTMLGUMP(170,230,440,100,8,0,36)
button 25 395 0fa5 0fa7 1 0 1
text 60 395 152 13


page 3
HTMLGUMP(170,230,440,100,9,0,36)


page 4
HTMLGUMP(170,230,440,100,10,0,36)


page 5
HTMLGUMP(170,230,440,100,11,0,36)

page 6
HTMLGUMP(170,230,440,100,12,0,36)



[dialog d_gq002_borek4 text]
Chudy Zemedelec Borek
Kdo jsi?
Muzu pomoci?
Obchodovat?
Hledam praci
Nesu ti
Prohlednout
Tak co pluh? Zdarila se ti oprava? Ceka mne spousta prace.
Ano. Namontuj prosim tu opravenou radlici na pluh.
Nemam co bych ti nabidl. Vse co sklidim ihned prodavam na trhu.
Mno, ja ti praci nenabidnu. Jsem rad, ze uzivim sam sebe.
Copak pro me mas?
Borek je mensiho vzrustu, podsadity, svalnaty. Tvar strhanou vecnou praci a ruce plne mozolu. V jeho ocich vsak presto nachazis jiskricku hravosti a veseli ze zivota.
Uz jsem <?src.sex(spravil,spravila)?> ten pluh.


[dialog d_gq002_borek4 button]
on=1
// button opravil jsem pluh
//
if (0<src.qtag(gq002)>==5)
 SAY(Dekuji. Tady mas odmenu.)
 src.qtag(gq002,6)
 src.givegold(500) 
 src.addexp(50+<eval(<src.tag(level)>*10)>)
else
 SAY("Jeste ti neco chybi udelat.")
endif



//////////////////////////////////////////////////////
//BOREK FINISH
//////////////////////////////////////////////////////


[dialog d_gq002_borekfinish]
5,5
page 0
resizepic 20 200 2620 640 350 
gumppic 30 210 03182 // podklad pod ksichtiky	
gumppic 40 215 03118 // ksichtik   3000-3164
text 220 210 52 0     // Jmeno a titul pozdraveneho
button 160 330 0988 0988 0 1 0  // Kdo jsi ?     
text 200 332 1152 1   // 
button 325 330 0988 0988 0 2 0  // Muzu pomoct ? 
text 350 330 1152 2    //
button 490 330 0988 0988 0 3 0  // Obchodovat ?  
text 510 330 1152 3   //
button 160 360 0988 0988 0 4 0  // Hledam praci  
text 190 360 1152 4   //
button 325 360 0988 0988 0 5 0  // Nesu ti ...   
text 350 360 1152 5   //
button 490 360 0988 0988 0 6 0  // Prohlednout ...      
text 520 360 1152 6   //


page 1
HTMLGUMP(170,230,440,100,7,0,36)


page 2
HTMLGUMP(170,230,440,100,8,0,36)


page 3
HTMLGUMP(170,230,440,100,9,0,36)


page 4
HTMLGUMP(170,230,440,100,10,0,36)


page 5
HTMLGUMP(170,230,440,100,11,0,36)   


page 6
HTMLGUMP(170,230,440,100,12,0,36)


[dialog d_gq002_borekfinish text]
Chudy Zemedelec Borek
Kdo jsi?
Muzu pomoci?
Obchodovat?
Hledam praci
Nesu ti
Prohlednout
Ahoj priteli, rad te znovu vidim.
Ani ne. Moje policko je male, stacim na nej sam... Dekuji za nabidku priteli.
Nemam co bych ti nabidl. Vse co sklidim ihned prodavam na trhu.
Mno ja ti praci bohuzel nenabidnu. Jsem rad, ze uzivim sam sebe...
Copak pro me mas?
Borek je mensiho vzrustu, podsadity, svalnaty. Tvar strhanou vecnou praci a ruce plne mozolu. V jeho ocich vsak presto nachazis jiskricku hravosti a veseli ze zivota.

[dialog d_gq002_borekfinish button]







//////////////////////////////////////////////////////
//KOVAR GORUG
//////////////////////////////////////////////////////



[dialog d_gq002_kovar]
5,5
page 0
resizepic 20 200 2620 640 350 
gumppic 30 210 03182 // podklad pod ksichtiky	
gumppic 40 215 03022 // ksichtik   3000-3164
text 220 210 52 0     // Jmeno a titul pozdraveneho
button 160 330 0988 0988 0 1 0  // Kdo jsi ?     
text 200 332 1152 1   // 
button 325 330 0988 0988 0 2 0  // Muzu pomoct ? 
text 350 332 1152 2    //
button 490 330 0988 0988 0 3 0  // Obchodovat ?  
text 520 332 1152 3   //
button 160 360 0988 0988 0 4 0  // Hledam praci  
text 190 362 1152 4   //
button 325 360 0988 0988 0 5 0  // Nesu ti ...   
text 365 362 1152 5   //
button 490 360 0988 0988 0 6 0  // Prohlednout ...      
text 520 362 1152 6   //


page 1
if (0<src.qtag(gq002_kovar)> == 0)
  argo.HTMLGUMP(170,230,440,100,7,0,36)
elseif (0<src.qtag(gq002_kovar)> < 3)
  argo.HTMLGUMP(170,230,440,100,16,0,36)
else
  argo.HTMLGUMP(179,230,440,100,17,0,36)
endif
if (src.qtag(q022)==2)
  argo.button(25,395,0fa5,0fa7,0,10,0)
  argo.text(60,395,152,30)
endif


page 2
if (0<src.qtag(gq002_kovar)> == 0)
  argo.HTMLGUMP(170,230,440,100,8,0,36)
  argo.button(25,395,0fa5,0fa7,1,0,1) 	// donest kladivo  
  argo.text(60,395,152,13)
elseif (0<src.qtag(gq002_kovar)> < 3)
  argo.HTMLGUMP(170,230,440,100,18,0,36)
  argo.button(25,395,0fa5,0fa7,0,9,0)	// kde spi trolove ?
  argo.text(60,395,152,26)
else
  argo.HTMLGUMP(170,230,440,100,19,0,36)
endif

page 3
if (0<src.qtag(gq002_kovar)> < 3)
  argo.HTMLGUMP(170,230,440,100,9,0,36)
else
  argo.HTMLGUMP(170,230,440,100,20,0,36)
endif

page 4		//hledam praci
if (0<src.qtag(gq002_kovar)> < 3)
  argo.HTMLGUMP(170,230,440,100,10,0,36)
else
  argo.HTMLGUMP(170,230,440,100,21,0,36)
endif


page 5		//nesu ti
argo.HTMLGUMP(170,230,440,100,11,0,36)
arg(buttonY,395)
if (<src.rescount i_gq002_kladivo> == 1)
  argo.button(25,<buttonY>,0fa5,0fa7,0,7,0)  //mam pro kovare jeho kladivo
  argo.text(60,<buttonY>+3,152,25)
  arg(buttonY,#+20)
endif
if ( (0<src.qtag(gq002)>==3)&&(<src.rescount i_gq002_radlice0> == 1) )
  argo.button(25,<buttonY>,0fa5,0fa7,0,8,0)  //mam radlici od borka a nabizim ji kovari
  argo.text(60,<buttonY>+3,152,14)
  arg(buttonY,#+20)
endif

page 6		//prohlednout
if (0<src.qtag(gq002_kovar)> < 3)
  argo.HTMLGUMP(170,230,440,100,12,0,36)
else
  argo.HTMLGUMP(170,230,440,100,22,0,36)
endif


page 7		//kladivo pro kovare
argo.HTMLGUMP(170,230,440,100,28,0,36)
argo.button(25,395,0fa5,0fa7,1,0,2)	//predat kladivo kovari
argo.text(60,398,142,24)

page 8		//radlice pro Borka
if (0<src.qtag(gq002_kovar)> <3)
  argo.HTMLGUMP(170,230,440,100,15,0,36)
else
  argo.HTMLGUMP(170,230,440,100,23,0,36)
  argo.button(25,395,0fa5,0fa7,1,0,3)	//predat Radlici na opravu
  argo.text(60,398,142,24)
endif

page 9		//kde spi trollove ?
argo.HTMLGUMP(170,230,440,100,27,0,36)

// Transport q022
page 10
argo.HTMLGUMP(170,230,440,100,31,0,36)
argo.button(25,395,0fa5,0fa7,1,0,4)
argo.text(60,398,142,32)

[dialog d_gq002_kovar text]
Kovar Gorug
Kdo jsi?				//1
Muzu pomoci?				//2
Obchodovat?				//3
Hledam praci				//4
Nesu ti					//5
Prohlednout				//6
HA! <?src.sex(Prisel,Prisla)?> jsi prave vcas ! Ja se jmenuji Gorug a jsem kovar. Nebudes verit, co se mi stalo. Ta parta mistnich Trollu je den ode dne drzejsi. Vadi jim, ze stale boucham kladivem do kovadliny. Pry chteji ve dne spat a prohlasuji, ze pracovat se ma v noci. Takova drzost! Vytrhal bych jim vsem hlavy a vystavil je na branach mesta! Jiste, ze jsem je s tim poslal do haje, ale oni se sem v noci prikradli a sebrali mi kladivo. Kladivo! MNE! MOJE KLADIVO! Grrrrrrrr! Kdybych si mel cim ukovat mec, pekne bych je prohnal! Uz s nimi nebudu mit zadne slitovani! Ghrrr!<br>  Potrebuji nekoho, kdo by jim ukazal, zac je toho loket!	// 7
Jiste muzes! Potrebuji, aby jsi mi <?src.sex(donesl,donesla)?> zpet me kladivo ! Co rikas? Ti proklati Trollove spi prez den tady kousek na Jih. Je to v lese na jedne mytine, tam by jsi se s nimi mel snadno vyporadat. A hlavne najdi moje kladivo!		// 8
Nechci obchodovat! Potrebuji svoje kladivo! Jak ma kovar pracovat bez kladiva?				//9
U me ji nenajdes. Sam nemam bez sveho kladiva co na praci!						//10
Copak pro me mas?			//11
Svalnaty, podmraceny s sibalskyma ocima. To je kovar Gorug. Kdyz se rozcili, nejlepsi je klidit se mu z cesty. A ted je velice rozzureny. Neustale mumla cosi o svem kladivu. 			//12
Dobre, donesu ti tve kladivo.		//13
Zemedelci Borkovi praskla radlice u pluhu, mohl bys mu ji opravit?					//14
A to do toho mam mlatit pestma? Nebo taky muzu pouzit tvoji hlavu! Bez naradi to opravit nejde ty <?src.sex(chytraku,chytra)?>! Grrrh...	//15
Tak co? Mas to kladivo ?!		//16
Bud zdrav! Jsem rad, ze te zase vidim. Jeste jednou dekuji za to kladivo...				//17
Slibil jsi mi, ze se postaras o navraceni meho kladiva! Vic mi pomoci nemuzes.				//18
Nenapada me nic, s cim by jsi mi mohl pomoci.								//19
Momentalne mam nejake zakazky. Zkus si prijit pozdeji.							//20
Bohuzel, ja te nezamestnam. Vsechno zvladam sam, a kdyz potrebuji nejakou drobnost, rad mi vypomuze nejaky mistni nadenik.			//21
Svalnaty, podmraceny s sibalskyma ocima. To je kovar Gorug. Kdyz se rozcili, nejlepsi je klidit se mu z cesty. Vypada, ze ma spoustu prace.	//22
Ale jiste! Bezevseho. A opravim ti ji zdarma, ze jsi mi pomohl. Stejne bych ti nerad neco dluzil... Dej ho sem.					//23
* Predat prasklou radlici kovari *	//24
Nesu ti tvoje kladivo !			//25
Kde jsi rikal, ze spi ti Trollove ?	//26
Poslouchas kolenem? Vsichni jste natvrdli jak stado vepru. Tady kus na jih je v lese mytina. A pospes si, nebo reknu nekomu jinemu!		//27
Vyborne ! Takze jsi ho nasel ! Konecne se mohu zase dat do prace...					//28
* Predat kladivo kovari *		//29
//Transport q022
Navin me posila pro zelezne ingoty. Potrebuje tak 150 kusu.	//30
Navin? Hmm, naposledy sme se pekne chytli... No uz me to preslo. Tady to je...	//31
* Vzit ingy *	//32



[dialog d_gq002_kovar button]
ON=1
src.qtag(gq002_kovar,1)
src.sysmessage("Prijmas ukol od kovare")
SAY("Skvele!")

ON=2
if (<src.rescount i_gq002_kladivo> == 1)
 SAY("Ohoooo. Ses pasak. Diky!")
 src.consume(i_gq002_kladivo)
 src.qtag(gq002_kovar,3)
 src.addexp(150+<eval(<src.tag(level)>*10)>)
else
 SAY "Kde mas moje kladivo? Ty jsi na nej zapomnel?!"
endif

ON=3
if (<src.rescount i_gq002_radlice0> == 1)
 SAY("Hmmm, parkrat do toho bouchnu tadyhle... a tady... hotovo!")
 src.consume(i_gq002_radlice0)
 src.giveitem(i_gq002_radlice1)
 src.qtag(gq002,4)
else
 SAY("He? Nechapu co po me chces! Kde mas tu radlici ?")
endif
ON=4
	src.giveitem(i_q022_ingoty)
	src.qtag(q022,3)




//////////////////////////////////////////////////////
//Speech Borek
//////////////////////////////////////////////////////


[speech spk_gq002_borek]
on = *ahoj*
on = *radlice*
on = *pluh*
on = *ukol*
on = *prace*
on = *den*

if (<distance>>3)
 SRC.SYSMESSAGE="Na takovou vzdalenost s tebou jednat nebude"
else
 f_gq002_Borek_DClick
endif

//////////////////////////////////////////////////////
//ON DCLICK+SPEECH Borek Function
//////////////////////////////////////////////////////

[function f_gq002_Borek_DClick]
f_gq002_checkOldTags
if (<src.flags>&statf_dead)
 return 1
elseif ( (<src.tag.realm>==2) || (0<src.qtag(mq002)> > 0) )
 say "Vyhni se mi obloukem, sluho Temneho Pana !"
 return 1
endif
if (0<src.qtag(gq002)>==6)
 DIALOG(d_gq002_borekfinish)
elseif (0<src.qtag(gq002)>==5)
 DIALOG(d_gq002_borek4)
elseif (0<src.qtag(gq002)>==4)
 DIALOG(d_gq002_borek3)
elseif (0<src.qtag(gq002)> >=1)
 DIALOG(d_gq002_borek2)
else
 DIALOG(d_gq002_borek)
endif


//////////////////////////////////////////////////////
//Speech Kovar
//////////////////////////////////////////////////////

[speech spk_gq002_kovar]
on = *ahoj*
on = *radlice*
on = *pluh*
on = *kladivo*
on = *ukol*
on = *prace*

if (<distance>>3)
 SRC.SYSMESSAGE="Na takovou vzdalenost s tebou jednat nebude"
else
 f_gq002_Kovar_DClick
endif



//////////////////////////////////////////////////////
//ON DCLICK+SPEECH Kovar Function
//////////////////////////////////////////////////////

[function f_gq002_Kovar_DClick]
f_gq002_checkOldTags
if (<src.flags>&statf_dead)
 return 1
elseif (<src.tag.realm>==2)
 say "Vyhni se mi obloukem, sluho Temneho Pana !"
 return 1
endif
DIALOG(d_gq002_kovar)
return 1




//////////////////////////////////////////////////////
//Questovni predmety - pluh
//////////////////////////////////////////////////////


[ITEMDEF i_gq002_pluh]
ID=01501
NAME=Pluh
TYPE=t_normal

CATEGORY=Moria Questy
SUBSECTION=gq002
DESCRIPTION=Pluh

on=@create 
attr=attr_move_never

ON=@userCLICK
src.sysmessage Stary znacne posramoceny pluh

on=@userdclick
f_gq002_checkOldTags
if (<src.flags>&statf_dead)
 return 1
elseif (<src.targ.distance> >1)
 src.sysmessage(Z takove dalky to nepujde)
 return 1
endif
if (<src.rescount i_gq002_radlice0> >= 1)
 src.sysmessage("Radlici musis nejdriv opravit nez ji budes moci namontovat zpatky.") 
elseif (<src.rescount i_gq002_radlice1> >= 1)&&(0<src.qtag(gq002)>==4)
 src.sysmessage("<src.sex(Namontoval,Namontovala)> jsi zpatky opravenou radlici.")
 src.consume(i_gq002_radlice1)
 src.qtag(gq002,5)
elseif (0<src.qtag(gq002)> ==1)
 src.sysmessage("Nevis, jak tu radlici vymontovat.") 
elseif (0<src.qtag(gq002)> ==2)
 src.sysmessage("<src.sex(Vymontoval,Vymontovala)> jsi z pluhu prasklou radlici.")
 src.giveitem(i_gq002_radlice0)
 src.qtag(gq002,3)
elseif (0<src.qtag(gq002)> >4)
 src.sysmessage("<src.sex(Udelal,Udelala)> jsi vse, co se dalo. Vic toho tady asi nezmuzes.")
else
 src.sysmessage("Tady nic nezmuzes, to je prace pro zkuseneho remeslnika.")
endif
return 1


//////////////////////////////////////////////////////
//Questovni predmety - Praskla radlice
//////////////////////////////////////////////////////

[ITEMDEF i_gq002_radlice0]
ID=01500
NAME=Praskla radlice
TYPE=t_normal

ON=@Create
attr=attr_move_never



ON=@userCLICK
src.sysmessage Stara, v pulce praskla, zelezna radlice
ON=@userDCLICK
src.sysmessage S timhle nic nesvedes.
return 1

[ITEMDEF i_gq002_radlice1]
ID=01500
NAME=Opravena radlice
TYPE=t_normal

ON=@Create
attr=attr_move_never



ON=@userCLICK
src.sysmessage Opravena zelezna radlice
ON=@userDCLICK
src.sysmessage S timhle nic nesvedes.
return 1



//////////////////////////////////////////////////////
//Questovni itemy - Bedna s lupem
//////////////////////////////////////////////////////


[ITEMDEF i_gq002_bedna]
ID=0e3c
NAME=Bedna s lupem
TYPE=t_normal

CATEGORY=Moria Questy
SUBSECTION=gq002
DESCRIPTION=Bedna s lupam

on=@create 
attr=attr_move_never

ON=@userCLICK
src.sysmessage Bedna se zmeti naloupenych veci

on=@userdclick 
f_gq002_checkOldTags
if (<src.flags>&statf_dead)
 return 1
elseif (src.isgm)
 target zamer spawn strazce bedny
 return 1
elseif (<src.targ.distance> >1)
 src.sysmessage("Z takove dalky to nepujde.")
 return 1
endif
if (link.more2)
 src.sysmessage("Trollove te nenechaji jen tak vlezt do te bedny.")
elseif (0<src.qtag(gq002_kovar)> > 1)
 src.sysmessage("Uz tu nic zajimaveho neni.")
elseif (0<src.qtag(gq002_kovar)>==1)
 src.sysmessage("Po chvili prehrabovani v bedne jsi neco nasel.")
 src.giveitem(i_gq002_kladivo)
 src.givegold(500)
 src.giveitem(random_FOOD,{5 10})
 src.giveitem(random_FOOD,{5 10})
 src.giveitem(RANDOM_REAGENT,{5 20})
 src.giveitem(RANDOM_REAGENT,{5 20})
 src.giveitem(RANDOM_REAGENT,{5 20})
 src.giveitem(RANDOM_REAGENT,{5 20})
 src.qtag(gq002_kovar,2)
else
 src.sysmessage("Nic zajimaveho tu nevidis.")
endif
return 1

ON=@TargOn_Char
src.sysmessage("Ee, musis oznacit spawnik.")
target
return 1

ON=@TargOn_Ground
src.sysmessage("Ee, musis oznacit spawnik.")
target
return 1

ON=@TargOn_Item
if (src.targ.type==t_spawn_char)
 link=<src.targ>
else
 src.sysmessage("Ee, musis oznacit spawnik.")
 target
endif
return 1

//////////////////////////////////////////////////////
//Questovni itemy - Kladivo
//////////////////////////////////////////////////////

[ITEMDEF i_gq002_kladivo]
ID=0fb4
NAME=Kovarske kladivo
TYPE=t_normal

ON=@Create
attr=attr_move_never


ON=@userCLICK
src.sysmessage Trpaslici kovarske kladivo
ON=@userDCLICK
src.sysmessage Na to nemas vlohy
return 1

[eof]