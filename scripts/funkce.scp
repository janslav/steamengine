[function isDefined]//arg - hodnota promìnné
if(safe(<eval (<args>)>))
 return 1
else
 return 0
endif

[function hashul]//args - material z nehoz ma hul bejt (nebo z lepsiho)
if(isgm)
 return 1
endif
if(findlayer(2))
 if(findlayer(2).isstaff)
  if(staff_<findlayer(2).baseid> >= material_<args>)
   return 1
  endif
 endif
endif
return 0

[function hascrook]//args - material z nehoz ma hul bejt (nebo z lepsiho)
if(isgm)
 return 1
endif
if(findlayer(2))
 if(findlayer(2).iscrook)
  if(crook_<findlayer(2).baseid> >= material_<args>)
   return 1
  endif
 endif
endif
return 0

[function dialogclose]//argument je cislo tlacitka co se jakoze zmackne, nebo defname dialogu a cislo tlacitka
if (argvcount > 1) 
	arg(dialoguid,<hval argv(0)>) //defname dialogu = d_muj_dialog
	arg(buttonid,<hval argv(1)>)  //cislo tlacitka = 0,1,...101,102 atd.
else
	arg(dialogmemory,<findtype(t_message)>)
	if (arg(dialogmemory))
		arg(dialoguid,<hval arg(dialogmemory).more2>)
		arg(buttonid,<hval argv(0)>)
	else
		return
	endif
endif

arg(stringtosend, 0bf <split2bytes(13)> <split2bytes(04)> <split4bytes(<hval arg(dialoguid)>)> <split4bytes(<hval arg(buttonid)>)>)
//logmsg(<arg(stringtosend)>)
sendpacket(<arg(stringtosend)>)


[function sectorinfo]
sysmessage(Sektor: <sector>)

[function convertbombtypes]
if(type==t_container)||(type==t_eq_bank_box)
 if(rescount)
  arg(u,0)
  while(u < rescount)
   findcont(u).convertbombtypes
   arg(u,#+1)
  endwhile
 endif
else
 if(dispid==i_bottle_purple)&&(type==t_potion)
  if(topobj.isplayer)&&(topobj.isonline)
   src.sysmessage(Nalezena bomba u online hrace <topobj.name> na pozici <topobj.p>) 
  endif
  type=t_potion_bomba 
 endif
endif

[function convertbombtypesonplayers]//convertbombtypesonplayers
if(isplayer)||(isgm)
 findlayer(21).convertbombtypes //batoh
 findlayer(29).convertbombtypes //banka
endif

[function arrowend]
arrowquest 0

[function repairplayers]
if(isplayer)
 dismount //odkonit 
 consume(1000 t_custom_spell) //lahve
 consume(1000 t_custom_dispellable)
 events(-e_spelleffect)
 spelleffect(41,1000) //dispell
 spelleffect(11,1000) //cure 
 events(+e_spelleffect) 
 say("all stay") //zastavit kone
 eqlayers(cont=<findlayer(21)>) //svléct
 zakladstats //resetovat
endif

[function respawncertain] //args - ID monster
if(strlen(<args>))
 nukeall(respawnspawns(<args>))
else
 nukeall(respawnspawns)
endif

[function respawnspawns]
if(type==t_spawn_char)
 if(strlen(<args>))
  if(<more1>==<args>)
   //finduid(094dfd).sysmessage(npc: <tag(lastspawned).ischar>)
   if(tag(lastspawned).ischar)
    //finduid(094dfd).sysmessage(removed <tag(lastspawned).name>)
    tag(lastspawned).remove   
   endif
   timer=1
  else
   //finduid(094dfd).sysmessage(neni to onen)  
  endif
 else //vsechny spawny
  if(tag(lastspawned))
   if(finduid(<tag(lastspawned)>))
    if(finduid(<tag(lastspawned)>).ischar)
     //finduid(094dfd).sysmessage(removed <tag(lastspawned).name>)
     finduid(<tag(lastspawned)>).remove    
    endif
   endif
  endif
  srccome
  dclick
  spawnfull 
 endif
endif

[function keyswipe]
//findlayer(101).emptycont
if(cont.layer==101)
 remove
endif

[function removehitching]
while(findid(i_hitching_memory))
 findid(i_hitching_memory).remove
endwhile

[function equipcontents] //argv0 - komu
while(rescount)
 arg(itemek,<findcont(0)>)
 itemek.cont=<argv(0)>
 itemek.layer=<itemek.typedef.layer>
 itemek.update
endwhile

[function movecontents]//argv0 - kam
while(rescount)
 arg(itemek,<findcont(0)>)
 itemek.cont=<argv(0)>
 itemek.update
endwhile

[function makeplayernoninvul]
if(isplayer)&&(flag_invul==1)
 src.sysmessage(Invul byl: <name>)
 invul=0
endif

[function makenpcnoninvul]
if(npc)&&(flag_invul==1)&&(!isvendor)
 src.sysmessage(Invul byl: <name>)
 invul=0
endif

[function redeedship]
newitemsafe(i_target_shippacker)
lastnew.timer=60
lastnew.equip

[itemdef i_target_shippacker]
name=Zamerovac sbaleni lode
id=i_memory
type=t_eq_script

on=@create
attr=010

on=@timer
remove

on=@equip
target(Zamer svou lod)

on=@targon_item
if(src.targ.type==t_ship)
 if(src.targ.more1==src) || (src.isgm)
  if (!(<src.region.flags>&region_flag_ship))
   arg(jmeno,<src.targ.name>)
   src.targ.DeedConvert  
   arg(deed,<src.act>)
   deed.name="Sbalena lod bez klice <jmeno>"
   //deed.dispid=i_deed_ship
   deed.update
  else
   src.sysmessage(Lod muzes sbalit jen na sousi)
  endif
 else
  src.redmessage(Musis byt majitelem lodi!)
 endif	
else
 src.sysmessage(Zameruj pouze lode!)
endif
remove
return 1

[function message_me]
if(tag(messagescount))
 dialog d_login_messages
 return 1  
endif

[function redmessage_me]
if(tag(redmessagescount))
 dialog d_login_redmessages
 return 1
endif 

[function delayedmessage] //args - text
if(isonline)&&(uid!=094dfd)&&(uid!=017857e)&&(uid!=09fa43) //pro Dinivana, Eufegenii a Verbatima vzdy do tabulky
 classmessage("<args>")
else
 //Do tabulky vzdy - jsou asi slepi takze jim to projistotu budem take ukladat 
 //Takze hovno - nasrat jim, delaj pak moc chytry coz jim neprislusi. Vyskakovat si nebudou
 if(tag(messagescount)) //uz ma nejake zpravy ve fronte
  arg(dalsi,<eval <tag(messagescount)>+1>)
  tag(message_<arg(dalsi)>,<args>)
  tag(messagescount,<arg(dalsi)>)
 else //prvni zprava do fronty
  tag(messagescount,1)
  tag(message_<tag(messagescount)>,<args>)	
 endif 
endif

[function pagereplymessage] //args - text
//if(isonline)&&(uid!=094dfd)&&(uid!=017857e)&&(uid!=09fa43) //pro Dinivana, Eufegenii a Verbatima vzdy do tabulky
 classmessage("<args>")
//else
 //Do tabulky vzdy - jsou asi slepi takze jim to projistotu budem take ukladat 
 //Takze hovno - nasrat jim, delaj pak moc chytry coz jim neprislusi. Vyskakovat si nebudou
 if(tag(messagescount)) //uz ma nejake zpravy ve fronte
  arg(dalsi,<eval <tag(messagescount)>+1>)
  tag(message_<arg(dalsi)>,<args>)
  tag(messagescount,<arg(dalsi)>)
 else //prvni zprava do fronty
  tag(messagescount,1)
  tag(message_<tag(messagescount)>,<args>)	
 endif 
//endif

[function delayedredmessage] //args - text
if(isonline)&&(uid!=094dfd)&&(uid!=017857e)&&(uid!=09fa43) //pro Dinivana a Eufegenii vzdy do tabulky
 redmessage("<args>")
else
 //Do tabulky vzdy - jsou asi slepi takze jim to projistotu budem take ukladat 
 //Takze hovno - nasrat jim, delaj pak moc chytry coz jim neprislusi. Vyskakovat si nebudou
 if(tag(redmessagescount)) //uz ma nejake zpravy ve fronte
  arg(dalsi,<eval <tag(redmessagescount)>+1>)
  tag(redmessage_<arg(dalsi)>,<args>)
  tag(redmessagescount,<arg(dalsi)>)
 else //prvni zprava do fronty
  tag(redmessagescount,1)
  tag(redmessage_<tag(redmessagescount)>,<args>)	
 endif 
endif


[itemdef i_delayed_messages]
id=i_memory
type=t_eq_script
name=Delayed messages counter

on=@create
timer=5

on=@timer
cont.message_me
remove

[itemdef i_delayed_redmessages]
id=i_memory
type=t_eq_script
name=Delayed redmessages counter

on=@create
timer=5

on=@timer
cont.redmessage_me
remove

[function multi_damage]
if(srccansee)
 arg(dist,<src.distancefrom(<uid>)>)
 if(!<dist>)
  damage(<argv(0)>,dam_god,<uid>)
 endif
 //kermel.sysmessage(uid:<uid.name>, src:<src.name>,dist:<dist>)
 effect(3,<s_explosion.EFFECTID>,6,15,1)
 sound=<s_explosion.sound>			
 events=-e_gethit 
 damage(<eval (<argv(0)>/<arg(dist)>)>,dam_god,<uid>) 
 events=+e_gethit 
endif

[function f_pocet_vadnych_spawnu]
var(pocet_vadnych_spawnu, 0)
src.xxi f_smazat_vadne_spawny
src.go(1,1,0,<mapplane>)
src.self
src.go(6136,4088,0,<mapplane>)
src.self
var(pocet_vadnych_spawnu,"")

[function f_smazat_vadne_spawny]
if (<dispid>==i_pet_wisp)&&(<baseid>==i_worldgem_bit)&&(<type>==t_spawn_char)&&(<more1>==00)
 remove
endif

[function fixNumber]
arg(len, <strlen(<args>)>)
while (arg(len)>0) 
  arg(pattern,<arg(pattern)>[-0123456789])
  arg(len, <eval arg(len)-1>)
endwhile

if (STRMATCH(<args>,<arg(pattern)>))
 return <eval args>
else
 //src.sysmessage("strmatch '<args>' failed")
 src.accmsg("Mozny pokus o podvod s chybnym prikazem <args>")
 return 0
endif

[function fixNumberPositive]
arg.number=<fixnumber(<args>)>
if (<arg.number> < 0)
 return 0
else
 return <arg.number>
endif

[itemdef i_port_randomvicinity]
id = i_moongate_red
type = t_port_randomvicinty
name = port do nahodnyho okoli - pokud mozno docasny reseni campeni moongatu

on=@create
attr_invis=1
attr_movenever = 1

[typedef t_port_randomvicinty]
on=@step
if (src.isplayer)
	arg(badposition, 1)
	while arg(badposition)
		src.p(<eval {(p_x-15) (p_x+15)}>, <eval {(p_y-15) (p_y+15)}>, <p_z>, <mapplane>)
		src.fix
		if (src.p_z == p_z)
			arg(badposition, 0)
		endif
	endif
	src.go(<src.p>)
endif

[function nastav_home] 
if(isplayer)
home=<home_<tag(nation)>>
endif

[function prodleva_pred_novym_pokusem] //argv(0) - kolik sekund
newitemsafe(i_delayer)
lastnew.timer=<argv(0)>
equip(<lastnew>)

[itemdef i_delayer]
id=i_memory
type=t_eq_script
name=Prodleva pred dalsimi pokusy

on=@create
attr=attr_invis|attr_decay

on=@timer
remove
return 0

[function getskilldifficulty] //argumenty= (cislo jmeno_skillu1,cislo jmeno_skillu2) atd...
singleparse(<argv(0)>) //rozseknout na <firstnumb>, <secondnumb>
 if(strcmpi(<secondnumb>,skill_inscription)) //0 pokud anoo
  var(magery,<firstnumb>) //prvni byla magery
 else
  var(insc,<firstnumb>)
 endif
singleparse(<argv(1)>)
 if(strcmpi(<secondnumb>,skill_inscription)) //0 pokud ano
  var(magery,<firstnumb>) //druha byla magery
 else
  var(insc,<firstnumb>)
 endif
//dalsi evt. skilly nas nezajimaj
return 1

[function vyrob_customsvitek] //argv(0) = cislo kouzla k vyrobeni, argv(1,2) - insc, magery (narocnosti)
if(!<argv(0)>)
 src.redmessage(Chyba! Kontaktuj GM) 
 src.findid(i_vyrobce_svitku).remove
 return 0
endif
if(!<src.restest 1 i_scroll_blank>)
 src.redmessage(Nemas dost svitku!) 
 src.findid(i_vyrobce_svitku).remove
 return 0
endif
arg(mana_po_vyrobe,<eval (<src.mana>-<eval (<a_manause[<argv(0)>]>)/2>)>)
if(<arg(mana_po_vyrobe)> > 0)
 if(<src.mana> < <arg(mana_po_vyrobe)>)
  src.redmessage(Nemas dost many!) 
  src.findid(i_vyrobce_svitku).remove
  return 0
 endif
else
  src.redmessage(Nemas dost many!) 
  src.findid(i_vyrobce_svitku).remove
  return 0
endif
//src.sysmessage(Puvodni cislo kouzla: <argv(0)>)
if(<src.findid(i_vyrobce_svitku).more1>) //dokud ma co vyrabet
 src.newitemsafe(i_vyrobni_timer)
 lastnew.more1=<argv(0)> //co se bude vyrabet
 lastnew.tag(insc,<argv(1)>)
 lastnew.tag(magery,<argv(2)>) //zatim nepouzito, ale co kdyby nekdy...
 lastnew.equip
else
 src.classmessage(Vyroba ukoncena)
 src.findid(i_vyrobce_svitku).remove
endif
return 1

[itemdef i_vyrobce_svitku]
id=i_memory
name=Pocita vyrobene svitky custom
type=t_eq_script

on=@create
attr=attr_invis|attr_decay

[itemdef i_vyrobni_timer]
id=i_memory
name=K vyrobe customsvitku
type=t_eq_script

on=@create
attr=attr_invis|attr_decay
timer=3

on=@timer
//cont.sysmessage(insc: <tag(insc)>, magery: <tag(magery)>, cislo svitku: <more1>)
if(vyroba_fizzle(<tag(insc)>,<cont.inscription>))
 cont.consume(1 i_scroll_blank)
 cont.mana=<eval (<cont.mana>-<eval (<a_manause[<more1>]>)/2>)>
 cont.newitemsafe(<a_scrollitem[<more1>]>)
 lastnew.bounce
 cont.findid(i_vyrobce_svitku).more1=<eval (<cont.findid(i_vyrobce_svitku).more1>-1)>
 cont.vyrob_customsvitek(<more1>,<tag(insc)>,<tag(magery)>) //kouzlo,narocnosti skillu 
else
 cont.consume(1 i_scroll_blank)
 cont.mana=<eval (<cont.mana>-<eval (<a_manause[<more1>]>)/2>)>
 cont.classmessage(Vyroba se nezdarila - dalsi pokus) 
 cont.vyrob_customsvitek(<more1>,<tag(insc)>,<tag(magery)>) //kouzlo,narocnosti skillu 
endif
remove 

[function vyroba_fizzle]
//<argv(0)> naroky svitku
//<argv(1)> tentyz skill vyrobce
//vypocet pravdepodobnosti neuspechu, je to zvoleny pomerne chytre (mezi 2/3 a 5/3 narocnosti svitku)
arg(dolni_mez,<eval ((<argv(0)>)/3)*2>)
arg(horni_mez,<eval ((<argv(0)>)/3)*5>)
if(<arg(horni_mez)> > 1000)
 arg(pomoc,<eval (<arg(horni_mez)>-1000)>)
 arg(horni_mez,<eval (((<arg(pomoc)>)/2)+1000)>) //horni mez zmensit na polovicni vzdalenost od 1000
endif
if(<argv(1)> < <arg(dolni_mez)>)
 arg(compareskill,0) //nevyrobi nic
 src.redmessage(Nedostatecny skill!)
else
 arg(compareskill,<argv(1)>)
endif
if (<arg(compareskill)> < <eval ({<arg(dolni_mez)> <arg(horni_mez)>})>)
 //src.sysmessage({<arg(dolni_mez)>, <arg(horni_mez)>}, skill: <arg(compareskill)>)
 return 0
endif
 //src.sysmessage({<arg(dolni_mez)>, <arg(horni_mez)>}, skill: <arg(compareskill)>)
return 1

[function wait_then_port]//argv(0) - jak dlouho cekat, argv(1,2,3,4) - kam portovat (x,y,z,m)
src.newitemsafe(i_wait_then_port)
lastnew.morep(<argv(1)> <argv(2)> <argv(3)> <argv(4)>)
lastnew.tag(kdy,<eval argv(0)>)
lastnew.equip

[function f_padaci_pauza]//argv(0) - jak dlouho cekat, argv(1) - kterej layer, argv(2) - napis
src.newitemsafe(i_pauza_pred_spadnutim_veci) //vec na zem ! (kun dismount)
lastnew.more1=<argv(1)>
lastnew.tag(kdy,<eval argv(0)>)
if(strlen(<argv(2)>))
 lastnew.tag(napis,<argv(2)>)
else
 lastnew.tag(napis," ")
endif
lastnew.equip

[function f_svlikaci_pauza] //argv(0) - jak dlouho cekat, argv(1) - kterej layer, argv(2) - napis
src.newitemsafe(i_pauza_pred_unequipem) //vec do baglu
lastnew.more1=<argv(1)>
lastnew.tag(kdy,<eval argv(0)>)
if(strlen(<argv(2)>))
 lastnew.tag(napis,<argv(2)>)
else
 lastnew.tag(napis," ")
endif
lastnew.equip

[function f_zbyva_casu] //argv(0) - od kdy se pocita, argv(1) - jakej byl tehdy limit , decisekundy!!!!!
arg(zbyva_casu,<eval (<argv(1)>-(<serv.time>-<argv(0)>))>) 
arg(dni,<eval (arg(zbyva_casu)/<den>)>)
if(<arg(dni)>)
 return <arg(dni)/10> dni
endif
arg(hodin,<eval (arg(zbyva_casu)/36000)>) //hodina ma 3600s
if(<arg(hodin)>)
 return <arg(hodin)/10> hodin
endif
arg(minut,<eval ((arg(zbyva_casu)-(arg(hodin)*36000))/600)>)
if(<arg(minut)>)
 return <arg(minut)/10> minut
endif
arg(sekund,<eval ((((arg(zbyva_casu))-(arg(hodin)*36000))-(arg(minut)*600))/10)>)
if(<arg(sekund)>)
 return <arg(sekund)/10> sekund
endif

[function removetarget]
while (memoryfindtype(MEMORY_WAR_TARG))
 memoryfindtype(MEMORY_WAR_TARG).remove
endwhile

[function removefight]
while (memoryfindtype(MEMORY_FIGHT))
 memoryfindtype(MEMORY_FIGHT).remove
 
endwhile

[function vyvrhel_prevest]
if (tag(realm)==1)&&(kills>19)
 vyvrhel(1)
endif

[function vyvrhel]
if !(args)
 tag(realm,<tag(realm)>*(-1))
 if (tag(realm)==1)
  kills=0
  fame=0
  karma=0
 else
  kills=30000
  fame=0
  karma=-3000
 endif
else
 if (tag(realm)>0)
  tag(realm,<tag(realm)>*(-1))
  kills=30000
  fame=0
  karma=-3000
 endif
Endif
if(<tag(realm)> < 0) //je to cerstvy vyvrhel
 tag(puvodni_jmeno,<name>) //ulozit si jmeno
 name=Vyvrhel <name>
else //je zpatky
 name=<tag(puvodni_jmeno)>
 tag.remove(puvodni_jmeno)
endif

[function prislusnost_realmu]
if (tag(realm)<0)
 Return Ano
else
 Return Ne
endif

[function statistikapovolani]
if(argvcount)
  arg(dny,<eval args>)
else
  arg(dny,30)
endif
write("Statistika povolani za poslednich <arg(dny)> dni")
arg(dny,<eval serv.time-(arg(dny)*24*60*60*10)>)
arg(i,0)
while (arg(i)<11)
  var(statspovolani_celkem[arg(i)],0)
  var(statspovolani_veteran[arg(i)],0)
  var(statspovolani_poslednidoba[arg(i)],0)
  var(statspovolani_poslednidoba_veteran[arg(i)],0)
  var(statspovolani_gondoru[arg(i)],0)
  arg(i,#+1)
endwhile
var(gondoru_celkem,0)

allplayers(f_statistikapovolani(<arg(dny)>))
write("celkem, veteranu, celkem za posledni dobu, celkem veteranu za posledni dobu.")
write("veteran=vic jak 10. level")
arg(i,0)
while (arg(i)<11)
  arg(povolani_name,<findres(profession,arg(i)).name>)
  write("<arg(povolani_name)>: <var(statspovolani_celkem[arg(i)])>, <var(statspovolani_veteran[arg(i)])>, <var(statspovolani_poslednidoba[arg(i)])>, <var(statspovolani_poslednidoba_veteran[arg(i)])>, <var(statspovolani_gondoru[arg(i)])>")
  arg(i,#+1)
endwhile
write("gondoru celkem: <var(gondoru_celkem)>")

[function f_statistikapovolani]
arg(profID,<nid(profession)>)
var(statspovolani_celkem[arg(profID)],#+1)
if (tag(level)>10)
  var(statspovolani_veteran[arg(profID)],#+1)
endif
if (tag(lastlogoutedAt)>args)
  var(statspovolani_poslednidoba[arg(profID)],#+1)
  if (tag(level)>10)
    var(statspovolani_poslednidoba_veteran[arg(profID)],#+1)
  endif
endif
if (tag(realm)==1)
  var(statspovolani_gondoru[arg(profID)],#+1)
  var(gondoru_celkem,#+1)
endif

[function forgetothers]//pro plevel 1
if (!act)
  return
else
  arg(storeact,<act>)
  arg(targmemory,<memoryfindtype(MEMORY_WAR_TARG)>)
  while (arg(targmemory))
    arg(targmemory).color=<eval arg(targmemory).color&~MEMORY_WAR_TARG>
    arg(targmemory,<memoryfindtype(MEMORY_WAR_TARG)>)
  endwhile
  arg(targmemory,<memoryfind(<arg(storeact)>)>)
  arg(targmemory).color=<eval arg(targmemory).color|MEMORY_WAR_TARG>
endif

[typedef t_shaft_multi]
on=@userdclick
multiitem_create(<typedef.tdata1>, <eval amount*typedef.tdata2>)
remove

on=@dropon_obj
multiitem_create(<typedef.tdata1>, <eval amount*typedef.tdata2>)
remove

[typedef t_arrow_piercing_multi]
on=@userdclick
multiitem_create(<typedef.tdata1>,<eval 20*amount>)
remove

on=@dropon_obj
multiitem_create(<typedef.tdata1>,<eval 20*amount>)
remove

[function multiitem_create]
src.lognewitemsafe(<argv(0)>)
lastnew.amount=<argv(1)>
lastnew.logcont(<src>)
tag(craftedby,"<tag(craftedby)>")

[FUNCTION countclients]
VAR.CLIENTCOUNT=<EVAL <VAR.CLIENTCOUNT> + 1>

//opraveno Dinivanem
[ITEMDEF i_shiphaste]
id=01ea7
type=t_eq_script
name=Ship Speed Control

ON=@timer
topobj.more1.sysmessage(Timer)
tag(oldlink,<link>) //lod
tag(linkid,<link.id>) //id lode
tag(linkname,<link.name>) //jmeno lode
tag(linkmore1,<link.more1>) //uid majitele lodi
link=<more1> //to byl majitel lodi
if !(<link.region.flags> & region_flag_ship) //Srat na to pokud neni na palube!
 tag(oldlink).tag.remove(hasteuid) //odstranit z lode 
 remove //ten gem
 return 1
else
 link=<tag.oldlink>
 tag.remove(oldlink)
 var(clientcount,0)
 region.allclients(countclients) //spocitat pasazery na lodi
 tag(maxtimercount,<eval (<clientcount>/3)+1>)
 tag(timercount,0)
 if(<tag(timercount)> < <tag(maxtimercount)>)
  tag(timercount,<tag(maxtimercount)>)  
 endif
 timer=<eval <tag(timercount)>>
 link.timerd=<eval <tag(timercount)>>
 return 1
endif

[function f_shiphasteTARTAROS]
return 0 //not implemented
IF !(<safe.TAG.HASTEUID>)
 SRC.NEWITEM i_shiphaste
 TAG.HASTEUID=<SRC.act.uid>
 SRC.ACT.LINK=<UID>
 SRC.ACT.MORE1=<SRC.UID>
 SRC.ACT.NAME=Ship Speed Control for <NAME>
 SRC.ACT.ATTR=attr_invis|attr_move_never
 SRC.ACT.P=<P>
 SRC.ACT.TIMER=1
ENDIF

[ITEMDEF i_shiphasteTARTAROS]
ID=01ea7
TYPE=t_eq_script
NAME=Ship Speed Control

ON=@Timer
TAG.OLDLINK=<LINK>
LINK=<MORE1>
IF !(<LINK.REGION.FLAGS>&region_flag_ship) //Don't do anything unless they're actually on board
LINK=<eval <TAG.OLDLINK>>
TAG.OLDLINK=
LINK.TAG.HASTEUID=
REMOVE
RETURN 1
ELSE
LINK=<eval <TAG.OLDLINK>>
TAG.OLDLINK=
TIMERd=1
LINK.TIMERd=2
RETURN 1
ENDIF

[function setpassword]
if (strlen(<args>))
  account.password="<args>"
  smsg("account password set to '<account.password>'")
else
  smsg("zadavej ve formatu '.setpassword(noveheslo)'")
endif

[function zrus_spawny]
var(zruseno_spawnu,0)
nukeall(zrusitspawny)
src.sysmessage(Zruseno <var(zruseno_spawnu)> spawnu)

[function obnov_spawny]
var(obnoveno_spawnu,0)
nukeall(obnovitspawny)
src.sysmessage(Obnoveno <var(obnoveno_spawnu)> spawnu)
var(zruseno_spawnu,"")
var(obnoveno_spawnu,"")

[function zrusitspawny]
if (type==t_spawn_char)
 type=t_negated_spawn_char
 tag(old_timer,<timer>)
 timer=0fffffff
 var(zruseno_spawnu,#+1)
endif 

[function obnovitspawny]
if (type==t_negated_spawn_char)
 type=t_spawn_char
 timer=<tag(old_timer)>
 tag.remove(old_timer)
 var(obnoveno_spawnu,#+1)
endif 

[typedef t_negated_spawn_char]
on=@timer
timer=0fffffff
return 1



//[typedef T_potion_shrink]
//on=@userdclick
//target
//return 1

//on=@targon_char
//if (src.targ.flag_conjured)
//  target
//  return 1
//elseif !(src.targ.ismypet)
//  target
//  return 1
//elseif (src.targ.distance>3)
//  target
//  return 1
//elseif !(src.targ.srccanseelos)
//  target
//  return 1
//endif
//src.targ.shboun
//src.newitemsafe(<hval typedef.tdata1>)
//lastnew.bounce
//remove
//return 1

[typedef t_figurine_2]
on=@userdclick
if (tag(my_id))
 if (!<more1l>)
   src.sysmessage("tento shrink je v poradku, a pri pristip poklikani uz vrati zvire.")
   more1 = 1
   return 1
 endif
 
 src.newnpc(<tag(my_id)>)
 src.act.p=<topobj.p>
 src.act.color=<color>
 src.act.name=<name>
 src.act.newmemory(<src>)
 src.act.act.color=<src.act.act.color>|MEMORY_IPET
 src.act.food=<tag(myfood)>
 src.act.hits=<tag(myhits)>
 remove
 
else
 arg.d = <findres(chardef, <more1>)>
 if (<arg.d>)
  if (<?findres(itemdef, <?<arg(d)>.icon?>)?> == <?dispid?>)
    src.sysmessage("byl sem pokazenej shrink, ale uz sem spravenej. Pristi poklikani uz vrati zvire.")
    tag(my_id, <arg.d>)
    more1 = 0
    return 1
  endif
 endif
 src.sysmessage("sem pokazenej shrink")
 return 1
endif



[typedef T_potion_shrink]//jestli to nefunguje, tak to rozbil enrico
on=@userdclick
target
return 1


on=@targon_char
if (src.targ.flag_conjured)
  target
  return 1
elseif !(src.targ.ismypet)
  target
  return 1
elseif (src.targ.distance>3)
  target
  return 1
elseif !(src.targ.srccanseelos)
  target
  return 1
elseif (src.targ.isplayer)  
  target
  return 1
elseif (src.targ.tag(noshrink))
  target 
  return 1
elseif (src.targ.isvendor)
  target 
  return 1
endif

SRC.NEWITEM=<src.targ.typedef.icon>
SRC.ACT.NAME=<SRC.TARG.NAME>
SRC.ACT.COLOR=<SRC.TARG.COLOR>
SRC.ACT.tag(my_id,<SRC.TARG.ID>)
SRC.ACT.TAG(MYHITS,<SRC.TARG.HITS>)
SRC.ACT.TAG(MYFOOD,<SRC.TARG.FOOD>)
src.act.more1=1
SRC.ACT.TYPE=t_figurine_2
SRC.ACT.BOUNCE
SRC.TARG.REMOVE
if(amount==1)
 remove
else
 amount=<amount>-1
endif

return 1



[function follow]
if (src.findid(i_follow))
  src.consume(1000 i_follow)
else
  src.sysmessage Following <name>
  src.newitemsafe(i_follow)
  src.equip(lastnew)
  lastnew.link=<uid>
  lastnew.timerd=1
  lastnew.more2=<src.flag_war>
endif

[itemdef i_follow]
name=following
id=i_memory
type=t_eq_script

on=@timer
if (more2!=cont.flag_war)
  remove
else
  cont.go(<link.topobj.p>)
  cont.updatex
  timerd=1
endif
return 1

on=@unequip
src.sysmessage Following stopped

[function weaponcreate]
events +t_allweapons
hitpoints=<eval (<nastaveni_durability_<argv(0)>>*<nastaveni_durability_<def.dispid>>)/1000>
if (argvcount>1)
color=color_<argv(1)>
else
color=color_o_<argv(0)>
endif
if (isbow)
  events +t_allbows
elseif (isstaff)
  events +t_allstaves
elseif (type==T_WEAPON_MACE_SMITH)
  if (typedef.layer==1)
    events +t_cm_blacksmithing_target
  endif
elseif (type==t_weapon_fence)
  if (typedef.layer==1)
    events +t_cm_bowcrafting_target
  endif
endif

[typedef t_post_hitching]
on=@userdclick
src.newitemsafe(i_hitching_memory)
lastnew.equip
lastnew.timer=60
lastnew.target("Target the creature to hitch.")
return 1

[itemdef i_hitching_memory]
name=hitching memory
id=i_memory
type=t_eq_script

on=@timer
remove
return 1

ON=@TARGON_ITEM
target
RETURN 1

ON=@TARGON_CHAR 
src.sysmessage(zameril si <src.targ.name>)
IF (SRC.targ.ISPLAYER)
 SRC.SYSMESSAGE("Hrace nesmis uvazovat")
 RETURN 1
ENDIF
IF !(SRC.TARG.ismypet)
  SRC.SYSMESSAGE("To neni tve zvire")
  RETURN 1
endif
IF (SRC.TARG.flag_immobile)
  SRC.TARG.flag_immobile=0
  SRC.SYSMESSAGE("Odvazal jsi <SRC.TARG.NAME> od stojanu!")
  RETURN 1
ELSE
  SRC.TARG.flag_immobile=1
  SRC.SYSMESSAGE("<SRC.TARG.NAME> je uvazan!")
  RETURN 1
ENDIF
remove
return 1

[typedef t_garbage_can]
on=@userdclick
open
return 1

on=@STACKON
timer=1
more=15

on=@timer
if (more)
  say("<more>")
  more=<eval more-1>
  if (rescount)
    timer 1
  endif
else
  emptycont
endif
return 1

[function f_potionkeg_empty]
MOREP=0,0
tag(ID,0)
color=0
name=

[typedef t_keg_potion]
//on=@UserToolTip  //99z8 nechodi 
//if (morex)
//  message("(<morex> <?<findres(itemdef,<tag(id)>)>.name?>)")
//endif

ON=@userCLICK
if (morex)&&(STRCMPI(<src.tag(clienttype)>,"new"))//0 pokud ano
  message("(<morex> <?<findres(itemdef,<tag(id)>)>.name?>)")
endif

ON=@userDCLICK
if (!f_lockeditem)
  TARGET What potion would you like to fill the keg with?
endif
return 1

ON=@TARGON_ITEM
if  (src.targ.type==t_garbage_can)
 if (src.targ.srccanseelos)&&(src.targ.distance<3)
  f_potionkeg_empty
  SRC.SOUND=snd_Z_END_LIQUID
  SRC.SYSMESSAGE Keg vyprazdnen.
  return 1
 else
  SRC.SYSMESSAGE Jsi prilis daleko od barelu.
  return 1
 endif	
elseif (src.targ.type==t_bottle_empty)
 if !(morex)
  SRC.SYSMESSAGE You can't use this on a empty keg!
  return 1
 endif
 if (<src.targ.topobj>!=<src>)
  SRC.SYSMESSAGE The empty bottle should be in your backpack!
  return 1
 endif
 SRC.SYSMESSAGE You fill an empty bottle.
 SOUND=snd_Z_END_LIQUID
 if (<src.targ.amount> < <eval moreX> )
  arg(amnt,<src.targ.amount>)
 else
  arg(amnt,<eval moreX>)
 endif
 src.newitem=<tag(ID)>
 lastnew.amount=<arg(amnt)>
 lastnew.attr=0
 lastnew.cont=<src.targ.cont> 
 src.targ.amount=<src.targ.amount>-<arg(amnt)>
 if !(<src.targ.amount>)
  src.targ.remove
 endif
 moreX=<moreX>-<arg(amnt)>
 if (<uid>==<topobj.uid>)
  src.accmsg("Natocil <?arg(u)?> <?<findres(itemdef,<tag(id)>)>.name?> z uid=<?uid?> (<?amount?> <?name?>) umisten <?p?> (<?region.name?>)")
 else
  src.accmsg("Natocil <?arg(u)?> <?<findres(itemdef,<tag(id)>)>.name?> z uid=<?uid?> (<?amount?> <?name?>) umisten uid=<?cont.uid?> (<?cont.name?>) topobj=<?topobj.uid?> (<?topobj.name?>)")
 endif
 if !(moreX)
  f_potionkeg_empty
 endif
 return 1
elseif (SRC.TARG.TYPE==t_potion)||(SRC.TARG.TYPE==t_allpotions)||(SRC.TARG.TYPE==t_drink)||(SRC.TARG.TYPE==T_potion_shrink)||(SRC.TARG.TYPE==t_speedpotions)||(SRC.TARG.TYPE==t_potion_bomba)
 if (src.targ.srccanseelos)&&(src.targ.distance<3)&&(src.targ.topobj==src)
  if (tag(ID))&&(SRC.TARG.BASEID!=tag(ID))
   SRC.SYSMESSAGE You can't mix potion types!
   RETURN 1
  elseif (MOREX>999) //max 1000 potion
   SRC.SYSMESSAGE There's no space for further potions!
   RETURN 1
  elseif (MOREX==0)
   tag(ID,<SRC.TARG.BASEID>) // store potion baseid i_potion_xxx
  endif
  arg(addPotionCount,1000-<moreX>)
  if (<src.targ.amount> < <arg(addPotionCount)>)
    arg(addPotionCount,<src.targ.amount>)
  endif
  SRC.NEWITEM(<src.targ.typedef.tdata1>)	// pridame prazdne lahve
  arg(newPot,<lastnew>)
  arg(newPot).amount=<arg(addPotionCount)>
  arg(newPot).ATTR=0
  arg(newPot).BOUNCE
  MOREX=<eval moreX+arg(addPotionCount)>
  SRC.SOUND=snd_Z_END_LIQUID
  SRC.SYSMESSAGE("You fill up the keg with a <SRC.TARG.NAME> Potion!")
  color=<src.targ.color>
  if (SRC.TARG.color==0)
   if (SRC.TARG.dispid==i_bottle_blue)//blue
    color=058
   elseif (SRC.TARG.dispid==i_bottle_orange)//orange
    color=02b
   elseif (SRC.TARG.dispid==i_bottle_purple)//purple
    color=0df
   elseif (SRC.TARG.dispid==i_bottle_yellow)//yellow
    color=035
   elseif (SRC.TARG.dispid==i_bottle_black)//black
    color=03d6
   elseif (SRC.TARG.dispid==i_bottle_green)//green
    color=044
   elseif (SRC.TARG.dispid==i_bottle_red)//red
    color=021
   elseif (SRC.TARG.dispid==i_bottle_white)//white
    color=0482
   endif	
  endif
  if (<arg(addPotionCount)> == <src.targ.amount>)
    SRC.TARG.REMOVE
  else
    src.targ.amount=<src.targ.amount> - <arg(addPotionCount)>
  endif
  return 1
 else
  src.sysmessage("Too far away or out of the line of sight")
 endif
endif
target
return 1

[typedef t_allweapons]
on=@userclick
if (STRCMPI(<src.tag(clienttype)>,"old"))//0 pokud ano
 return 1
endif
if (topobj==src)||(src.isgm)
  src.smsg durabilita:<more1l>/<more1h>
endif

on=@equip
if (typedef.layer==2)
  arg(index,0)
  while (arg(index)<cont.rescount)
   arg(founditem,<cont.findcont(arg(index))>)
   if (arg(founditem).layer==2)
     if (arg(founditem)!=uid)
       arg(founditem).unequip
       break
     endif
   endif
   arg(index,#+1)
  endwhile
endif
src.temporary_null

on=@unequip
src.temporary_null

[function showbowtype_old]
if (type==t_weapon_bow_run)
 message("('Run')")
else
 message("('Stand')")
endif
[function showbowtype_new]
return 0
[function showbowtype_]
return 1

[typedef t_allbows]
on=@userclick
showbowtype_<src.tag(clienttype)>

on=@userdclick
return 1

on=@equip
if (type==t_weapon_bow_run)
  if !(typedef.dam)
    arg(standbowID,<strmid(<baseid>,0,<STRLEN(<baseid>)>-4)>)
    if (findres(itemdef,arg(createID))!=i_unused)
      typedef.dam=<?<arg(standbowID)>.dam?>
    endif
  endif
  if (src.profession==class_ranger)||(src.isgm)
    src.events +e_runarcher
  else
    switchthisbow
  endif
endif

on=@unequip
if (src.profession==class_ranger)||(src.profession==class_thief)
  src.macefighting=0
endif
src.events -e_runarcher

[typedef t_allstaves]
on=@userdclick
arg(staffmaxmana,<eval nastaveni_staffmaxmana_<baseid>>)
if (src.mana==src.maxmana)&&(more2l<arg(staffmaxmana))
 arg(manaswap,<eval ((src.statslevel*100)+3000)>)//koeficient podle levelu
 more2l=<eval more2l+(src.mana*arg(manaswap))/10000>
 src.mana=0
 if (more2l>arg(staffmaxmana))
  src.mana=<eval ((<more2l>-<arg(staffmaxmana)>)*10000)/<arg(manaswap)>>
  more2l=<arg(staffmaxmana)>
 endif
elseif (src.mana<src.maxmana)
 arg(manadiff,<eval src.maxmana-src.mana>)
 if (more2l<arg(manadiff))
  src.mana=<src.mana>+<more2l>
  more2l=0
 else
  src.mana=<src.maxmana>
  more2l=<eval more2l-arg(manadiff)>
 endif
endif
sound 250
trigger @userclick
return 0

on=@userclick
arg(staffmaxmana,<eval nastaveni_staffmaxmana_<baseid>>)
if (STRCMPI(<src.tag(clienttype)>,"old"))//0 pokud ano
 return 1
endif
if (<src>==<topobj>)||(src.isgm)
  src.classmessage("Mana v holi:<?more2l?>/<?arg(staffmaxmana)?>")
endif

on=@unequip
if (more2l>0)
  classmessage("Mana v holi je vazana s tvou mysli - sundanim hole  mana z hole vyprchala.")
  more2l=0
endif

[typedef t_allarmors]
on=@equip
src.temporary_null
tag.OMadded=<eval armorOM>
src.tag.armordef=<eval <src.tag.armordef>>+<tag.OMadded>

on=@unequip
src.temporary_null
src.tag.armordef=<eval <src.tag.armordef>>-<tag.OMadded>
tag(OMadded,"")

on=@userclick
if (STRCMPI(<src.tag(clienttype)>,"old"))//0 pokud ano
 return 1
endif
src.smsg durabilita:<more1l>/<more1h>

[typedef t_allshields]
on=@userdclick
safe src.findlayer(2).bounce

on=@equip
src.temporary_null

on=@unequip
src.temporary_null

on=@userclick
if (STRCMPI(<src.tag(clienttype)>,"old"))//0 pokud ano
 return 1
endif
src.smsg durabilita:<more1l>/<more1h>

[function armorcreate]//ie (copper,plate)
events +t_allarmors
hitpoints=<eval (<nastaveni_durability_<argv(0)>>*<nastaveni_durability_<argv(1)>>*<layerar[typedef.layer]>)/100000>
color=color_o_<argv(0)>
if (def.layer==24)
 events +t_uneq_4
elseif (def.layer==4)
 events +t_uneq_24
elseif (def.layer==5)
 events +t_uneq_13_19
elseif (def.layer==13)
 events +t_uneq_5
elseif (def.layer==19)
 events +t_uneq_5
endif

[function shieldcreate]
events +t_allshields
hitpoints=<eval (<nastaveni_durability_<argv(0)>>*<nastaveni_durability_<argv(1)>>*<layerar[typedef.layer]>)/100000>
color=color_o_<argv(0)>

[typedef t_uneq_24]
on=@equip
safe cont.findlayer(24).unequip

[typedef t_uneq_4]
on=@equip
safe cont.findlayer(4).unequip

[typedef t_uneq_5]
on=@equip
safe cont.findlayer(5).unequip

[typedef t_uneq_13_19]
on=@equip
safe cont.findlayer(13).unequip
safe cont.findlayer(19).unequip

[function logdupe]
arg(mnozstvi,1)
if (argvcount)
  arg(mnozstvi,<eval args>)
endif
if (ischar)
  try x logdupe(<arg(mnozstvi)>)
else
  while (arg(mnozstvi)>0)
    dupe
    src.accmsg("dupe: created item uid=<lastnew> (<amount> <lastnew.name>)")
    arg(mnozstvi,#-1)
  endwhile
endif

[function fastwalk]
if (findlayer(25))
  return 0
endif
newitem i_mount_fake
equip <act.uid>
flags=<flags>&~statf_onhorse
sysmessage You move like quicksilver

[itemdef i_mount_fake]
id=i_pet_horse
name=faked horse speeding up players
layer=30
type=t_eq_script

on=@equip
link=<src.uid>
more2=<src.uid>
layer=25
type=t_eq_horse
update

[function iscommander]
if (npc)
 return 0
elseif (isgm)
 return 2
elseif (account.tag(commander))
 return <account.tag(commander)>
else
 return 0
endif

[function escortme]//default is the escorting npc
if (array_find(src.tag.escortchar,<uid>)>-1)
 src.sysmessage(<name> no longer escorts you)
 src.array_findremove(tag.escortchar,<uid>)
 events -e_nomove
else
 src.sysmessage(<name> now escorts you)
 src.events +e_escorting
 src.array_add(tag.escortchar,<uid>)
 events +e_nomove
endif

[events e_escorting]
on=@step
if (tag(escortchar_00))
 if (flag_running)
  array_foreachuid(tag.escortchar,makerun(<dir_inttochar(<dir>)>))
 else
  array_foreachuid(tag.escortchar,makewalk(<dir_inttochar(<dir>)>))
 endif
else
 events -e_escorting
endif

[function makerun]
arg(storeevents,<events>)
events -e_nomove
arg(storepos,"<p>")
run(<args>)
p=<arg(storepos)>
move(<args>)
events +arg(storeevents)

[function makewalk]
arg(storeevents,<events>)
events -e_nomove
arg(storepos,"<p>")
walk(<args>)
p=<arg(storepos)>
move(<args>)
events +arg(storeevents)

[function targetitem]
newitemsafe(i_targetgeneric)
lastnew.more=1
lastnew.tag(apply,<args>)
equip(<lastnew>)

[function targetchar]
newitemsafe(i_targetgeneric)
lastnew.more=2
lastnew.tag(apply,<args>)
equip(<lastnew>)

[function targetobj]
newitemsafe(i_targetgeneric)
lastnew.more=3
lastnew.tag(apply,<args>)
equip(<lastnew>)

[itemdef i_targetgeneric]
name=target generic
id=i_memory
type=t_eq_script

on=@equip
target
timer=60

on=@timer
remove
return 1

on=@targon_item
if (more&1)
 src.targ.<tag(apply)>
 remove
else
 Target
endif
return 1

on=@targon_char
if (more&2)
 src.targ.<tag(apply)>
 remove
else
 Target
endif
return 1

[function allcontainersClick]
if (topobj==src)||(src.isgm)
  message("(<rescount> ks)")
endif
return 0

[function srccombatLOS]
if !(srccanseelos)
elseif ((src.p_z-p_z)>19)||((src.p_z-p_z)<(-19))
else
 return 1
endif
return 0

[function nextday]//vrati pocet sekund do dalsiho checku
arg(odminule,<eval (<serv.rtime>%(3600*24))-(3*3600)>)
arg(tonextday,<eval (24*3600)-odminule>)
return <tonextday>

[function logbounce]
src.Accmsg(Bounced uid=<uid> (<amount> <name>) from uid=<cont> (<cont.name>) topobj=<topobj> (<topobj.name>))
bounce

[function SectoDays]
arg(seconds,<eval args>)
arg(hours,<eval <seconds>/(3600)>)
arg(seconds,<eval <seconds>-(<hours>*(3600))>)
arg(mins,<eval <seconds>/(60)>)
arg(seconds,<eval <seconds>-(<mins>*(60))>)
return"<hours>:<mins>:<seconds>"

[function Sectoyears]
arg(seconds,<eval args>)
arg(years,<eval <seconds>/(3600*24*365)>)
arg(seconds,<eval <seconds>-(<years>*(3600*24*365))>)
return"<years>:<SectoDays(<seconds>)>"

[function isdead]
if (<flags>&statf_dead)
 return 1
else
 return 0
endif


[function ischardef]
if (hval(<hval uid>&0ff000000)==08e000000)
 return 1
endif
return 0

[function isitemdef]
if (hval(<hval uid>&0ff000000)==0a2000000)
 return 1
endif
return 0

[function new]
act=0
if !(argvcount)
 MENU MENU_ADDITEM
endif
arg(tocreate,<hval args>)
if (hval(<arg(tocreate)>&0ff000000)==0a2000000)//itemdef
 newi(<arg(tocreate)>)
elseif (hval(<arg(tocreate)>&0ff000000)==08e000000)
 newc(<arg(tocreate)>)
elseif (findres(itemdef,arg(tocreate)))
 newi(<arg(tocreate)>)
elseif (findres(chardef,arg(tocreate)))
 newc(<arg(tocreate)>)
else
src.sysmessage Spatny argument
endif
//if (act)
//elseif (act.isitem)
//elseif (act.ischar)
//endif

[function lognewnpc]
arg(sfnact,<act>)
newnpc(<hval args>)
accmsg(creates char uid=<lastnewchar> (<lastnewchar.name>))
act=<arg(sfnact)>

[function srccome]
src.go <topobj.p>

[function return_nth]
//n, args
return<argv(argv(0))>

[function isadmin]
if (isplayer)
 if (account.plevel>5)
  return 1
 endif
endif
return 0

[FUNCTION Nuke]
src.sysmessage Pouzij .XX, .XXC nebo .XXI
//if (argvcount)
//	sysmessage "Select region to <?argv?> items:"
//	XXI.<?argv?>
//else
//	sysmessage "Select region to delete items:"
//	XXI.Remove
//endif

[FUNCTION NukeChar]
src.sysmessage Pouzij .XX, .XXC nebo .XXI
//if (argvcount)
//	sysmessage "Select region to <?argv?> items:"
//	XXC.<?argv?>
//else
//	sysmessage "Select region to delete items:"
//	XXC.Remove
//endif

[function skillcheckstats]
hiding=0
f_skillcheckstats(<args>)
hiding=100
f_skillcheckstats(<args>)
hiding=200
f_skillcheckstats(<args>)
hiding=300
f_skillcheckstats(<args>)
hiding=500
f_skillcheckstats(<args>)
hiding=750
f_skillcheckstats(<args>)
hiding=1000
f_skillcheckstats(<args>)
hiding=1200
f_skillcheckstats(<args>)

[function f_skillcheckstats]
arg(i,0)
arg(counter,0)
while arg(i)<1000
  arg(counter,#+<skillcheck(hiding,<eval args>)>)
  arg(i,#+1)
endwhile
logmsg(skill <hiding>:::value <eval args>:::success <arg(counter)>)

[function reveal]
if (<flags>&0800008)
  flag_hidden=0
  flag_invisible=0
  invis 0
  smsg(Byl jsi objeven)
endif

[function allinstances]
src.invis 0
if (argvcount>1)
 arg(property,<argv(0)>)
 arg(comm,<argv(1)>)
else
 arg(property,<baseid>)
 arg(comm,<args>)
endif
IF (isitem)
 arg(f_run,goitemid)
else
 arg(f_run,gocharid)
endif
arg(storep,<src.p>)
arg(firstuid,0)
arg(lastuid,0)
src.try <f_run>(<arg(property)>)
while (<src.act>!=<arg(firstuid)>)&&(<src.act.baseid>==<arg(property)>)&&(<src.act>!=<arg(lastuid)>)
 arg(nowuid,<src.act>)
 src.act.try(<arg(comm)>)
 if (<arg(firstuid)>==0)
  arg(firstuid,<arg(nowuid)>)
 endif
 arg(lastuid,<src.act>)
 src.try <f_run>(<arg(property)>)
endwhile
src.go <storep>


[function alloftype]
src.invis 0
if (argvcount>1)
 arg(property,<argv(0)>)
 arg(comm,<argv(1)>)
else
 arg(property,<type>)
 arg(comm,<args>)
endif
arg(storep,<src.p>)
arg(firstuid,0)
arg(lastuid,0)
src.try gotype(<arg(property)>)
while (<src.act>!=<arg(firstuid)>)&&(<src.act.type>==<arg(property)>)&&(<src.act>!=<arg(lastuid)>)
 arg(nowuid,<src.act>)
 src.act.try(<arg(comm)>)
 if (<arg(firstuid)>==0)
  arg(firstuid,<arg(nowuid)>)
 endif
 arg(lastuid,<src.act>)
 src.try gotype(<arg(property)>)
endwhile
src.go <storep>

[function array_add]//adds at the end, returns the index
//<argv(0)>   array name
//<argv(1)>   value 
arg(u,0) 
while (<?safe <argv(0)>_<arg(u)>?>) 
 arg(u,<arg(u)>+1) 
endwhile
<argv(0)>_<arg(u)>=<argv(1)>
return <arg(u)> 

[function array_find]//returns index of the value (-1 if not found)
//<argv(0)>   array name
//<argv(1)>   value 
arg(u,0) 
while (<?safe <argv(0)>_<arg(u)>?>) 
 if (!strcmpi(<?<argv(0)>_<arg(u)>?>,<?argv(1)?>)) 
  return <arg(u)> 
 endif 
 arg(u,<arg(u)>+1) 
endwhile 
return -1 

[function array_remove]//removes the indexed item (and moves the rest)
//<argv(0)>   array name
//<argv(1)>   index 
arg(u,<argv(1)>) 
while (<?safe <argv(0)>_<arg(u)>?>) 
 if(<?safe <argv(0)>_<eval arg(u)+1>?>)
  <argv(0)>_<arg(u)>=<safe <argv(0)>_<eval arg(u)+1>>
 else 
  <argv(0)>_<arg(u)>="" //jen smazat
 endif
 arg(u,<arg(u)>+1) 
endwhile 

[function array_findremove]//removes item with this value (uses array_remove and array_find) 
//<argv(0)>   array name
//<argv(1)>   value 
arg(cislo,<?array_find(<argv(0)>,<argv(1)>)?>) 
if (<arg(cislo)>>-1) 
array_remove(<argv(0)>,<arg(cislo)>) 
return 1 
endif 
return 0 

[function array_foreach]//runs functions so that each of them gets the value as its argument
//<argv(0)>   array name
//<argv(1)>   function name
arg(u,0) 
while (<?safe <argv(0)>_<arg(u)>?>) 
 <argv(1)>(<?<argv(0)>_<arg(u)>?>) 
 arg(u,<arg(u)>+1) 
endwhile 

[function array_foreachuid]//for arrays containing UIDs - runs the given functinos on all the objects. If some of the UIDs is invalid, removes the item.
//<argv(0)>   array name
//<argv(1)>   function name
arg(u,0) 
while (<?safe <argv(0)>_<arg(u)>?>) 
 if (finduid(<?<argv(0)>_<arg(u)>?>))
 //elseif (safe finduid(<?<argv(0)>_<arg(u)>?>).ischar) 
  finduid(<?<argv(0)>_<arg(u)>?>).<?argv(1)?> 
 else
  array_remove(<argv(0)>,<arg(u)>)
 endif
 arg(u,<arg(u)>+1) 
endwhile 

[function array_dump]//sysmessaging all the names and values - usable from console too
//<argv(0)>   array name
arg(u,0) 
while (<?safe <args>_<arg(u)>?>) 
 src.smsg <args>_<arg(u)>=<?<args>_<arg(u)>?> 
 arg(u,<arg(u)>+1) 
endwhile 

[function array_clear]//what could it be? :)
//<argv(0)>   array name
arg(u,0) 
while (strlen(<?safe <argv(0)>_<arg(u)>?>)>0)
<argv(0)>_<arg(u)>=""
arg(u,<arg(u)>+1) 
endwhile 

[function array_size]//size of given array
//<argv(0)> array name
arg(u,0)
while(<?safe <argv(0)>_<u>?>)
 arg(u,<arg(u)>+1)
endwhile
return <arg(u)>

[function array_get]//gets value of item on given position
//<argv(0)> array name
//<argv(1)> position
if(<?safe <argv(0)>_<argv(1)>?>)
 return<?<argv(0)>_<argv(1)>?>
else
 return -1
endif

[function array_getremove]//gets value of item on given position and removes the item
//<argv(0)> array name
//<argv(1)> position
arg(retval,<?array_get(<argv(0)>,<argv(1)>)?>) 
if (<arg(retval)>>-1) 
 array_remove(<argv(0)>,<argv(1)>) 
endif 
return<retval> //either the desired value or -1


[function array_test]
src.smsg ADD 1 3 5 7 9: dump: remove 1: dump: findremove 5: dump: clear: dump
array_add("var.test",1)
array_add("var.test",3)
array_add("var.test",5)
array_add("var.test",7)
array_add("var.test",9)
src.smsg added
src.smsg sizeof var.test: <array_size(var.test)>
array_dump("var.test")
array_remove("var.test",1)
src.smsg removed first
array_dump("var.test")
array_findremove("var.test",5)
src.smsg findremoved 5
array_dump("var.test")
array_clear("var.test")
src.smsg cleared
array_dump("var.test")

[function input]//nadpis, value, default
tag(input_text,"<argv(0)>")
tag(input_valname,"<argv(1)>")
tag(input_default,"<argv(2)>")
dialog d_input

[dialog d_input]
350,350
argo.tag(sirka,400)
argo.tag(vyska,<eval 23+(2*d_def_radek_vyska)+(3*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_zpruhledni

argo.settext(100,<tag(input_text)>)
argo.text(<argo.dialog_textpos(0,0)>,0481,100)
argo.settext(101,<tag(input_default)>)//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.textentry(<argo.dialog_textpos(1,0)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,1,101)
//30 60 350 20 4 0 2
argo.button(<d_def_okraj>,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb7,0fb9,1,0,1)//ok
argo.button(<d_def_okraj>+33,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb1,0fb3,1,0,0)//cancel

[DIALOG d_input BUTTON]
on=0
tag(input_text,"")
tag(input_valname,"")
tag(input_default,"")
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

ON=1
<tag(input_valname)>="<argtxt(1)>"
tag(input_text,"")
tag(input_valname,"")
tag(input_default,"")
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

[function informovat]//kolik radku,array?,nadpis,jmeno stringu,SetLocation_arg1,SetLocation_arg2
if (argv(1))
 arg(i,0)
 while (i<argv(1))
  tag(informovat_text,<?safe tag(informovat_text)?><?<argv(3)>[i]?><br>)
  arg(i,#+1)
 endwhile
else
 tag(informovat_text,<?<argv(3)>?>)
endif
if (argvcount == 6)
 dialog(d_informovat,<argv(0)>,<argv(2)>,<argv(4)>,<argv(5)>)
else
 dialog(d_informovat,<argv(0)>,<argv(2)>)
endif

[dialog d_informovat]
if (argvcount == 4)
 argo.SetLocation=<argv(2)>,<argv(3)>
else
 argo.SetLocation=50,50
endif
argo.tag(sirka,400)
argo.tag(vyska,<eval ((1+argv(0))*d_def_radek_vyska)+(3*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,<argv(0)>)
argo.dialog_zpruhledni

argo.texta(<argo.dialog_textpos(0,0)>,0481,<argv(1)>)
argo.settext(100,"<tag(informovat_text)>")//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.htmlgump(<argo.dialog_textpos(1,0)>,argo.tag(sirka)-(d_def_odsazeni+(2*(d_def_okraj+d_def_skvira))),argv(0)*d_def_radek_vyska,100,0,1)
//argo.button(<d_def_okraj>,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb7,0fb9,1,0,1)//ok
//argo.button(<d_def_okraj>+33,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb1,0fb3,1,0,0)//cancel
tag(informovat_text,"")

[dialog d_informovat text]

[dialog d_informovat button]
on=0

[function potvrdit]//funkce pro ANO, text
tag(potvrd_funkce,"<argv(0)>")
tag(potvrd_text,"<argv(1)>")
dialog d_potvrdit

[dialog d_potvrdit]
100,100
argo.tag(sirka,300)
argo.tag(vyska,<eval 23+(5*d_def_radek_vyska)+(3*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,4)
argo.dialog_zpruhledni

argo.settext(101,Potvrzeni prikazu)
argo.text(<argo.dialog_textpos(0,0)>,0481,101)
argo.settext(100,<tag(potvrd_text)>)//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.HTMLGUMP(<argo.dialog_textpos(1,0)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),4*d_def_radek_vyska,100,0,1)
argo.button(<d_def_okraj>,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb7,0fb9,1,0,1)//ok
argo.button(<d_def_okraj>+33,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb1,0fb3,1,0,0)//cancel

[DIALOG d_potvrdit BUTTON]
on=0
tag(potvrd_funkce,"")
tag(potvrd_text,"")

ON=1
debugtry(<tag.potvrd_funkce>)
tag(potvrd_funkce,"")
tag(potvrd_text,"")

[function f_dialog_YesNo] // funkce Yes No pro potvrzeni jednoduchych akci - jde jen o hotove telo dialogu
SetLocation=285,250
tag(sirka,230)
tag(vyska,90)
dialog_prvni
dialog_pozadi(<tag(nexty)>,1)
dialog_pozadi(<tag(nexty)>,2,102,101)
dialog_zpruhledni

if (strlen(<argv(0)>))
  texta(<dialog_textpos(0,0)>,d_def_WriteColor,<argv(0)>)
else
  texta(<dialog_textpos(0,0)>,d_def_WriteColor,Opravdu chces provest akci?)
endif
//Yes button
button(<tag(sloupec_x[0])>+<d_def_skvira>,<tag(obj_y[1])>+15,4005,4007,1,0,1)
texta(<tag(sloupec_x[0])>+<d_def_button_sirka>+<d_def_skvira>,<tag(obj_y[1])>+15,d_def_WriteColor,Ano)
//No button
button(<tag(sloupec_x[1])>+<d_def_skvira>,<tag(obj_y[1])>+15,4005,4007,1,0,2)
texta(<tag(sloupec_x[1])>+<d_def_button_sirka>+<d_def_skvira>,<tag(obj_y[1])>+15,d_def_WriteColor,Ne)

[function singleparse]
var(firstnumb,<StrGetTok("<args>",0," ")>)
var(secondnumb,<StrGetTok("<args>",1," ")>)

[function changestat]//stat,o kolik
if (<argv(1)>)
 arg(actualstat,<?<argv(0)>?>)
 arg(changedstat,<eval <arg(actualstat)>+(<argv(1)>)>)
 if (arg(changedstat)<0)
  arg(changedstat,0)
 endif
 if (arg(changedstat)!=arg(actualstat))
  <argv(0)>=<arg(changedstat)>
 endif
endif

[function promilerange]
//power
//effect min
//effect max
return <eval <argv(1)>+(((<argv(2)>-<argv(1)>)*<argv(0)>)/1000)>

[function changestatpoint]//statpoint,o kolik
if (argv(1))
 arg(actualstat,<?<argv(0)>?>)
 arg(changedstat,<eval <arg(actualstat)>+(<argv(1)>)>)
 arg(maxstat,<?max<argv(0)>?>)
 if (arg(changedstat)<0)
  arg(changedstat,0)
 elseif (arg(changedstat)>arg(maxstat))
  arg(changedstat,<arg(maxstat)>)
 endif
 if (arg(changedstat)!=arg(actualstat))
  <argv(0)>=<arg(changedstat)>
 endif
endif

[function truncatestat]
arg(maxstat,<?max<args>?>)
if (<?<args>?>>arg(maxstat))
 <args>(<arg(maxstat)>)
endif

[function truncateallstats]
truncatestat(stam)
truncatestat(hits)
truncatestat(mana)

[function intset]
tag(intellev,<eval args>)
intel=<eval args>

[function dexset]
tag(dexlev,<eval args>)
dex=<eval args>

[function strset]
tag(strlev,<eval args>)
str=<eval args>

[function vitset]
tag(vitlev,<eval args>)
vit=<eval args>

[function levelset]
tag(level,<eval args>)

//[function averhit]
//arg(storehits,<act.hits>)
//if (argvcount)
// arg(pokusu,<eval args>)
//else
// arg(pokusu,1000)
//endif
//arg(p,<src.p>)
//combat_main
//src.smsg utok:<damage_celkem>
//src.p=<arg(p)>
//arg(damage_done,0)
//arg(damage_min,10000)
//arg(damage_max,0)
//arg(u,<arg(pokusu)>)
//while (arg(u))
// act.hits=10000
// act.damage <damage_celkem> dam_physical <uid>
// arg(sebrano_hits,<eval 10000-<act.hits>>)
// arg(damage_done,<eval <arg(damage_done)>+<arg(sebrano_hits)>>)
// if (arg(sebrano_hits)<arg(damage_min))
//  arg(damage_min,<arg(sebrano_hits)>)
// endif
// if (arg(sebrano_hits)>arg(damage_max))
//  arg(damage_max,<arg(sebrano_hits)>)
// endif
// arg(u,<u>-1)
//endwhile
//
//src.smsg celkem damage:<arg(damage_done)>
//arg(damage_aver,<eval (<arg(damage_done)>*1000)/<arg(pokusu)>>)
//src.smsg prumerne damage:<eval <arg(damage_aver)>/1000>.<eval <arg(damage_aver)>-((<arg(damage_aver)>/1000)*1000)>
//
////src.smsg minimalni damage:<arg(damage_min)> - <eval (<arg(damage_min)>*<arg(pokusu)>*1000)/<arg(damage_aver)>>%%
////src.smsg maximalni damage:<arg(damage_max)> - <eval (<arg(damage_max)>*<arg(pokusu)>*1000)/<arg(damage_aver)>>%%
//
//f_getspeed(<zbran_char>)
//arg(damage_timed,<eval (6000/<tag.hitspeed>)*<arg(damage_aver)>>)
//
//src.smsg relativni damage za cas:<eval <arg(damage_timed)>/100000>.<eval <arg(damage_timed)>-((<arg(damage_timed)>/100000)*100000)>
//act.hits=<arg(storehits)>

[function nudgeup]
zset(<eval p_z+(args)>)

[function nudgedown]
zset(<eval p_z-(args)>)

[function zset]
p(<p_x>,<p_y>,<eval (args)>,<mapplane>)
updatex

[function fixbow]
return 1	//docasne zastavena funkce - buguje to opravi to snad nekdo kdo vi co to ma delat poradne :)
switchbow

[function switchbow]
if (findlayer(2))
  return <findlayer(2).switchthisbow>
endif

[function switchthisbow]
if (isbow)
  if (type==t_weapon_bow_run)
    arg(createID,<strmid(<baseid>,0,<STRLEN(<baseid>)>-4)>)
    arg(damset,-1)
    src.classmessage(Zbran v 'Stand' modu)
  else
    if (src.profession==class_ranger)||(src.profession==class_thief)||(src.isgm)
      arg(createID,<baseid>_run)
      arg(damset,<typedef.dam>)
      src.classmessage(Zbran v 'Run' modu)
    else
      src.redmessage(Nesmis pouzivat Run zbrane)
      return 1
    endif
  endif
  if (findres(itemdef,arg(createID))!=i_unused)
    src.newitemsafe(<arg(createID)>)
    arg(createdbow,<lastnew>)
    if (<arg(createdbow).baseid>==<arg(createID)>)
      arg(createdbow).copyprops(<uid>)
      if (arg(damset)>-1)
        arg(createdbow).typedef.dam=<arg(damset)>
      endif
      remove
      arg(createdbow).equip
      src.skill(-1)
      return 1
    endif
  endif
endif
src.redmessage(Prepnuti nemozne - neexistuje odpovidajici verze zbrane.)
return 0

[function copyprops]//uid odkud kopirovat
link=<args>
morep=<link.morep>
more1=<link.more1>
more2=<link.more2>
color=<link.color>
name="<link.name>"
//timer=<link.timer>
amount=<link.amount>
destroyevents
copyevents(<safe link.events>,)
//kopirovani tagu:
tag(clear)
arg(u,0)
while (<u><<link.tag(getcount)>)
 arg(tagname,<link.tag(enum,<u>)>)
 //try tag.radek[<eval <strmid(<arg(tagname)>,5,0ff)>>]=<?tag.<arg(tagname)>?>
 tag(<arg(tagname)>,<?link.tag.<arg(tagname)>?>)
 arg(u,#+1)
endwhile
link=<link.link>

[function copyevents]
arg(i,<argvcount>-1)
while arg(i)
  events +<argv(arg(i)-1)>
  arg(i,#-1)
endwhile

[events e_dummy]
on=@step
return 1

on=@gethit
hits=10000
return 0

on=@death
hits=<maxhits>
return 1

[function GMclient]
var(ntimes,1)
serv.allclients(GMonetime(<args>))
var(ntimes,"")

[function GMonetime]
if (account.plevel>3)
 if (ntimes)
  <args>
  var(ntimes,0)
 endif
endif

[function firstclient]
var(ntimes,1)
serv.allclients(onetime(<args>))
var(ntimes,"")

[function onetime]
if (ntimes)
 <args>
 var(ntimes,0)
endif

[itemdef i_chest_weapset]
id=i_bag

on=@create
attr=attr_move_never
tag(metal,copper)

on=@userdclick
emptycont
color=color_o_<tag.metal>
name=<tag.metal> weapons
weapset(<tag(metal)>)

[function weapset]//metals
arg(u,0)
while (arg(u)<33)
 newloot("<n_weapons_armors[u]>_<args>",1)
 arg(u,#+1)
endwhile
newloot(i_arrow_<args>,1000)
newloot(i_xbolt_<args>,1000)
newloot(i_arrow_piercing_<args>,1000)
newloot(i_xbolt_piercing_<args>,1000)

[itemdef i_gate_onehit]
id=i_moongate_red
name=nastavi 1 hp

on=@step
src.hits=1

on=@userdclick
src.hits=1

[itemdef i_chest_items]
id=i_bag
name=Items

on=@create
attr=attr_move_never

on=@userdclick
src.food=<src.def.maxfood>
src.smsg Jidlo nastaveno na max
emptycont
newloot=i_pet_horse_tan
newloot=i_spellbook_8
lastnew.morex=0ffff
newloot=i_bandage,1000
newloot=tm_reagbag
newloot=tmp_ingots
newloot=i_potion_heal
lastnew.dupe(20)
newloot=i_potion_healgreat
lastnew.dupe(20)
newloot=i_potion_invisibility
lastnew.dupe(20)
newloot=i_potion_refreshgreater
lastnew.dupe(20)
newloot=i_drum
newloot=i_pickaxe
NEWLOOT=i_mortar_pestle
NEWLOOT=i_keg_potion
NEWLOOT=i_gold,60000
newloot=i_deed_vendor
newloot=i_deed_guildstone

[template tmp_ingots]
container=i_pouch

item=i_scroll_blank,500
item=i_bottle_empty,500

item=i_ingot_copper,500
item=i_ingot_iron,500
item=i_ingot_gold,500
item=i_ingot_silver,500
item=i_ingot_verite,500
item=i_ingot_valorite,500
item=i_ingot_obsidian,500
item=i_ingot_adamantinum,500
item=i_ingot_mithril,500
item=i_ingot_shadow,500

item=i_log_copper,500
item=i_log_iron,500
item=i_log_verite,500
item=i_log_valorite,500
item=i_log_obsidian,500
item=i_log_adamantinum,500
item=i_log_mithril,500

[itemdef i_chest_armorset]
id=i_bag

on=@create
attr=attr_move_never
tag(metal,copper)
tag(armortyp,plate)

on=@userdclick
emptycont
name=<tag.metal> <tag.armortyp> armors
color=color_o_<tag.metal>
armorset(<tag.metal>,<tag.armortyp>)

[function armorset]//kov, druh
arg(u,<f_<argv(1)>defname(0,<argv(0)>)>)
while (arg(u)>0)
 arg(defname,<f_<argv(1)>defname(<u>,<argv(0)>)>)
 newloot(arg(defname))
 arg(u,<u>-1)
endwhile

[function onestring]
if (argvcount)
 arg(u,0)
 arg(text,"")
 while (arg(u)<argvcount)
  arg(text,"<arg(text)><argv(u)> ")
  arg(u,#+1)
 endwhile
 return"<arg(text)>"
endif
return"<args>"

[function onestring2]
if (argvcount)
 arg(u,0)
 arg(text,"")
 while (arg(u)<argvcount)
  arg(text,"<arg(text)>_<argv(u)>")
  arg(u,#+1)
 endwhile
 return"<arg(text)>"
endif
return"<args>"

[function killstotal]
if (<safe isplayer>)
 return <eval tag.monsterkillstotal>+<eval tag.playerkillstotal>
endif
return 0

[itemdef i_allaccounts]
id=i_memory
type=t_eq_script

on=@timer
src=<cont>
tag(max,#+100)
src.accmsg(counts next accounts:<eval tag(max)>)
while (tag(min)<tag(max))
 if !(accountmgr.findenum(<tag(min)>))
   remove
   return 0
 endif
 try accountmgr.findenum(<tag(min)>).<tag(args)>
 tag(min,#+1)
endwhile
if (var(allaccsaving))
  serv.save 1
  timer=5
else
  timer=1
endif
return 1

[function allaccounts]
//if (isplayer)
//  newitemsafe(i_allaccounts)
//  lastnew.equip
//  lastnew.tag(args,"<args>")
//  lastnew.timer=1
//  lastnew.tag(min,1)
//  lastnew.tag(max,0)
//else
  arg(u,1)
  while (accountmgr.findenum(<u>))
   accountmgr.findenum(<u>).<args>
   arg(u,#+1)
  endwhile
//endif

[function getbow]//by enrico
findtype(t_weapon_xbow).equip
findtype(t_weapon_bow_run).equip



[function allplayers]
allaccounts(accountchars(<args>))

[function accountchars]
arg(u,0)
while (arg(u)<chars)
 char(<u>).<args>
 arg(u,#+1)
endwhile

//[function shboun]
//if (npc)
//dospell_final(72,1000)
//lastnew.bounce
//endif

[function shboun]//a tohle pripadne taky rozbil enrico
src.newitemsafe(i_targetshr)
lastnew.equip


[function huu]
show name

//[function shrink]//a tohle pripadne taky rozbil enrico //a zaremoval to tartaros, bo to vubec nema existovat, shrinkovani pres kouzla uz nepouzivame, ze...
//dospell_final(72,1000)

[itemdef i_targetshr]
name=target shrink
id=i_memory
type=t_eq_script

on=@equip
target
src.classmessage(zamer zvire)
timer=15
return 1

on=@timer
remove
return 0

on=@targon_char
if(src.targ.tag(noshrink))
 target Zvire nelze shrinknout
 return 1
else
 SRC.NEWITEM=<src.targ.typedef.icon>
 SRC.ACT.NAME=<SRC.TARG.NAME>
 SRC.ACT.COLOR=<SRC.TARG.COLOR>
 SRC.ACT.tag(my_id,<SRC.TARG.ID>)
 SRC.ACT.TAG(MYHITS,<SRC.TARG.HITS>)
 SRC.ACT.TAG(MYFOOD,<SRC.TARG.FOOD>)
 src.act.more1=1
 SRC.ACT.TYPE=t_figurine_2
 SRC.ACT.BOUNCE
 SRC.TARG.REMOVE
 remove
 return 1
endif



[function equipjezditka]
SRC.NEWITEM=<src.targ.typedef.icon>
SRC.ACT.NAME=<SRC.TARG.NAME>
SRC.ACT.COLOR=<SRC.TARG.COLOR>
SRC.ACT.MORE1=<SRC.TARG.ID>
SRC.ACT.BOUNCE
SRC.TARG.REMOVE
remove




[function tempspawn]//ID, amount, homedist
NEWITEMsafe(i_spawnfull_timer)
lastnew.timer=1
lastnew.p=<p>
lastnew.more1=0
arg(timeruid,<lastnew>)
NEWITEMsafe(01ea7)
lastnew.events +t_char_tempspawn
lastnew.P=<P>
lastnew.TYPE=34
lastnew.more=<argv(0)>
lastnew.amount=<argv(1)>
lastnew.morep=1,2,<argv(2)>
lastnew.ATTR=0b0
finduid(<arg(timeruid)>).link=<lastnew>

[typedef t_char_tempspawn]
on=@timer
if (morem)
 if !(more2)
  remove
 endif
endif
morem=<morem>+1
return 0

[function myaction]
if (action!=-1)
 if (action==067)
  return"Wandering"
 elseif (action==076)
  return"AFK"
 elseif (action==06d)
  return"Going Home"
 elseif (action==00) 
  return"Skill_Alchemy"
 endif
 return<action>
endif
if (findid(i_discordance_timer))
 if (findid(i_discordance_timer).more1==1)
  return"Skill_Discordance"
 endif
elseif (findid(i_medit_timer))
 if (findid(i_medit_timer).timer>(-1))
  return"Skill_Meditation"
 endif
elseif (findid(i_peacemaking_timer))
 if (findid(i_peacemaking_timer).more1==1)
  return"Skill_Peacemaking"
 endif
elseif (findid(i_potion_poison_timer))
 if (findid(i_potion_poison_timer).more1==1)
  return"Skill_Poisoning"
 endif
elseif (findid(i_provo_timer))
 if (findid(i_provo_timer).more1>1)
  return"Skill_Provocation"
 endif
elseif (findid(i_taming))
 if (findid(i_taming).timer>(-1))
  return"Skill_Taming"
 endif
elseif (findid(i_hiding_timer))
  if (findid(i_hiding_timer).more1==1)
    return"Hiding"
  endif
elseif (findid(i_mem_lockpick))
 return"Skill_Lockpicking"  
elseif (findid(i_vyroba_latky))
 return"Skill_Tailoring"
elseif (findid(i_delayer))
 return"Delaying"
endif

[itemdef i_ismyfriend]
id=i_memory
layer=77
type=t_src_changer
name=ismyfriendsrc

on=@equip
var(varismyfriend,<link.ismyfriend>)
remove
return 1

[function ismyfriend]
if (ispet)//nejsem svuj pan
 return <finduid(<master>).ismyfriend>
elseif (src.ispet)//src neni svuj pan, change src.
 newitemsafe(i_ismyfriend)
 lastnew.link=<uid>
 safe(finduid(<src.master>).equip(<lastnew>))//debug
 return <varismyfriend>
else
 return <f_ismyfriend>
endif

[function f_ismyfriend]
if (<src>==<uid>)
 //say shodne UID
 return 1
elseif (isonmyside)
 //say isonmyside
 return 1
elseif (ismyguard)
 //say ismyguard
 return 1
elseif (iguardhim)
 //say iguardhim
 return 1
//elseif (isinmyguild)
// say shodna guilda
// return 1
//elseif (isinfriendguild)
// say pratelska guilda
// return 1
//elseif (isinmyparty)
// say party
// return 1
endif
return 0

[function isonmyside]
if (isplayer)
 if (src.isplayer)
  if (src.tag(realm)==tag(realm))
   return 1
  endif
 endif
else//jsem NPC
 if (flag_spawned)
  if (src.flag_spawned)//obe ze spawnu
   if (ismurderer)
    if (src.ismurderer)
     return 1
    endif
   else
    if !(src.ismurderer)
     return 1
    endif
   endif
  endif 
 endif
endif
return 0

[function mark]
morep=<src.p>

[function ismyguard]
if (npc)
 if (typedef.tag(guard))
  if (src.isplayer)
   if (typedef.tag.guard==1)
    if (src.tag(realm)>0) 
     if (!(src.flag_criminal))||(isfrommytown)
      if !(src.ismurderer)
       if (src.tag(realm)==1)
        return 1
       endif
      endif
     endif
    endif
   elseif (typedef.tag.guard==2)
    if (src.tag(realm)>0)
     if (src.tag(realm)==2)
      return 1
     endif
    endif
   endif
  endif
 endif
endif
return 0

[function iguardhim]
if (isplayer)
 if (src.npc)
  if (src.typedef.tag(guard))
   if (src.typedef.tag(guard)==1)
    if !(flag_criminal)
     if (tag(realm)==1)
      return 1
     endif
    endif
   elseif (src.typedef.tag(guard)==2)
    if (tag(realm)==2)
     return 1
    endif
   endif
  endif
 endif
endif
return 0

[function isinmyguild]
if (<src>!=<uid>)
 if (GuildMem)
  if (src.GuildMem)
   if (GuildMem==src.GuildMem)
    //if (findid(i_memory_guild).link.SrcIsCandidate(<uid>))
     //if (findid(i_memory_guild).tag.abbrev)
     //return 1
     //endif
    //else
     return 1
    //endif
   endif
  endif
 endif
endif
return 0

[function isinfriendguild]
return 0

[function isinmyparty]
return 0

[function ismymaster]
if (<master>==<src>)
 return 1
endif
return 0

[function isnonmobile]
return <eval(<flag_freeze>+<flag_stone>+<flag_immobile>)>

[function speedmet]
newitemsafe(i_speed)
lastnew.p=<p>
lastnew.link=<uid>
lastnew.timerd=1

[function lognewitemsafe]
arg(sfnact,<act>)
newitem(<hval args>)
accmsg(creates item uid=<lastnew> (<lastnew.name>))
act=<arg(sfnact)>

[function newitemsafe]
if (<args>)
  arg(sfnact,<act>)
  newitem(<hval args>)
  act=<arg(sfnact)>
endif

[function f_newitemsafe]
if !(<itemdone>)
 newitemsafe(<args>)
 var(itemdone,1)
endif

[function random2]
return <eval {<argv(0)> <argv(1)>}>

[function damstats]
src=<uid>
arg(trydam,5)
arg(tryarmor,0)
while (arg(tryarmor)<=150)
 eqlayers(setarmor(<arg(tryarmor)>))
 arg(trydam,5)
 while (arg(trydam)<=150)
  arg(celkdam,0)
  arg(u,0)
  while (arg(u)<1000)
   hits=1000
   damage <arg(trydam)> dam_physical
   arg(celkdam,<arg(celkdam)>+<eval 1000-<hits>>)
   arg(u,#+1)
  endwhile
  logmsg("a:<?arg(tryarmor)?>,d:<?arg(trydam)?>, celkdam:<?arg(celkdam)?>")
  arg(trydam,<arg(trydam)>+5)
 endwhile
 arg(tryarmor,<arg(tryarmor)>+5)
endwhile

[function setarmor]
more1=-1
def.armor=<args>
drop
equip

[function freetag]
if !(tag(<args>)) //kdyz ma tag hodnotu 0
  tag.remove(<args>)
endif

[function destroyevents]
while (<eval events>)
 removefirstevent(<events>)
endif

[function removefirstevent]
events -<argv(0)>

[function allstats]
int=<args>
dex=<args>
str=<args>
maxhits=<args>
maxmana=<args>
maxstam=<args>

[function changeallstats]
int=<eval int+(args)>
dex=<eval dex+(args)>
str=<eval str+(args)>
maxhits=<eval maxhits+(args)>
maxmana=<eval maxmana+(args)>
maxstam=<eval maxstam+(args)>

[function isstaff]
if (type==T_WEAPON_MACE_STAFF)
 return 1
endif
return 0

[function iscrook]
if (type==T_WEAPON_MACE_CROOK)
 return 1
else
 return 0
endif

[function istwohandmace]
if (typedef.dispid==i_hammer_war)
elseif (typedef.dispid==i_maul)
else
return 0
endif
return 1

[function isshield]
if (type==T_shield)
 return 1
endif
return 0

[function ismace]
if (type==T_WEAPON_MACE_SMITH)
 return 1
endif
return 0

[function isfencing]
if (typedef.dispid==i_dagger)||(id==i_knife_skinning)
 return 0
elseif (type==T_WEAPON_FENCE)
 return 1
endif

[function issword]
if (typedef.dispid==i_hatchet)
 return 0
elseif (type==T_WEAPON_sword)
 return 1
endif

[function isleather]
if (type==t_armor_leather)
 if (typedef.tdata3==0)   
  return 1                
 endif                    
endif                     
return 0                  
                          
[function isstudded]      
if (isarmor)||(isshield)		    
 if (typedef.tdata3==armortype_studded)     
  return 1                                    
 endif                                        
endif                                         
return 0                             
                                              
[function isring]
if (isarmor)||(isshield)		
 if (typedef.tdata3==armortype_ring)  
  return 1                            
 endif                                
endif                                 
return 0                              
                                      
[function ischain]
if (isarmor)||(isshield)		
 if (typedef.tdata3==armortype_chain) 
  return 1                            
 endif                                
endif                                 
return 0                              

[function isbone]    
if (isarmor)				
 if (typedef.tdata3==armortype_bone)  
  return 1                            
 endif                                
endif                                 
return 0                              
                                      
[function isbow]
if (typedef.dispid==i_bow)		
elseif (typedef.dispid==i_crossbow)
elseif (typedef.dispid==i_crossbow_heavy)
else
return 0
endif
return 1

[function isbolt]
if (typedef.dispid==i_xbolt)		
else
return 0
endif
return 1


[function isarrow]
if (typedef.dispid==i_arrow)		
else
return 0
endif
return 1


[function Isplate]
if (isarmor)||(isshield)
 if (typedef.tdata3==armortype_plate)
  return 1
 endif
endif
return 0
                              
[function ispet]
if(flags)
 if (<flags>&statf_pet)
  return 1
 endif
endif
return 0

[function isjezditko]
if (obody==c_llama) || (obody==c_horse_tan) || (obody==c_horse_brown_dk) || (obody==c_horse_brown_lt) || (obody==c_a_mustang_black) || (obody==c_a_mustang_chocolate) || (obody==c_a_mustang_crimson) || (obody==c_a_mustang_grey_pack) || (obody==c_a_mustang_grey) || (obody==c_a_mustang_pamamino) || (obody==c_a_mustang_pamamino_pack) || (obody==c_a_mustang_redroan) || (obody==c_a_mustang_redroan_pack) || (obody==c_a_mustang_roan_pack) || (obody==c_a_mustang_roan) || (obody==c_a_mustang_sky) || (obody==c_a_mustang_skygray) || (obody==c_a_mustang_wimmimate) || (obody==c_a_mustang_wimmimate_pack) || (obody==c_ostard_desert) || (obody==c_ostard_forest) || (obody==c_ostard_zostrich) || (obody==c_ostard_zostrich_jmeno1) || (obody==c_ostard_zostrich_jmeno2) || (obody==c_ostard_zostrich_jmeno3) || (obody==c_ostard_zostrich_green)
 return 1
endif
return 0

[function isconjured]
if (<flags>&statf_conjured)
return 1
endif
return 0


[function ishungry]
if ({0 9} > <eval <food>+2>)&&(isgm==0)
return 1
endif
return 0

[function ishungrymsg]
if (ishungry)
redmessage(Hlad te rusi ve tvem soustredeni...)
return 1
endif
return 0

[function isthrowing_monster]
if(obody==c_ogre)||(obody==c_ogre_lord)||(obody==c_ettin)||(obody==c_cyclops)||(obody==c_elem_rock)||(obody==c_elem_ice_archer)||(obody==c_elem_snow_archer)||(obody==c_elem_rock)||(obody==c_moria_jeskyni_Obr)
 return 1
else
 return 0
endif

[function isshooting_monster]
if(obody==c_skeleton_archer)||(obody==c_skeleton_archer_lvl_2)||(obody==c_skeleton_archer_lvl_3)||(obody==c_skeleton_archer_lvl_4)
 return 1
else
 return 0
endif

[function ismistr_povolani]
arg(u,0)
//finduid(094dfd).sysmessage(src: <src>, uid: <uid>)
while (arg(u)<50) 
 arg(skillname,<findres(skill,<arg(u)>).name>) 
 if !(strcmpi(<obody>,mistr_<arg(skillname)>))
  //finduid(094dfd).sysmessage(<obody>)
  return 1
 endif 
 arg(u,#+1)
endwhile
return 0

[function armorsum]
return <eval((avervalue(<typedef.armor>)/2)+ac)>

[function avervalue]
return <eval (<argv(0)>+<argv(1)>)>

[function greykarma]
if (body==c_man)||(body==c_woman)
karma=-1
else
karma=0
endif

[FUNCTION iscasting]
if (action==skill_magery)
 return 1
elseif (findid(i_customspell_timer))
 return 1
else
 return 0
endif

[function isinvis]
if (flags&0802008)
 return(1)
else
 return(0)
endif

[FUNCTION MASTER]
if (ispet)
 if (memoryfindtype(memory_ipet).link.ispet)
  return <memoryfindtype(memory_ipet).link.master>
 else
  return <memoryfindtype(memory_ipet).link>
 endif
else
 return <uid>
endif

[function addexp]
if(<tag(hypno_mystik)>) //mystik v monstru neco zabil
 tag(experience,#+(<eval(args)>)) //jen expy, zadny levely ani nic.
else
 if ((<strlen(<tag(rp_rank)>)>) && (<eval(args)> > 0))
  arg(exptogive,<eval(args)>+(<eval(args)>*<RP_Rank[<tag(rp_rank)>]>)/100)
 else
  arg(exptogive,<eval(args)>)
 endif
 tag(experience,#+<exptogive>) 
 if (isplayer)
  f_srovnatlevel
  newitemsafe(i_addexp)
  lastnew.tag(kolik,<args>)
  safe equip(lastnew)
  racemessage("<sex Ziskal Ziskala> jsi <exptogive> zkusenosti")
 endif
endif

[itemdef i_addexp]
id=i_memory
layer=77
type=t_src_changer
name=temporary addexp

on=@equip
cont.accmsg(Obtained <tag(kolik)> exp. Has now #<eval cont.tag(experience)># experience and #<eval cont.tag(level)># level.)
remove 
return 1

[function animate]
if (src.isplayer)
 if (tag(brain)==6)
 accmsg("Hrac <name> uidcka <uid> se pokusil animatnout brain vendor.")
 src.sysmessage("Toto nelze ozivovat!")
 return 0
 endif
 if (isitem)
  if (type==t_corpse)
   if (link)
    return 1
   endif
   arg(animstoreact,<src.act>)
   sound snd_SPELL_ANIMATE_DEAD
   src.newnpc <morex>
   if !(<safe.lastnewchar.tag(noanimate)>==1) //animatnout nepujde cokoliv, co ma tag(noanimate)
    lastnewchar.eqlayers(nonewbie(remove))
    lastnewchar.findlayer(21).contents(nonewbie(remove))  
    lastnewchar.p=<p>
    lastnewchar.updatex
    //lastnewchar.newitemsafe(i_rune_conjured)
    //lastnew.link=<src>
    //lastnew.timer=600
    //lastnewchar.equip(<lastnew>)
    if (src.isplayer)
     src.newitemsafe(i_rune_vzboureni)
     lastnew.link=<lastnewchar>
     src.equip(lastnew)
    endif
    lastnewchar.newmemory(<src>)
    lastnew.COLOR=memory_ipet|MEMORY_GUARD
    lastnew.more1=04
    lastnew.MORE2=<SERV.TIME>
    lastnew.MOREP=<link.P>
    lastnewchar.FOOD=<lastnewchar.def.maxfood>
    lastnewchar.skill 064
    contents(f_animate_transfer)
    lastnewchar.findlayer(21).contents(nonewbie(drop))
    lastnewchar.FLAGS=(<lastnewchar.FLAGS>|statf_conjured)
    lastnewchar.NEWITEMsafe(i_rune_summon_creature)
    lastnew.LAYER=41
    lastnew.ATTR=026
    lastnew.CONT=<lastnewchar>
    lastnew.LINK=<src>
    lastnew.TIMER=600
    lastnew.MOREX=40
    src.act=<arg(animstoreact)>
    remove
   else
    lastnewchar.remove
    src.sysmessage("Tohle ozivovat nebudes ...")
   endif
  endif
 endif
else
  src.tag(animating,<uid>)
  src.summon(<morex>)
endif
return 1

[function f_animate_transfer]
safe(lastnewchar.equip(<uid>))

[itemdef i_rune_vzboureni]
NAME=timer vzboureni
ID=i_rune_summon_creature 
TYPE=T_EQ_SCRIPT 

on=@create
timer=1

ON=@timer
timer=1
if !(safe.link.npc)
 remove
 return 1
endif
arg(ubermany,<eval (link.tag(experience)/nastaveni_ability_animatemanadrain)+1>)
if (cont.mana>arg(ubermany))
 cont.mana=<eval cont.mana-arg(ubermany)>
 link.memoryfind(<cont>).color=<link.memoryfind(<cont>).color>|02
else
 link.memoryfind(<cont>).remove
 link.attack(<cont>)
 remove
endif
return 1

[itemdef i_rune_conjured]
NAME=conjured creature
ID=i_rune_summon_creature 
TYPE=t_custom_dispellable
layer=70

on=@equip
cont.flags=<link.topobj.flags>|statf_conjured

ON=@unequip
cont.sound snd_SPELL_teleport
cont.go 1,1 //simulace efektu zmizeni
cont.hits=
return 1

on=@timer
remove
return 1

[function nonewbie]
if !(<attr>&attr_newbie)
 <args>
endif

[function uidinfo]
act=<args>
sysmessage Name:<act.name>
sysmessage cont:<act.cont.name>, <act.cont.uid>
sysmessage topobj:<act.topobj.name>, <act.topobj.uid>
sysmessage amount:<act.amount>

[function usering]
safe(findlayer(8).dclick)

[function usehand]
if (findlayer(1))
 findlayer(1).dclick
else
 findlayer(2).dclick
endif

[function guardit]
newitem i_guardit
act.equip

[itemdef i_guardit]
id=i_memory
type=t_eq_script
name=guardit rune

on=@timer
remove
return 1

on=@equip
timer 50
target zamer dvere/kontejner/telepad ktery ma byt strazen.
return 1

on=@userdclick
target zamer spawn strazce.
return 1

on=@targon_item
if (more1)
 if (src.targ.type==t_spawn_char)
  src.targ.tag(negateOnDeath,1)
  link=<more1>
  link.link=<src.targ>
  link=""
  remove
  return 1
 else
  src.targ.say toto neni character spawn.
  remove
  return 1
 endif
else
 more1=<src.targ>
 if (src.targ.type==t_door)||(src.targ.type==t_door_guarded)
  src.targ.type=t_door_guarded
  dclick
  return 1
 elseif (src.targ.type==t_container)||(src.targ.type==t_container_guarded)
  src.targ.type=t_container_guarded
  dclick
  return 1
 elseif (src.targ.type==t_telepad)||(<src.targ.type==t_telepad_guarded)
  src.targ.type=t_telepad_guarded
  dclick
  return 1
 else
  src.targ.say neznamy typ... nastav rucne :P
  src.sysmessage typ: t_door_guarded, t_container_guarded, t_telepad_guarded
  src.sysmessage strazenou vec nalinkovat na spawn strazce
  src.sysmessage more1=barva zpravy
  src.sysmessage dej vedet proc to nastalo at se muze upravit skript
  return 1
 endif
endif

[typedef t_telepad_guarded]
on=@step
if (link.more2)
 src.sysmessage neprojdes dokud je strazce brany nazivu!
 return 1
elseif (src.isplayer)
src.go <morep>
return 1
endif

[typedef t_door_guarded]
on=@userdclick
if (<link>)
  tag(spwnlink,<link>)
  link=""
endif
if (more2==0)
 more2=<dispid>
 morep=<p>
endif
if (dispid!=more2) 
 type=t_door
 dclick
 type=t_door_guarded
 more2=<dispid>
 morep=<p>
elseif (tag(spwnlink).more2)
 if (src.isGM)
  src.sysmessage("Strazce je stale na zivu.")
  type=t_door
  dclick
  type=t_door_guarded
 else
   src.sysmessage("Neprojdes, dokud je strazce pruchodu nazivu!")
 endif
else
type=t_door
dclick
type=t_door_guarded
endif
return 1

on=@timer
dispid=<more2>
p=<morep>
update
return 1

[typedef t_container_guarded]
on=@userdclick
if (link.more2)
 src.sysmessage nedostanes se dovnitr dokud je strazce nazivu!
 return 1
else
open
//return 1 //doufam, ze to nebylo dulezity :) ale mne to sere bo poklady
endif

[function potionmana]
if (findid(i_potion_managreater))
 findid(i_potion_managreater).dclick
 return 1
elseif (findid(i_potion_mana))
 findid(i_potion_mana).dclick
 return 1
else
 findid(i_potion_manaless).dclick
 return 1
endif

[function potioncure]
if (findid(i_potion_curegreat))
findid(i_potion_curegreat).dclick
return 1
elseif (findid(i_potion_cure))
findid(i_potion_cure).dclick
return 1
else
findid(i_potion_cureless).dclick
return 1
endif

[function potionexplosion]
if (findid(i_potion_lava))
findid(i_potion_lava).dclick
return 1
elseif (findid(i_potion_explosiongreat))
findid(i_potion_explosiongreat).dclick
return 1
elseif (findid(i_potion_explosion))
findid(i_potion_explosion).dclick
return 1
else
findid(i_potion_explosionless).dclick
return 1
endif

[function potioninvis]
findid(i_potion_invisibility).dclick

[function potionreactive]
findid(i_potion_reactive).dclick

[function potionreflection]
findid(i_potion_reflection).dclick

[function potionnightsight]
findid(i_potion_nightsight).dclick
[function potionshrink]
if (findid(i_potion_shrink))
findid(i_potion_shrink).dclick
return 1
endif


[function potionheal]
if (findid(i_potion_healgreat))
findid(i_potion_healgreat).dclick
return 1
elseif (findid(i_potion_heal))
findid(i_potion_heal).dclick
return 1
else
findid(i_potion_healless).dclick
return 1
endif

[function potionrefresh]
if (findid(i_potion_refreshgreater))
findid(i_potion_refreshgreater).dclick
return 1
elseif (findid(i_potion_refresh))
findid(i_potion_refresh).dclick
return 1
else
findid(i_potion_refreshless).dclick
return 1
endif

[function potionenergy]
if (findid(i_potion_energygreat))
findid(i_potion_energygreat).dclick
return 1
elseif (findid(i_potion_energy))
findid(i_potion_energy).dclick
return 1
else
findid(i_potion_energyless).dclick
return 1
endif

[function potionstrength]
if (findid(i_potion_strengthgreat))
findid(i_potion_strengthgreat).dclick
return 1
elseif (findid(i_potion_strength))
findid(i_potion_strength).dclick
return 1
else
findid(i_potion_strengthless).dclick
return 1
endif

[function potionclever]
if (findid(i_potion_clevergreat))
findid(i_potion_clevergreat).dclick
return 1
elseif (findid(i_potion_clever))
findid(i_potion_clever).dclick
return 1
else
findid(i_potion_cleverless).dclick
return 1
endif

[function potionagility]
if (findid(i_potion_agilitygreat))
findid(i_potion_agilitygreat).dclick
return 1
elseif (findid(i_potion_agility))
findid(i_potion_agility).dclick
return 1
else
findid(i_potion_agilityless).dclick
return 1
endif


[function potionreflex]
findid(i_potion_reflection).dclick

[function potionmobility]
findid(i_potion_mobility).dclick


[function potionspellshield]
findid(i_potion_shield).dclick

[function potionstoneskin]
findid(i_potion_stoneskin).dclick

[function mshield]
findid(i_customscroll_manashield).dclick


[function bandage]
findid(i_bandage).dclick

[function bandageblood]
findid(i_bandage_bloody).dclick

[function regy]
dialog d_regy

[function f_personal_light_level]
//tag(packet,"04e <split4bytes(<uid>)> <hval(<argv(0)>)>")
sendpacket(04e <split4bytes(<uid>)> <hval <argv(0)>>)

[function huedeffect]
//arg(htype,<argv(0)>)
//itemid,<argv(1)>
//speed,<argv(2)>
//frames,<argv(3)>
//explode,<argv(4)>
//hue,<argv(5)>
//fixdir,<argv(6)>
//rendermode,<argv(7)>
serv.allclients(sendpacket(0c0 <hval(<argv(0)>)> <split4bytes(<uid>)> <split4bytes(<src>)> <split2bytes(<nid(<argv(1)>)>)>  <split2bytes(<p_x>)> <split2bytes(<p_y>)> <hval(<p_z>)> <split2bytes(<src.p_x>)> <split2bytes(<src.p_y>)> <hval(<src.p_z>)> <hval(<argv(2)>)> <hval(<argv(3)>)> 00 00 <hval(<argv(6)>)> <hval(<argv(4)>)> <split4bytes(<argv(5)>)> <split4bytes(<argv(7)>)>))

//i_fire_column
[function countframes]
echo f_countframes(<?<args>.dupelist?>)

[function f_countframes]
return <argvcount>

[function srcchangehighlight]//src gets the packet, default is the target char
src.sendpacket(<changehighlightpacket(<args>)>)

[FUNCTION changehighlight]//uid to get the packet,highlight
arg(sendto,<finduid(argv(0))>)
if (sendto)//target valid
 src.smsg sending to <arg(sendto)>
 sendpacket(<arg(sendto).changehighlightpacket(<argv(1)>)>)
endif
src.smsg not sending

[FUNCTION changehighlightpacket]//returns packet
return"077 <split4bytes(<hval uid>)> <split2bytes(<nid(body)>)> <split2bytes(<p_x>)> <split2bytes(<p_y>)> <hval(p_z)> <hval(dir)> <split2bytes(<color>)> 00 <argv(0)>"

[FUNCTION split2bytes]
return"<?hval(<argv(0)> >> 8)?> <?hval(<argv(0)>&0ff)?>"

[FUNCTION split4bytes]
return"<hval (<argv(0)>&0ff000000)/01000000> <hval (<argv(0)>&0ff0000)/010000> <hval (<argv(0)>&0ff00)/0100> <hval <argv(0)>&0ff>"
//return"<?hval(<argv(0)> >> 24)?> <?hval((<argv(0)> >> 16)&0ff)?> <?hval((<argv(0)> >> 8)&0ff)?> <?hval(<argv(0)>&0ff)?>"

[dialog d_regy]
100,0
resizepic 100 100 9250 400 180
tilepic 120 120 3962
tilepic 125 125 3962
tilepic 160 120 3963
tilepic 165 125 3963
tilepic 210 120 3972
tilepic 215 125 3972
tilepic 240 120 3973
tilepic 245 125 3973
tilepic 280 120 3974
tilepic 285 125 3974
tilepic 320 120 3976
tilepic 325 125 3976
tilepic 360 120 3980
tilepic 365 125 3980
tilepic 400 120 3981
tilepic 405 125 3981
htmlgump 130 140 40 20 0 0 0 0
htmlgump 170 140 40 20 1 0 0 0
htmlgump 215 140 40 20 2 0 0 0
htmlgump 250 140 40 20 3 0 0 0
htmlgump 290 140 40 20 4 0 0 0
htmlgump 330 140 40 20 5 0 0 0
htmlgump 370 140 40 20 6 0 0 0
htmlgump 410 140 40 20 7 0 0 0

tilepic 120 170 3960
tilepic 125 175 3960
tilepic 160 170 3961
tilepic 165 175 3961
tilepic 210 170 3964
tilepic 215 175 3964
tilepic 240 170 3965
tilepic 245 175 3965
tilepic 280 170 3966
tilepic 285 175 3966
tilepic 320 170 3967
tilepic 325 175 3967
tilepic 360 170 3968
tilepic 365 175 3968
tilepic 400 170 3969
tilepic 405 175 3969
tilepic 440 170 3970
tilepic 445 175 3970
htmlgump 130 190 40 20 8 0 0 0
htmlgump 170 190 40 20 9 0 0 0
htmlgump 215 190 40 20 10 0 0 0
htmlgump 250 190 40 20 11 0 0 0
htmlgump 290 190 40 20 12 0 0 0
htmlgump 330 190 40 20 13 0 0 0
htmlgump 370 190 40 20 14 0 0 0
htmlgump 410 190 40 20 15 0 0 0
htmlgump 450 190 40 20 16 0 0 0

tilepic 120 220 3971
tilepic 125 225 3971
tilepic 160 220 3975
tilepic 165 225 3975
tilepic 210 220 3977
tilepic 215 225 3977
tilepic 240 220 3978
tilepic 245 225 3978
tilepic 280 220 3979
tilepic 285 225 3979
tilepic 320 220 3982
tilepic 325 225 3982
tilepic 360 220 3983
tilepic 365 225 3983
tilepic 400 220 3984
tilepic 405 225 3984
tilepic 440 220 3985
tilepic 445 225 3985
htmlgump 130 240 40 20 17 0 0 1
htmlgump 170 240 40 20 18 0 0 0
htmlgump 215 240 40 20 19 0 0 0
htmlgump 250 240 40 20 20 0 0 0
htmlgump 290 240 40 20 21 0 0 0
htmlgump 330 240 40 20 22 0 0 0
htmlgump 370 240 40 20 23 0 0 0
htmlgump 410 240 40 20 24 0 0 0
htmlgump 450 240 40 20 25 0 0 0

[dialog d_regy text]
<?rescount i_reag_black_pearl?>
<?rescount i_reag_blood_moss?>
<?rescount i_reag_garlic?>
<?rescount i_reag_ginseng?>
<?rescount i_reag_mandrake_root?>
<?rescount i_reag_nightshade?>
<?rescount i_reag_sulfur_ash?>
<?rescount i_reag_spider_silk?>
<?rescount i_reag_batwing?>
<?rescount i_reag_blackmoor?>
<?rescount i_reag_blood_spawn?>
<?rescount i_reag_blood_vial?>
<?rescount i_reag_bone?>
<?rescount i_reag_brimstone?>
<?rescount i_reag_daemon_bone?>
<?rescount i_reag_fertile_dirt?>
<?rescount i_reag_dragon_blood?>
<?rescount i_reag_executioners_cap?>
<?rescount i_reag_eye_of_newt?>
<?rescount i_reag_obsidian?>
<?rescount i_reag_pig_iron?>
<?rescount i_reag_pumice?>
<?rescount i_reag_serpent_scale?>
<?rescount i_reag_volcanic_ash?>
<?rescount i_reag_dead_wood?>
<?rescount i_reag_worm_heart?>

[dialog d_regy button]

[function recallhome]
if(<src.restest 1 i_scroll_recall>)
  safe(src.findid(i_scroll_recall).dclick)
  act=<findid(i_rune_base).uid>
  last  
else
  safe(cast 020)
  act=<findid(i_rune_base).uid>
  last
endif

[function greenmessage]
Sysmessagecol(044,"<args>")

[function redmessage]
Sysmessagecol(021,"<args>")

[function bluemessage]
Sysmessagecol(063,"<args>")

[function classmessage]
if (strlen(<tag.classcolor>))
Sysmessagecol(<eval <tag.classcolor>>,"<args>")
else
Sysmessagecol(5,"<args>")
endif

[function disconnect]
sendpacket(082 00)

[function racemessage]
Sysmessagecol(<color>,"<args>")

[function strtoascii]
var(asciitext,"")
arg(u,0)
arg(asciilen,<eval <argv(0)>>)
while (<arg(u)> < <arg(asciilen)>)
 var(asciitext,"<?safe asciitext?> <?hval strgetascii("<argv(1)>",<u>)?>")
 arg(u,<eval <u>+1>)
endwhile
return <asciitext>

[FUNCTION Sysmessagecol] 
//sysmessage(<argv(1)>)
//return 0
//debug
if (isitem)
  return 0
elseif (npc)
  return 0
endif
if (<ARGVCOUNT>!=2)
  src.sysmessage smsgcol:Spatne zadano
  return 0
endif
arg(length,<strlen(<argv(1)>)>+45)
if (arg(length)>83+45)
 arg(length,<eval 83+45>)
endif
var(packet,01c <?hval (<length>&0ff00)/0100?> <?hval <length>&0ff?> 00 00 00 00 00 00 02 <?hval (<argv(0)>&0ff00)/0100?> <?hval <argv(0)>&0ff?> 00 03 053 079 073 074 065 06d 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 <?strtoascii((<length>-45),"<argv(1)>")?> 00)
sendpacket(<packet>)

[function messagecol]
// barva,text
if (src.npc)
return 0
endif
if (ARGVCOUNT!=2)
sysmessage msgcol:Spatne zadano
return 0
endif
arg(length,<strlen(<argv(1)>)>+45)
if (arg(length)>83+45)
 arg(length,<eval 83+45>)
endif
src.sendpacket(01c <split2bytes(<arg(length)>)> <hval (<uid>&0ff000000)/01000000> <hval (<uid>&0ff0000)/010000> <hval (<uid>&0ff00)/0100> <hval <uid>&0ff> 00 00 03 <hval (<argv(0)>&0ff00)/0ff> <hval <argv(0)>&0ff> 00 03 <strtoascii(30,"<name>")> <strtoascii((<length>-45),"<argv(1)>")> 00)

[function paperdoll]
var(packet,"088 <split4bytes(<this>)><strtoascii(60,<args>)> <hval(FLAG_war)>")
//logmsg(<var(packet)>)
src.sendpacket(<var(packet)>)

[function counsectors]
var(count,0)
region.sectors(raisecount)
smsg count:<count>

[function raisecount]
var(count,<count>+1)

[Function getNameHue]
//What color should our name be? 
//default obj = the name to examine 
if (isItem) 
 return 0 
elseif (isnazgul)
 return 03c2
elseif (ismurderer) 
 //I am bad 
 return 026
elseif (isinmyguild)
 //srcchangehighlight(2)
 return 044 //03c
elseif (flag_criminal) //i am a graycolored 
 return 03e9 
elseif (isneutral)
 return 03e9 
elseif (src.MemoryFind(UID))
  if (src.MemoryFind(UID).color&01)
    //Src saw me commit a crime 
    return 03e9 
  endif 
ENDIF 
return 063 //blue

[function f_canseelos]
newitemsafe(i_cansee)
lastnew.link=<args>
safe equip(<lastnew>)
return <var(cslos)>

[itemdef i_cansee]
id=i_memory
layer=77
type=t_src_changer
name=cansee memory

on=@equip
var(cslos,<link.srccanseelos>)
remove
return 1

[function movetype] 
temporary_null
newitem i_sortitem 
equip(act)
act.timer 20
act.tag(function,type)
if (strlen(<args>))
  act.tag(max,<fixnumber(<args>)>)
else
  act.tag(max,0ff)
endif

[function moveitem] 
temporary_null
newitem i_sortitem 
equip(act)
act.timer=20
act.tag(function,item)
if (strlen(<args>))
  act.tag(max,<fixnumber(<args>)>)
else
  act.tag(max,0ff)
endif

[itemdef i_sortitem] 
id=i_memory
name=sortitem 
type=t_eq_script 
layer=30 

on=@timer
remove
return 0

on=@equip 
target Co chces presunout? 

on=@targon_item 
if (<src.targ.baseid> == i_truhla_heslo ) || (<src.targ.type> == mit_main) || (<src.targ.baseid> == i_gembox) // vyjimky
 src.redmessage("Sem nelze predmety presunout.")
 remove
 return 1
elseif (more1 == 0) && (<src.targ.cont> == <src>)
  src.redMessage("Prikaz nelze pouzit na predmety, ktere mas na sobe!")
  return 1
endif
if (more1)
 if (<src.targ.type>!=t_container)
  Target    
  return 1        
 elseif (<src.targ.topobj>!=<src>)&&(src.isgm==0)
  target
  return 1  
 elseif (<src.targ>==<link>)
  target
  return 1
 endif
 var(itemcont,<src.targ>)
 var(itemtype,<tag.itemtype>)
 var(itembaseid,<tag.itembaseid>)
 var(maxtomove,<tag.max>)
 var(moved,0)
 src.accmsg("moves from uid=<link> (<link.name>) topobj=<link.topobj> (<link.topobj.name>) to uid=<hval itemcont> (<finduid(itemcont).name>) topobj=<finduid(itemcont).topobj> (<finduid(itemcont).topobj.name>)")
 var(cantmovenext,0)
 link.contents2(f_sort<tag.function>)
 var(cantmovenext,"")
 remove
 return 1
else
 if(src.targ.baseid==i_ring_power)||(src.targ.baseid==i_leading_ring_power)
  src.sysmessage(Ani to nezkousej buggere)
  remove
  return 1
 endif
 if (<src.targ.topobj>!=<src>)&&(src.isgm==0)
  src.sysmessage(Vec musis mit u sebe!)
  remove
  return 1
 endif
 tag(itemtype,<src.targ.type>) //type of the item
 tag(itembaseid,<src.targ.baseid>)//baseid of the item
 more1=1
 link=<src.targ.cont>//the bag the item is in
 if (<src.targ.type>==t_container)
  cont.sysmessage(Nelze aplikovat na containery)
  return 1
 endif
 target Kam je chces dat?
 return 1
endif
return 1

[function f_sorttype]
if(cantmovenext)
 //src.sysmessage(Vic se tam nevejde!)  -radsi bez hlasky
 return 1
endif 
if (<cont>!=<itemcont>)
 if (moved<=maxtomove)
  if (type==itemtype)
   if (movable==0)&&(src.isgm==0)
    src.sysmessage <name> nelze presunout.
   else    
    if(<uid>==<itemcont.cont>) //chce to soupnout do neceho co uz ma v sobe (batoh do pytliku co je uvnitr napriklad)
     src.accmsg(Hrac <src> (<src.name>) se snazi o podvod s kontejnery pri moveitem)
     return 1
    else 
     if(itemcont.rescount < 255)
      cont=<itemcont>
     else      
      var(cantmovenext,1)
      return 1
     endif
    endif
    var(moved,<moved>+1)
    src.accmsg(moves uid=<uid> (<amount> <name>))
   endif
  endif
 endif
endif

[function f_sortitem]    
if(cantmovenext)
 //src.sysmessage(Vic se tam nevejde!) - radsi bez hlasky
 return 1
endif 
if (<cont>!=<itemcont>)
 if (moved<=maxtomove)
  if (baseid==itembaseid)
   if (movable==0)&&(src.isgm==0)
    src.sysmessage <name> nelze presunout.
   else    
    if(<uid>==<itemcont.cont>) //chce to soupnout do neceho co uz ma v sobe (batoh do pytliku co je uvnitr napriklad)
     src.accmsg(Hrac <src> (<src.name>) se snazi o podvod s kontejnery pri moveitem)
     return 1
    else 
     if(itemcont.rescount < 255)
      cont=<itemcont>
     else      
      var(cantmovenext,1)
      return 1
     endif
    endif
    src.accmsg(moves uid=<uid> (<amount> <name>))
    var(moved,<moved>+1)
   endif
  endif
 endif
endif

[function stackitem]
temporary_null
newitem i_stackitem 
act.equip
act.timer 20

[itemdef i_stackitem]
id=i_memory
name=stackitem 
type=t_eq_script
layer=30 

on=@timer
remove
return 0

on=@equip 
target V jakem baglu chces prerovnat veci?
return 1 

on=@targon_item 
if (<src.targ.type>!=t_container)
 src.sysmessage toto neni kontejner!
 remove
 return 1
elseif (<src.targ.topobj>!=<src>)&&(<src.isgm>==0)
 src.sysmessage kontejner musis mit u sebe!
 remove
 return 1
endif
src.newitem i_bag
link=<src.act>
link.p=<src.p>
var(secondbag,<link>)
src.targ.contents(f_allcontainers) //opraveni bugu
src.targ.contents(f_allitems)
link.contents(cont=<src.targ>)
link.remove
var(secondbag,"")
remove
return 1

[function f_allcontainers]
if (iscontainer)
  cont=<var(secondbag)>
endif

[function f_allitems]
amount=<cont.rescount(<baseid>)>
cont=<var(secondbag)>
src.targ.contents(f_removeid(<baseid>))
src.accmsg("leaves uid=<uid> (<amount> <name>)")

var(craftedbywasted,0)
if (tag(craftedby))
  if (var(craftedbywasted))
    tag(craftedby,<hval(tag(craftedby)|craftexp_gotexp)>)
  endif
endif

[function f_removeid] 
if (baseid==args)
 if (tag(craftedby)&craftexp_gotexp)
  var(craftedbywasted,1)
 endif
 src.accmsg("stacks uid=<uid> (<amount> <name>)")
 remove
endif

[function eqlayers]
safe(findlayer(layer_hand1).<args>)
safe(findlayer(layer_hand2).<args>)
safe(findlayer(layer_shoes).<args>)
safe(findlayer(layer_pants).<args>)
safe(findlayer(layer_shirt).<args>)
safe(findlayer(layer_helm).<args>)
safe(findlayer(layer_gloves).<args>)
safe(findlayer(layer_ring).<args>)
safe(findlayer(layer_light).<args>)
safe(findlayer(layer_collar).<args>)
safe(findlayer(layer_half_apron).<args>)
safe(findlayer(layer_chest).<args>)
safe(findlayer(layer_wrist).<args>)
safe(findlayer(layer_beard).<args>)
safe(findlayer(layer_tunic).<args>)
safe(findlayer(layer_ears).<args>)
safe(findlayer(layer_arms).<args>)
safe(findlayer(layer_cape).<args>)
safe(findlayer(layer_robe).<args>)
safe(findlayer(layer_skirt).<args>)
safe(findlayer(layer_legs).<args>)
safe(findlayer(layer_horse).<args>)

[function eqvendorlayers]
safe(findlayer(layer_hand1).<args>)
safe(findlayer(layer_hand2).<args>)
safe(findlayer(layer_shoes).<args>)
safe(findlayer(layer_pants).<args>)
safe(findlayer(layer_shirt).<args>)
safe(findlayer(layer_helm).<args>)
safe(findlayer(layer_gloves).<args>)
safe(findlayer(layer_ring).<args>)
safe(findlayer(layer_light).<args>)
safe(findlayer(layer_collar).<args>)
safe(findlayer(layer_half_apron).<args>)
safe(findlayer(layer_chest).<args>)
safe(findlayer(layer_wrist).<args>)
safe(findlayer(layer_beard).<args>)
safe(findlayer(layer_tunic).<args>)
safe(findlayer(layer_ears).<args>)
safe(findlayer(layer_arms).<args>)
safe(findlayer(layer_cape).<args>)
safe(findlayer(layer_robe).<args>)
safe(findlayer(layer_skirt).<args>)
safe(findlayer(layer_legs).<args>)


[function layerremove]
if (findlayer.<args>)
findlayer.<args>.remove
endif

[function clone]
if (isitem) || (<uid> == <src>)
  return 0
endif
src.profession=<eval <safe profession>>
src.tag(skillrank,<tag.skillrank>)
src.tag(race,<tag.race>)
src.name=<name>
src.title=<title>
src.body=<body>
src.obody=<obody>
src.color=<color>
src.oskin=<oskin>
src.karma=<karma>
src.fame=<fame>
src.tag(experience,<tag.experience>)
src.kills=<kills>
src.str=<str>
src.dex=<dex>
src.int=<int>
src.hits=<hits>
src.stam=<stam>
src.mana=<mana>
src.tag(level,<tag.level>)
src.update

src.allskills 0
arg(dupesk,0)
while (arg(dupesk)<50)
  src.<findres(skill,arg(dupesk)).name>=<?<findres(skill,arg(dupesk)).name>?>
  arg(dupesk,#+1)
endwhile

src.privshow 0//<qval <eval <safe gm>+<safe privshow>>?1:0>
//src.events(-0)
//src.events=<events>

src.eqlayers(remove)
src.layerremove(layer_hair)

//var(uid,<src>)
eqlayers(dupelayer)

[function dupelayer]
dupe
src.equip(newitem)
newitem.updatex
//src.newitem=<baseid>
//src.act.dispid=<dispid>
//src.act.name=<name>
//src.act.color=<color>
//src.act.more1=<more1>
//src.act.more2=<more2>
//src.act.morex=<morex>
//src.act.morey=<morey>
//src.act.morez=<morez>
//src.act.morem=<morem>
//src.act.attr=<attr>
//src.act.equip
//src.act.updatex

[function goplayer]
if (findid.i_goplayer)
 p=<findid(i_goplayer).morep>
 findid(i_goplayer).remove
 update
else
 invis 1
 newitem i_goplayer
 act.morep=<p>
 act.tag.name=<args>
 act.equip//cont=<uid>
 //act.f_goplayer
endif

[itemdef i_goplayer]
id=i_memory
type=t_eq_script
name=go player

on=@equip
tag.going=1
while (<tag.going>==1)
cont.goname(<tag.name>)
if strmatch(<cont.act.topobj.name>,<tag.name>)
 if (cont.act.topobj.ischar)&&(cont.act.topobj.npc==0)
  tag.going=0
  cont.act=<cont.act.topobj.uid>
  cont.sysmessage="<tag.name> is here in <cont.region.name>."
  remove
 endif
elseif (cont.act.baseid==i_relating_crystal)
 if strmatch(<cont.act.link.topobj.name>,<tag.name>)&&(cont.act.link.topobj.ischar)&&(cont.act.link.topobj.npc==0)
  tag.going=0 
  cont.p <cont.act.link.p>
  cont.sysmessage Linked from:<cont.act.topobj.link.name>
  cont.act=<cont.act.link>
  cont.update
  cont.sysmessage="<tag.name> is here in <cont.region.name>."
  remove
 endif
elseif (cont.act.type==t_eq_memory_obj)
 if strmatch(<cont.act.link.name>,<tag.name>)&&(cont.act.link.ischar)&&(cont.act.link.npc==0)
  tag.going=0 
  cont.p <cont.act.link.p>
  cont.sysmessage Linked from:<cont.act.topobj.name>
  cont.act=<cont.act.link>
  cont.update
  cont.sysmessage="<tag.name> is here in <cont.region.name>."
  remove
 endif
endif
if (cont.act.uid==<hval <safe tag.first>>)
 tag.going=0
 cont.p <morep>
 cont.update
 cont.sysmessage no player named <tag.name>
 remove
endif
if !(strlen(<tag(first)>))
 tag.first=<cont.act.uid>
endif
endwhile
remove
return 1

[itemdef i_port_mordor]
id=i_moongate_red
type=t_script
name=Brana Mordor

on=@step
if (src.ismurderer)
src.go <morep>
endif
return 1

[itemdef i_port_gondor]
id=i_moongate_red
type=t_script
name=Brana Gondor

on=@step
if (src.ismurderer==0)
src.go <morep>
endif
return 1

[ITEMDEF i_gate_undelay]
NAME=Undelay Gate
ID=i_moongate_red
TYPE=t_script

ON=@STEP
src.findid(i_delay_timer).remove
return 1

[ITEMDEF i_gate_delay_specific]
NAME=Specific Delay Gate
ID=i_moongate_red
TYPE=t_script

ON=@Create
COLOR=098b
ATTR=010
MORE1=20//pocet vterin k odpoctu

ON=@STEP
if (SRC.npc==0)
 if (link.isitem)
  src.sysmessage Musis pockat.
 else  
  SRC.NEWITEM=i_delay_timer
  SRC.ACT.MORE1=<MORE1>
  SRC.ACT.link=<uid>
  SRC.ACT.MOREP=<MOREP>
  SRC.ACT.EQUIP
  link=<src.act>
 endif
endif

[ITEMDEF i_delay_timer]
ID=i_memory
Name=Teleport Delay Timer
TYPE=T_EQ_SCRIPT

ON=@CREATE
ATTR=attr_invis|attr_decay

ON=@EQUIP
TIMER=1

on=@unequip
if (link.isitem)
 link.link=-1
endif

ON=@Timer
if (more1)
 cont.sysmessage cas do presunu:<eval <more1>>
 more1=<eval <more1>-1>
 TIMER=1
 return 1
else
 cont.GO <MOREP>
 remove
 return 1
endif

[function typearmor]
if (<ac>!=<args>)
typedef.armor=<eval <typedef.armor>-(<ac>-<args>)>
endif

//[function changeshield]
//if (memoryfindtype(0400))
// if (memoryfindtype(0400).link.align)
//  if (findlayer(2).isshield)
//   act=<findlayer.2.uid>
//  else
//   newitem i_shield_null
//   act.cont=<uid>
//  endif
//  if (memoryfindtype(0400).link.align==1)
//   act.dispid=i_shield_order
//  else
//   act.dispid=i_shield_chaos
//  endif
//  act.updatex
//  return 1
// endif
//endif
//sysmessage Musis byt clenem pvp guildy.
//
//[itemdef i_shield_null]
//id=i_shield_buckler
//armor=0
//weight=0
//
//on=@userdclick
//removefromview
//remove
//return 1
//
//on=@pickup_pack
//removefromview
//remove
//return 1
//
//on=@pickup_ground
//removefromview
//remove
//return 1
//
//on=@equip
//removefromview
//remove
//return 1

[function class]
events=+e_gethit
events=+e_spelleffect
clearclass
events=+e_allplayers
events=+e_character
if (argvcount)
  arg(class,<args>)
elseif (strlen(<tag.class>))
  arg(class,<tag.class>)
else
  redMessage("Chyba class ! Bij na poplach a volej GM ! Jsi rozbity...")
  return 1
endif
safe(events=+e_<arg(class)>)
safe(profession=class_<arg(class)>)
tag(class,<strmid(<profession>,6,20)>)
src.tag(classcolor,color_<tag.class>)
setplayer
if (profession==prof_undeclared)
  sysmessage("Problem s tvoji postavou...kontaktuj GM")
endif
return <profession>

[function clearclass]
events -e_necro 
events -e_mag 
events -e_priest 
events -e_war 
events -e_ranger 
events -e_thief 
events -e_shaman 
events -e_craft
events -e_mystik

[itemdef i_gate_goodness]
NAME=Gate of goodness
ID=i_moongate_blue
TYPE=T_SCRIPT
CATEGORY=Other
SUBSECTION=gates
DESCRIPTION=brana pro vynulovani kills

on=@step
src.kills=0

[function ismagic]
if (<safe tag.magic>)
return 1
elseif (findid(i_rune_curse))
elseif (findid(i_rune_bless))
elseif (findid(i_rune_clumsy))
elseif (findid(i_rune_feeblemind))
elseif (findid(i_rune_weaken))
elseif (findid(i_rune_agility))
elseif (findid(i_rune_cunning))
elseif (findid(i_rune_strength))
elseif (findid(i_honorcounter))
elseif (findid(i_fearcounter))
elseif (findid(i_stealthbonus))
elseif (findid(i_bloodritual))
else
return 0
endif
return 1

[FUNCTION iscontainer]
if !(isitem)
 RETURN 0
elseIF (TYPE==t_container)
 RETURN 1
ELSEif (<safe rescount>)
 RETURN 1
ENDIF
return 0

[FUNCTION countamount]
if (iscontainer)
var(amount_sum,0)
contents(f_contamount)
return <amount_sum>
endif

[FUNCTION f_contamount]
var(amount_sum,<amount_sum>+<amount>)

[function dspnext]
act.try show typedef.resdispid
act.try typedef.resdispid=<act.typedef.resdispid>+1
act.try updatex

[function dspprev]
act.try show typedef.resdispid
act.try typedef.resdispid=<act.typedef.resdispid>-1
act.try updatex

[function dspset]
act.try show typedef.resdispid
act.try typedef.resdispid=<eval args>
act.try updatex

[FUNCTION givegold]
arg(goldyet,<eval args>)
while (arg(goldyet)>0)
  IF (arg(goldyet)<=65535)
    src.NEWITEMsafe(i_gold)
    src.accmsg(gave <arg(goldyet)> gold (uid=<lastnew>) to uid=<uid> (<name>) topobj=<topobj> (<topobj.name>))
    lastnew.AMOUNT=<arg(goldyet)>
    lastnew.CONT(<uid>)
    arg(goldyet,0)
  else
    src.NEWITEMsafe(i_gold)
    src.accmsg(gave 60000 gold (uid=<lastnew>) to uid=<uid> (<name>) topobj=<topobj> (<topobj.name>))
    lastnew.AMOUNT=60000
    lastnew.CONT(<uid>)
    arg(goldyet,#-60000)
  endif
endwhile

[function newmemory]
var(newmemoryact,<act.uid>)
if (memoryfind(<args>))
 memoryfind(<args>).remove
endif
newitem i_memory
equip(act)
act.link=<args>

[function runxtimes]
arg(u,<argv(0)>)
while (u>0)
 <argv(1)>
 arg(u,#-1)
enddo

[function f_fizzleefect]
src.EFFECT=3,i_fx_smoke_small,0,20,10
src.sfx=92
src.sysmessage(The spell fizzles.)

[events e_rychlost]
on=@step
vpredsafe
vpredsafe
vpredsafe
vpredsafe
vpredsafe

[function rychlost]
if <isevent.e_rychlost>
events -e_rychlost
else
events +e_rychlost
endif



[function vpredsafe]
arg(vpredsafact,<act>)
newnpc(c_dummy)
act.p=<p>
act.walk(<dir_inttochar(<dir>)>)
//if !({0 4}) //snad to min laguje a snizil se i pocet CC
p=<act.p>
//updatex //vypada to ze todle nemaj clienti moc radi kdyz zrusim tu cast za else s go a necham jen tudle tak je z leapu v 95% cc a vypada to ze to funguje i bez toho
//else
//go <act.p>
//endif
act.remove
act=<arg(vpredsafact)>

[function vzadsafe]
arg(vpredsafact,<act>)
newnpc(c_dummy)
act.p=<p>
act.walk(<dir_inttochar(<dir_revert(<dir>)>)>)
//if !({0 4}) //snad to min laguje a snizil se i pocet CC
p=<act.p>
//updatex //vypada to ze todle nemaj clienti moc radi kdyz zrusim tu cast za else s go a necham jen tudle tak je z leapu v 95% cc a vypada to ze to funguje i bez toho
//else
//go <act.p>
//endif
act.remove
act=<arg(vpredsafact)>




[chardef c_dummy]
id=c_man
name=dummy

on=@create
flag_insubstantial=1

[events e_nomove]
on=@step
move <dir_inttochar(<dir_revert(<dir>)>)>
updatex



[function dir_revert]
doswitch <args>
return 4
return 5
return 6
return 7
return 0
return 1
return 2
return 3
enddo

[function dir_inttochar]
doswitch <args>
return"n"
return"ne"
return"e"
return"se"
return"s" 
return"sw"
return"w"
return"nw"
enddo

[function isfighting]
if	(action==skill_wrestling)
elseif	(action==skill_swordsmanship)
elseif	(action==skill_fencing)
elseif	(action==skill_macefighting)
elseif	(action==skill_archery)
else
return 0
endif
return 1

[function isfighting_melee]
if 	(action==skill_wrestling)
elseif	(action==skill_swordsmanship)
elseif	(action==skill_fencing)
elseif	(action==skill_macefighting)
 if (findlayer(2).type==t_weapon_bow_run)
  return 0
 endif
else
return 0
endif
return 1

[function isfighting_ranged]
if (action==skill_archery)
 return 1
elseif (action==skill_macefighting)
 if (findlayer(2).type==t_weapon_bow_run)
  return 1
 endif
 return 0
endif
return 0

[function iscrafting]
if 	(action==skill_cooking)
elseif 	(action==skill_blacksmith)
elseif 	(action==0) //skill_alchemy
elseif 	(action==skill_carpentry)
elseif 	(action==skill_Tinkering)
elseif 	(action==skill_bowcraft)
elseif 	(action==skill_Tailoring)
elseif 	(action==skill_Carpentry)
elseif 	(action==Skill_Inscription)
elseif 	(action==Skill_cartography)
else
 return 0
endif
return 1

[function ismacroing]
if 	(action==skill_cooking)
elseif 	(action==skill_blacksmith)
elseif 	(action==0) //skill_alchemy
elseif 	(action==skill_carpentry)
elseif  (findid(i_taming))
 if (findid(i_taming).timer>(-1))
  return 1
 endif
elseif  (findid(i_delayer))
elseif  (findid(i_mem_lockpick))
elseif	(findid(i_vyroba_latky))
elseif 	(action==skill_lumberjack)
elseif 	(action==skill_mining)
elseif 	(action==skill_fishing)
elseif 	(action==skill_Tinkering)
elseif 	(action==skill_bowcraft)
elseif 	(action==skill_Tailoring)
elseif 	(action==skill_Carpentry)
elseif 	(action==Skill_Inscription)
elseif 	(action==Skill_cartography)
else
 return 0
endif
return 1

[function sqrt_int]
if (<eval <args>>==0)
 return 0
endif
arg(root1,<eval <args>*10000>)
arg(root2,<eval <args>*10000>)
arg(sqrt_tries,0)
arg(sqrt,-1)
while (<sqrt_tries><30)
 arg(lastroot,<root1>)
 arg(sqrt_tries,<sqrt_tries>+1)
 arg(root1,<eval (<root1>/2)+((<root2>/<root1>)/2)>)
 
 if (<lastroot>==<root1>)||(<lastroot>==(<root1>-1))
  arg(sqrt,<lastroot>)
  return <sqrt>
 endif
endwhile
return <sqrt>

[function canbeweapon]
if (def.layer==1)||(def.layer==2)
 return 1
endif
return 0

[function isweapon]
if (type==t_weapon_sword)
elseif (type==t_weapon_fence)
elseif (type==t_weapon_mace_smith)
elseif (type==t_weapon_mace_crook)
elseif (type==t_weapon_mace_staff)
elseif (type==t_weapon_bow_run)
elseif (type==t_weapon_bow)
elseif (type==t_weapon_xbow)
else
return 0
endif
return <uid>

[function isarmor]
if (type==t_armor)
elseif (type==t_armor_leather)
else
return 0
endif
return 1

[function hex]
sysmessage="<hval <args>>"
return <hval <args>>

[function dec]
sysmessage <eval <args>>
return <eval <args>>

[itemdef i_test_pay]
id=i_bottle
type=t_normal
name=paytest

on=@userdclick
if (src.pay(80000)) //you have paid
src.message adding backpack
//src.newitem i_backpack //add very expensive backpack
//src.act.bounce
endif
src.updatex
return 1

[function pay]
arg(shouldpay,<eval args>)
if (src.isgm)
  classmessage(jako gm neplatis nic)
  arg(enough,1)
  return 1
endif
arg(enough,0) //if not enough gold, var(enough) will be '0'
arg(totalgold,<eval findlayer(21).rescount(t_gold)+bankbalance>)
if (arg(shouldpay)>arg(totalgold)) //the amount to pay is more then you own??
  classmessage(potrebujes <arg(shouldpay)> gp.) //refuse transaction
  accmsg(Has now #<arg(shouldpay)># gp.)
  return 0
else //you have enough to pay
  //allow you to check transaction later
  if (arg(shouldpay)>src.findlayer(21).rescount(t_gold))
  //say cannot be paid with backpack money alone <eval <args>-<src.findlayer.21.rescount i_gold>>
    findlayer(29).consumegoldamount(<eval arg(shouldpay)-findlayer(21).rescount(t_gold)>) //put the needed money in the backpack
    findlayer(21).consumeallgold //pay the needed amount
  else
    findlayer(21).consumegoldamount(<arg(shouldpay)>)
  endif
  //now exactly the rest of the needed gold is in the pack so we can cosume it all
endif
arg(enough,1)
accmsg(Has now #<eval args># gp.)
return 1

[function consumegoldamount] //move enough gold so that we can pay
//args- amount of gold left to move
arg(yettohandle,<eval args>)
//we know that there is enough of this
while (arg(yettohandle)>0)
  arg(foundpile,<findtype(t_gold)>)
  arg(foundamount,<arg(foundpile).amount>) //amount found
  if (arg(foundamount)<=arg(yettohandle))
    //pile is not big enough, so move it all
    arg(yettohandle,#-<arg(foundpile).amount>)
    arg(foundpile).remove
  else //not entire pile needed
    //ie yettohandle < foundamount
    arg(foundpile).amount=<eval arg(foundamount)-arg(yettohandle)>
    arg(yettohandle,0)
  endif
endwhile

[function consumeallgold]
while (findtype(t_gold))
  findtype(t_gold).remove
endwhile
if (findtype(t_gold))
 //in case sphere made something weird with the loop protection
  consumeallgold
endif

[function logmsg]
serv.smsg("<args>\n")

[function accmsg]
//logmsg("accmsg: uid=<uid> (<name>) src=<src> (<src.name>)")
if (ischar)
 if (isplayer)
  try s(<serverTime> <?args?>)
 endif
elseif (src.isplayer)
 src.try s(<?args?>)
endif

[function s]

[function detachitem]
if (<amount>>1)
var(detamount,<amount>-1)
amount=1
dupe(detamount)
endif

[function contents]
//logmsg("object of contents:<uid>, <name>, id:<Baseid>")
if (<safe rescount>)
 arg(count,<rescount>)
 while (arg(count))
  arg(count,<eval <arg(count)>-1>)
  findcont(<arg(count)>).<args>
 endwhile
endif

[function contents2]
if (<safe rescount>)
 arg(count,<rescount>)
 while (arg(count))
  arg(count,<eval <arg(count)>-1>)
  if (<safe findcont(<arg(count)>).rescount>)
   findcont(<arg(count)>).contents2(<args>)
  endif
  findcont(<arg(count)>).<args>
 endwhile
endif

[function contentsToCont]
// argv(0) == container, argv(1) == the amount of items to move
if (<safe rescount>)
 arg(count,<argv(1)>)
 while (arg(count))
  arg(count,<eval <arg(count)>-1>)
  findcont(<arg(count)>).cont=<argv(0)>
 endwhile
endif

[function ismurderer]
if (isplayer)
 if (kills>serv.murdermincount)||(karma<=-8000) 
  return 1
 else
  return 0
 endif
elseif (brain==BRAIN_UNDEAD)||(brain==BRAIN_MONSTER)||(brain==BRAIN_ANIMAL)
 if (karma<=-800)
  return 1
 else
  return 0
 endif
elseif (brain==BRAIN_BESERK)
 return 1
elseif (karma<=-3000)
  return 1
else
  return 0
endif

[function isneutral]
if (isplayer)
 if (karma<serv.PlayerNeutral) 
  return 1
 else
  return 0
 endif
elseif (brain==BRAIN_BESERK)||(brain==BRAIN_MONSTER)||(brain==BRAIN_UNDEAD)
 if (karma<=0)
  return 1
 else
  return 0
 endif
elseif (brain==BRAIN_ANIMAL)
  if (karma<=100)
    return 1
  else
    return 0
  endif
elseif (karma<0)
  return 1
else
  return 0
endif

[function bounceitem]
newitemsafe(<args>)
lastnew.cont=<findlayer(21)>

[function giveitem]
if (ischar)
 lognewitemsafe(<argv(0)>)
 if (argvcount>1)
  lastnew.amount=<argv(1)>
 endif
 lastnew.logcont(<uid>)
else
 src.lognewitemsafe(<argv(0)>)
 if (argvcount>1)
  lastnew.amount=<argv(1)>
 endif
 lastnew.logcont(<uid>)
endif

[function logcont]//
if(baseid==i_ring_power)||(baseid==i_leading_ring_power)||(baseid==i_generals_bracelet)
 return 1
endif
arg(moveto,<finduid(args)>)
src.accmsg("moves uid=<uid> (<amount> <name>) from uid=<cont> (<cont.name>) topobj=<topobj> (<topobj.name>) TO uid=<arg(moveto)> (<arg(moveto).name>) topobj=<arg(moveto).topobj> (<arg(moveto).topobj.name>)")
cont=<arg(moveto)>

[function logpi]
//arg(moveto,<finduid(args)>)
//src.Accmsg(moved uid=<uid> (<amount> <name>) from uid=<cont> (<cont.name>) topobj=<topobj> (<topobj.name>) TO pos=<args>)
p=<args>
trigger(@dropon_ground)
updatex

[function logpc]
arg(moveto,<finduid(args)>)
src.Accmsg(moved uid=<uid> (<name>) TO pos=<args>)
p=<args>
updatex

[function class_cancast]//class abbrev
arg(spellnumber,<eval (<actarg1>&0ffff)-1>)
if (arg(spellnumber)<32)
 if (isbit(<?spells_<args>_0_0?>,<?arg(spellnumber)?>))
  return 1
 endif
elseif (<arg(spellnumber)><64)
 if (isbit(<?spells_<args>_0_1?>,<?arg(spellnumber)?>))
  return 1
 endif
else
 if (isbit(<?spells_<args>_0_2?>,<?arg(spellnumber)?>))
  return 1
 endif
endif
return 0

[function hasspell]
arg(spellnumber,<eval (<args>&0ffff)-1>)
if (<arg(spellnumber)><32)
 if (isbit(<more1>,<arg(spellnumber)>))
  return 1
 endif
elseif (<arg(spellnumber)><64)
 if (isbit(<more2>,<arg(spellnumber)>))
  return 1
 endif
else
 if (isbit(<morex>,<arg(spellnumber)>))
  return 1
 endif
endif
return 0

[function fullbook]
if !(src.findtype(t_spellbook))
src.sysmessage Musis mit u sebe knihu.
return 0
endif
newitem i_fullbookmem
equip(act)

[itemdef i_fullbookmem]
id=i_memory
type=t_eq_script
name=fullbook memory

on=@equip
timer 20
target Ze ktereho kontejneru chces naplnit knihu?
return 1

on=@timer
remove
return 1

on=@targon_item
if !(src.findtype(t_spellbook))
src.sysmessage Musis mit u sebe knihu.
return 1
elseif !(src.targ.iscontainer)
src.sysmessage Toto neni kontejner!
return 1
elseif (<src.targ.topobj>!=<src>)
src.sysmessage Kontejner musis mit u sebe.
return 1
elseif !(src.targ.findtype(t_scroll))
src.sysmessage V kontejneru nejsou zadne svitky.
return 1
endif
src.act=<uid>
link=<src.findtype(t_spellbook)>
src.targ.contents(f_fullbook)
remove
return 1

[function f_fullbook]
if (type==t_scroll)
 if !(src.act.link.hasspell(<morex>))
  src.act.link.addspell(<morex>)
  consume 1
 endif
endif

[function emptybook]
//say <findres(spell,12).scrollitem>
newitem i_emptybookmem
act.equip

[itemdef i_emptybookmem]
id=i_memory
type=t_eq_script
name=emptybook memory

on=@equip
timer 60
target z ktere knihy chces vytahnout svitky?
return 1

on=@targon_item
if (src.targ.type!=t_spellbook)
 src.sysmessage toto neni kouzelna kniha.
elseif (src.targ.topobj.uid!=src.uid)&&(src.isgm==0)
 src.sysmessage knihu musis mit u sebe.
else
 arg(bookloop,1)
 arg(tocont,<src.reccont>)
 while (bookloop<71)
  if (src.targ.hasspell(<bookloop>))
   src.newitemsafe(<findres(spell,<bookloop>).scrollitem>)//i_scroll_clumsy    
   lastnew.logcont(<arg(tocont)>)
  endif
  arg(bookloop,<bookloop>+1)
 endwhile
 src.targ.more1=0
 src.targ.more2=0
 src.targ.morex=0
endif
remove
return 1

on=@timer
remove
return 0

[function gotofirstchar]
var(gotofirst,0)
nukecharall f_ftfirst

[function f_ftfirst]
if (<gotofirst>==0)
 src.go <p>
endif
if (<npc>)
var(gotofirst,<gotofirst>+1)
endif

[function gohome]
go <home>

[function npcspawn]
if (npc)
 if (memoryfindtype(memory_ispawned))
  try <args>
 endif
endif

[function npcfree]
if (npc)
 if (memoryfindtype(memory_ispawned)==0)&&(memoryfindtype(memory_ipet)==0)
  try <args>
 endif
endif

[function nukecharall]
xxc <args>
arg(storepos,<p>)
p=1 1 0 <mapplane>
self
go(6143,4095,0,<mapplane>)
self
go <arg(storepos)>

[function nukeall]
xxi <args>
arg(storepos,<p>)
p=1 1 0 <mapplane>
self
go(6143,4095,0,<mapplane>)
self
go <arg(storepos)>

[function spawnfull]
if (type==t_spawn_char)
 src.newitem i_spawnfull_timer
 src.act.link=<uid>
 src.act.timer=1
 src.act.p=<p>
 src.act.more1=0
endif

[itemdef i_spawnfull_timer]
id=i_gold
type=t_script
name=fullspawner (.x spawnfull)

on=@create
attr=attr_invis

on=@timer
if (<link.amount>><link.more2>)&&(link.type==t_spawn_char)//&&(<eval <more1>><100)
more1=<eval <more1>+1>
link.timer=0
timer 1
else
 remove
endif
return 1

[function isspid]
if (<more>==<spawnedid>)
 try <args>
endif

[function getspid]
var(spawnedid,<more1>)
src.sysmessage Typ spawnu je <spawnedid>, <hval <spawnedid>>

[function spawncount]
var(charspawn,0)
var(charspawnbad,0)
var(spcntmode,<eval args>)
nukeall(f_spwncnt)
src.sysmessage spatnych npc spawnu:<eval charspawnbad>
src.sysmessage npc spawnu celkem:<eval charspawn>

[function f_spwncnt]
if (type==t_spawn_char)
 if (<eval more1>==0)||(timer<0)
  var(charspawnbad,#+1)
  remove
 else
    var(charspawn,#+1)
 endif
 if (var(spcntmode))
  srccome
  dclick
  spawnfull
 endif
endif

[function skillgain]
arg(skillname,<findres(skill,<args>).key>)
arg(skillvalue,<?<arg(skillname)>?>)
if (npc)||(<region.flags>&region_flag_safe)
 logmsg(skillgain none)
else
 if (arg(skillvalue)<profession.<arg(skillname)>)
  arg(advrate,<eval (advrate_<arg(skillname)>)>)
  arg(rand,<eval {0 <eval ((<arg(skillvalue)>*<arg(skillvalue)>)/(1000000/<arg(advrate)>))> }>)//mnozstvi pokusu na skillgain
  if !(arg(rand))//||(<isgm>)
   <arg(skillname)>=<eval <arg(skillvalue)>+1>
  endif
 endif 
endif

[itemdef i_npc_mount]
id=i_memory
type=t_eq_script
name=mountmemory

on=@create
timer=0

on=@timer
if (tag(mount))
 cont.newitemsafe(<tag(mount)>)
else
 cont.newitemsafe(i_mt_horse_tan)
endif
//lastnew.more2=<cont>
//lastnew.link=<cont>
if(tag(mountcolor))
 lastnew.color=<eval <tag(mountcolor)>> 
else
 lastnew.color=<color>
endif
lastnew.cont=<cont>
remove
return 0

[function specability]
if (findid(i_spec_ability_counter))
 if ({0 100000}<eval(tag(ability)))
  return 1
 endif
endif
return 0

[typedef t_packbankcheck]

[FUNCTION packorbank]
arg(TEMPTYPE,<TYPE>)
TYPE=t_packbankcheck
IF (TOPOBJ == UID)
  arg(RETURN,0)
ELSEIF (TOPOBJ.FINDLAYER(21).FINDTYPE(t_packbankcheck))
  arg(RETURN,1)
ELSEIF (TOPOBJ.FINDLAYER(29).FINDTYPE(t_packbankcheck))
  arg(RETURN,2)
ENDIF
TYPE=<arg(TEMPTYPE)>
RETURN <arg(RETURN)>

[function reccont]
if (finduid(tag(reccont)))
  if (tag(reccont).iscontainer)
    if (tag(reccont).topobj==uid)
      if (tag(reccont).packorbank==1)||(tag(reccont)==findlayer(21))
        return <tag(reccont)>
      endif
    endif
  endif
endif
return <findlayer(21)>

[function setreceivingcontainer]
newitemsafe(i_reccont)
lastnew.equip

[itemdef i_reccont]
id=i_memory
type=t_eq_script
name=reccont memory

on=@equip
timer 20
target zamer kontejner
return 1

on=@targon_item
if !(src.targ.iscontainer)
 src.redmessage(Toto neni kontejner!)
elseif (src.targ.id == i_truhla_heslo)
 src.redmessage(Do teto truhly nelze umistovat vyrobky.)
else
 src.tag(reccont,<src.targ>)
endif
remove
return 1

[function namebag]
newitem i_namecontainer
act.equip
act.tag.name=<args>

[itemdef i_namecontainer]
id=i_memory
type=t_eq_script
name=contname memory

on=@equip
timer 20
target zamer kontejner ktery ma byt pojmenovan.
return 1

on=@targon_item
if (<src.targ.type> != t_container)
 src.sysmessage toto neni kontejner!
elseif (<src.targ.topobj.uid> != <src.uid>) 
 src.sysmessage kontejner musis mit u sebe.
else
 src.targ.name=<tag.name>
endif
remove
return 1

on=@timer
remove
return 0

[function f_vyroba_latky]
if (<src.rescount(i_vyroba_latky)>)
 src.RedMessage("Uz vyrabis!")
 src.AccMsg("Postava <src.name> cislo <src> vyrabi latku nebo nite prilis rychle.")
 return 0
endif
src.newequip(i_vyroba_latky)
arg(memtimer,<lastnew>)
memtimer.timer=3
memtimer.more2=<argv(1)>
arg(jmenovyrobku,<argv(1).name>)
if !(<eval <src.tailoring>/<mnozkoef>>)	// kolik zdroju muzu zpracovat
 arg(muzezpr,6)
else
 arg(muzezpr,<eval <src.tailoring>/<mnozkoef>>*6)
endif
arg(mnozstvi_zdroju,<src.rescount(<argv(0)>)>)
if (<mnozstvi_zdroju> >= <muzezpr>)
 memtimer.more1=<pocet_z_<argv(2)>>*<muzezpr>
else
 memtimer.more1=<pocet_z_<argv(2)>>*<mnozstvi_zdroju>
endif
src.consume(<muzezpr> <argv(0)>)
if (argvcount > 3)
  argv(3).f_spinningwheel_rotate
  memtimer.tag(spinrotate,<argv(3)>)
endif

[itemdef i_vyroba_latky]
id=i_memory
name=vyroba vlny
type=t_eq_script

on=@Timer
if strlen(<tag.spinrotate>)
 tag(spinrotate).f_spinningwheel_norotate
endif
src.newitem(<more2>,<more1>)
lastnew.bounce
remove

[function f_spinningwheel_rotate]
if (dispid == i_spinning_wheel)
 dispid=01016
elseif (dispid == 01019)
 dispid=0101a
elseif (dispid == 0101c)
 dispid=0101d
elseif (dispid == 010a4)
 dispid=010a5
else
 return 0
endif
update
return 1

[function f_spinningwheel_norotate]
if (dispid == 01016)
 dispid=i_spinning_wheel
elseif (dispid==0101a)
 dispid=01019
elseif (dispid == 0101d)
 dispid=0101c
elseif (dispid == 010a5)
 dispid=010a4
else
 return 0
endif
update
return 1

[typedef t_telepad]//by mandos aby ten kram fungoval i na duchy a i v pripade invis gate
on=@step
if (src.isplayer)
//	SRC.EFFECT=3,i_fx_smoke,6,15,1//smazano by enrico. Jestli chcete efekt, dejte si to konkretniho itemu
  SRC.GO <MOREP>
endif

[typedef t_realmpad]//portne jen danej realm (dle more1 bud 1 nebo 2) (ZAKAZ VSTUPU)
on=@step
if(src.isplayer)
 if (<src.tag(realm)>==<more1>)
  SRC.GO <morex> <morey> <morez> <morem>
  src.sysmessage(Nejsi opravnen vstoupit) 
 endif
endif

[events e_nofield]
on=@spellcast
if (argn==24)||(argn==28)||(argn==39)||(argn==47)||(argn==50)
 sysmessage(Toto zde nejde kouzlit)
 return 1
endif

[events e_noport]
on=@spellcast
if (argn==32)
 sysmessage(Nelze cestovat kouzelm)
 return 1
endif

on=@itemequip
if (act==i_travelstone_delay)
 sysmessage(Dostavnik neprijima)
 return 1
endif

on=@itemstep
if (act.type==t_telepad2)
 sysmessage(brana te neprijima)
 return 1
endif

[function nolightfizz] argv0  -0 bez napisu, 1 s napisem
//je mi lito ale musi to bejt takhle vetveny protoze kdyz to bylo v jednom radku 
//tak napr absence cehokoliv v ruce pusobila chybove vypisy a tech se radeji vyvarujeme, ze 
if (flag_nightsight==1)||(<sector.light> < <eval {0 64}>)||(isneartype(t_light_lit,2))||(safe findid(i_hypno_delka))
 return 0
else 
 if (findlayer(2))
  if(findlayer(2).type==t_light_lit)      
   return 0
  else
   if(<argv(0)>)
    sysmessage(Je spatne videt)
   endif
   return 1
  endif
 else
  if(<argv(0)>)
   sysmessage(Je spatne videt)
  endif
  return 1
 endif
endif

[function hungryhurts]
if (food <1)
 damage(10)
 sysmessage(umiras hlady)
endif

[function fizzmagictravel]
if (actarg1==32)||(actarg1==52) //zrovna kouzlim recall, brana
 stopmagery
endif

[function showfood]
arg(ham1,<strgettok("<return_nth(1,<typedef.foodtype>)>",0," ")>,<strgettok("<return_nth(1,<typedef.foodtype>)>",1," ")>)
arg(ham2,<strgettok("<return_nth(2,<typedef.foodtype>)>",0," ")>,<strgettok("<return_nth(2,<typedef.foodtype>)>",1," ")>)
arg(ham3,<strgettok("<return_nth(3,<typedef.foodtype>)>",0," ")>,<strgettok("<return_nth(3,<typedef.foodtype>)>",1," ")>)
arg(ham4,<strgettok("<return_nth(4,<typedef.foodtype>)>",0," ")>,<strgettok("<return_nth(4,<typedef.foodtype>)>",1," ")>)
return <arg(ham1)>,<arg(ham2)>,<arg(ham3)>,<arg(ham4)>


[function whoami]
sysmessage("Jsem <name> <tag.class> <tag.nation> <tag.title>.")
sysmessage("Premohl jsem <tag.playerkillstotal> nepratel a <Tag.monsterkillstotal> monster.")
sysmessage("Ja sam jsem byl porazen <deaths> krat.")
sysmessage("Zde ziji jiz <eval <age>/86400 > dni")

[function addflesh]
if (src.targ.tag(rezal))
else
 src.targ.tag(rezal,1)
 if (src.targ.link.obody==c_man)
  src.newitemsafe(i_flesh_head)
 else
  src.newitemsafe(i_flesh_head_2)
 endif
 lastnew.p=<src.targ.p>	
 lastnew.link=<src.targ.link>
 lastnew.name=<lastnew.link.name> 
 lastnew.timer=300
 lastnew.attr_decay=1
 src.newitemsafe(i_flesh_torso)
 lastnew.p=<src.targ.p>	
 lastnew.link=<src.targ.link>
 lastnew.timer=300
 lastnew.attr_decay=1
 src.newitemsafe(i_flesh_left_arm)
 lastnew.p=<src.targ.p>	
 lastnew.link=<src.targ.link>
 lastnew.timer=300
 lastnew.attr_decay=1
 src.newitemsafe(i_flesh_right_arm)
 lastnew.p=<src.targ.p>	
 lastnew.link=<src.targ.link>
 lastnew.timer=300
 lastnew.attr_decay=1
 src.newitemsafe(i_flesh_left_leg)
 lastnew.p=<src.targ.p>	
 lastnew.link=<src.targ.link>
 lastnew.timer=300
 lastnew.attr_decay=1
 src.newitemsafe(i_flesh_right_leg)
 lastnew.p=<src.targ.p>	
 lastnew.link=<src.targ.link>
 lastnew.timer=300
 lastnew.attr_decay=1
 arg(deftocreate, <hval({0122a 1 0122b 1 0122c 1 0122d 1 0122e 1})>)
 src.newitemsafe(<findres(itemdef,<arg(deftocreate)>)>)
 lastnew.p=<src.targ.p>	
 lastnew.name=krev
 lastnew.timer=300
endif

[function kermel]
return<finduid(#094dfd)>

[function eufegenie]
return<finduid(#017857e)>

[itemdef i_checkownerforpet]
id=i_memory
name=test majitele
type=t_eq_script

on@=create
attr=attr_invis|attr_decay
timer=10

on=@equip
tag(owner,<cont.master>)

on=@timer
timer=10
if (tag(owner)==<cont.master>)
else
 cont.hits=0
endif

[function osedlej]
reveal
stopscriptedskills
skill -1
if (<flag_dead>)
 return 1
endif
newitemsafe(i_mount_target_timer)
equip(lastnew)

[itemdef i_mount_target_timer]
ID=i_memory
TYPE=t_eq_script
name=mounter

on=@equip
dclick
return 1

on=@userdclick
timer=60
target Na ktere zvire chces nasednout ?
return 1

on=@targon_item
target
return 1

on=@targon_char
if !(src.targ.ismypet)
 src.sysmessage "Toto neni tve zvire."
 remove
 return 1
elseif !(src.targ.srccombatLOS)||(src.targ.distance>6)
 SRC.SYSMESSAGE "Nevidis cil."
 remove
 return 1
else
 src.dismount
 if (src.targ.body==c_llama_pack)
  src.newitem(i_mt_llama_pack)
 elseif (src.targ.body==c_horse_pack)
  src.newitem(i_mt_horse_pack)
 else
  src.targ.dclick
  remove
  return 1
 endif
 arg(mymemjezdik,<lastnew>)
 mymemjezdik.tag(mount_pack_id,<src.targ.id>)
 mymemjezdik.more2=<src.targ>
 mymemjezdik.color=<src.targ.color>
 mymemjezdik.tag(mycolor,<src.targ.color>)//ne, neni to zbytecne ackoli to tak vypada
 mymemjezdik.tag(myhits,<src.targ.hits>)
 mymemjezdik.name=<src.targ.name>
 mymemjezdik.tag(bagl,<src.targ.findlayer(21)>)
 mymemjezdik.tag(myfood,<src.targ.food>)
 finduid(mymemjezdik.tag(bagl)).cont=0400b065b
 src.equip(mymemjezdik)
endif
//cont.newitemsafe(i_hladovitko)
//lastnew.tag(myfood,<src.targ.food>)     
//lastnew.tag(myuid,<mymemjezdik>)
//cont.equip(<lastnew>)
//tag(fooduid,<lastnew>)
remove
return 1

on=@timer
remove
return 1

[itemdef i_hladovitko]
id=i_memory
type=t_eq_script
name=hlad jezditka

on=@create
attr=attr_invis|attr_decay
timer=3600

on=@timer
if (tag(myfood)==0)
 cont.sysmessage("Zvire je hladove")
 cont.dismount
 remove
 return 1
endif
tag(myfood,#-1)
finduid(<tag(myuid)>).tag(myfood,<tag(myfood)>)
timer=3600
return 1



[function mount_equip]
if(cont.tag(hypno_mystik)) //prehazovani zviratek mezi batuzky - nic nedelat (hlavne nemazat protoze tam je ten hrac)
else
 if (cont.isplayer)
  cont.smazsracky
  tag(mycolor,<color>)
  cont.newitemsafe(i_hladovitko)
  arg(mylastnew,<lastnew>)
  mylastnew.tag(myfood,<finduid(<more2>).food>)     
  mylastnew.tag(mymaxfood,<finduid(<more2>).typedef.maxfood>)     
  mylastnew.tag(myname,<finduid(<more2>).name>)     
  mylastnew.tag(myuid,<uid>)
  cont.equip(<mylastnew>)
  tag(myfood,<finduid(<more2>).food>)
  tag(fooduid,<mylastnew>)
  tag(myhits,<finduid(<more2>).hits>)
  tag(myid,<finduid(<more2>).id>)
  if (finduid(<more2>).tag(myid))
   tag(myid,<finduid(<more2>).tag(myid)>)
  endif
  if (finduid(<more2>).issumon)
   cont.newitemsafe(i_summonmountremover)
   lastnew.timer=600
   cont.equip(<lastnew>)
   arg(mymnt,<finduid(<more2>).tag(runecaster)>)
   if (arg(mymnt))
    finduid(<arg(mymnt)>).link=<uid>
   else
    remove
   endif
  endif  
  finduid(<more2>).actarg1=""
  finduid(<more2>).remove 
 endif
 more2=<cont>
endif

[function mount_unequip]
arg(mylastnewchar,<lastnewchar>)
if !(<arg(mylastnewchar)>)
  return 1
endif
if (cont.tag(hypno_mystik)) //prehazovani zviratek mezi batuzky - nic nedelat 
else
 if !(strcmpi("<arg(mylastnewchar).id>","mage_horse_brown"))||!(strcmpi("<mylastnewchar.id>","necro_horse_undead"))
  mylastnewchar.remove
  return 1
 endif
 if (tag(myid))
  if (mylastnewchar.id==<tag(myid)>)
  else
   cont.newnpc(<tag(myid)>)
  endif
  mylastnewchar.tag(myid,<tag(myid)>)
 endif
 cont.tag(lastjezdik,<mylastnewchar>)
 if (tag(mycolor))
 else
   tag (mycolor,0)
 endif
 mylastnewchar.color=<tag(mycolor)>
 mylastnewchar.oskin=<tag(mycolor)>
 cont.consume(i_hladovitko)
 if (cont.findid(i_summonmountremover))
  mylastnewchar.remove
  cont.consume(i_summonmountremover)
  return 1
 endif

 if (tag(myhits))//casem zrusit (videno Yavannou 15.8.2006)
  mylastnewchar.hits=<tag(myhits)>
 endif

 mylastnewchar.food=<tag(myfood)>
 if (mylastnewchar.food<1)
  mylastnewchar.flag_pet=0
  mylastnewchar.emote("umira hlady")
 elseif (mylastnewchar.food > <mylastnewchar.typedef.maxfood> *2)
  mylastnewchar.food=<mylastnewchar.typedef.maxfood>
 endif
 finduid(tag(fooduid)).remove
 if (cont.hits==0)
  mylastnewchar.tag(jezdec_chcip,<serv.time>)
  cont.newitemsafe(i_vrazditko)
  arg(vrazditko,<lastnew>)
  vrazditko.p=<mylastnewchar.p>
  vrazditko.z=-100
  vrazditko.link=<mylastnewchar>
  vrazditko.timer=1
  vrazditko.remove
 endif 

endif

[itemdef i_vrazditko]
id=i_memory
layer=77
type=t_src_changer
name=vrazditko srcChanger

on=@timer
//var(attack_src,<link>)
sector.allchars(sejmimne(<link>))
remove 
return 1

[function smazsracky]
consume(i_hladovitko)
consume(i_summonmountremover)

[function mount_unequip_pack]
arg(mylastnewchar,<lastnewchar>)
if (cont.tag(hypno_mystik))
else
 mylastnewchar.remove
 cont.smazsracky
 cont.newnpc(<tag(mount_pack_id)>)
 arg(mylastnewchar,<lastnewchar>)
 cont.tag(lastjezdik,<mylastnewchar>) 
 if (tag(mycolor))
 else
   tag(mycolor,0)
 endif
 mylastnewchar.color=<tag(mycolor)>
 mylastnewchar.oskin=<tag(mycolor)>
 cont.consume(i_hladovitko)
 mylastnewchar.food=<tag(myfood)>
 if (mylastnewchar.food<1)
  mylastnewchar.flag_pet=0
  mylastnewchar.emote("umira hlady")
 elseif (mylastnewchar.food > <mylastnewchar.typedef.maxfood>*2)
  mylastnewchar.food=<mylastnewchar.typedef.maxfood>
 endif
 mylastnewchar.name=<name>
 finduid(tag(fooduid)).remove
 if (tag(myhits))//casem zrusit
  mylastnewchar.hits=<tag(myhits)>
 endif

 finduid(tag(bagl)).cont=<mylastnewchar>

 mylastnewchar.newmemory(<cont>)
 mylastnewchar.act.COLOR=<mylastnewchar.act.COLOR>|memory_ipet
 mylastnewchar.act.more1=04
 mylastnewchar.act.MORE2=<SERV.TIME>
 mylastnewchar.act.MOREP=<mylastnewchar.P>
 mylastnewchar.act=<var(newmemoryact)>

 if (cont.hits<=0)
  mylastnewchar.tag(jezdec_chcip,<serv.time>)  
  cont.newitem(i_vrazditko)
  arg(vrazditko,<lastnew>)
  vrazditko.p=<mylastnewchar.p>
  vrazditko.z=-100
  vrazditko.link=<mylastnewchar>
  vrazditko.timer=1
  //vrazditko.remove
 endif 


endif


[function udelej_pack]
if !(src.targ.ismypet)
 src.sysmessage "Toto neni tve zvire."
 return 1
elseif !(src.targ.srccombatLOS)||(src.targ.distance>6)
 SRC.SYSMESSAGE "Nevidis cil."
 return 1
endif
if (src.targ.id==c_horse_tan)
 src.targ.newnpc(c_horse_pack)
elseif (src.targ.id==c_horse_gray)
 return 1
elseif (src.targ.body==c_horse_tan)||(src.targ.body==c_llama)||(src.targ.body==c_horse_gray)
 src.targ.newnpc(<src.targ.id>_pack)
else
 src.sysmessage "Toto zvire neni ochotno nest batozinu."
 return 1
endif
arg(mylastnewchar,<lastnewchar>)
mylastnewchar.name=<src.targ.name>
mylastnewchar.hits=<src.targ.hits>
mylastnewchar.food=<src.targ.food>		
mylastnewchar.tag(myid,<src.targ.id>)
mylastnewchar.newmemory(<src>)
mylastnewchar.act.COLOR=<mylastnewchar.act.COLOR>|memory_ipet
mylastnewchar.act.more1=04
mylastnewchar.act.MORE2=<SERV.TIME>
mylastnewchar.act.MOREP=<mylastnewchar.P>
mylastnewchar.act=<var(newmemoryact)>
src.targ.remove
remove

[function odsedlej]
reveal
stopscriptedskills
skill -1
if (<flag_dead>) && (isplayer)
 return 1
endif
newitemsafe(i_umount_target_timer)
equip(lastnew)

[itemdef i_umount_target_timer]
ID=i_memory
TYPE=t_eq_script
name=umounter

on=@equip
dclick
return 1

on=@userdclick
timer=60
target Zamer zvire
return 1

on=@targon_item
target
return 1

on=@targon_char
if !(src.targ.ismypet)
 src.sysmessage "Toto neni tve zvire."
elseif !(src.targ.srccombatLOS)||(src.targ.distance>6)
 SRC.SYSMESSAGE "Nevidis cil."
else
 if (src.targ.id==c_llama_pack)
  src.targ.tag(myid,c_llama)
 elseif (src.targ.id==c_horse_pack)
  src.targ.tag(myid,c_horse_tan)
 endif
 if (src.targ.tag(myid))
  src.targ.newnpc(<src.targ.tag(myid)>)
 else
  src.sysmessage "Toto zvire nema batozinu."
  remove
  return 1
 endif
 arg(mylastnewchar,<lastnewchar>)
 mylastnewchar.name=<src.targ.name>
 mylastnewchar.hits=<src.targ.hits>
 mylastnewchar.food=<src.targ.food>		

 if (src.targ.findlayer(21))
  src.targ.findlayer(21).attr_newbie=0
  src.targ.findlayer(21).attr_newbie=0
  src.targ.findlayer(21).attr_movenever=0
  src.targ.findlayer(21).cont=<src>
 endif

 mylastnewchar.newmemory(<src>)
 mylastnewchar.act.COLOR=<mylastnewchar.act.COLOR>|memory_ipet
 mylastnewchar.act.more1=04
 mylastnewchar.act.MORE2=<SERV.TIME>
 mylastnewchar.act.MOREP=<mylastnewchar.P>
 mylastnewchar.act=<var(newmemoryact)>
 src.targ.remove
 
endif
remove
return 1

on=@timer
remove
return 1



[function magic_horse]
if (<tag(hop)>)
elseif (<strlen(<src.tag(magic_horse)>)>)
 tag(hop,1)
 <src.tag(magic_horse)>_mif
 mif_finishitem
endif

[function wimmimate_mif]
tag(imi_value[101], 2)
tag(imi_value[102], 2)
tag(imi_value[103], 2)

[function red_roan_mif]
tag(imi_value[101], 3)
tag(imi_value[102], 3)
tag(imi_value[103], 3)

[function chocolate_mif]
tag(imi_value[101], 5)
tag(imi_value[102], 5)
tag(imi_value[103], 5)

[function pamamino_mif]
tag(imi_value[101], 4)
tag(imi_value[102], 4)
tag(imi_value[103], 4)

[function zostrich_mif]
tag(imi_value[101], 8)
tag(imi_value[102], 8)
tag(imi_value[103], 8)
tag(imi_value[134], 3)

[function sky_mif]
tag(imi_value[101], 6)
tag(imi_value[102], 7)
tag(imi_value[103], 6)

[function grey_mif]
tag(imi_value[101], 5)
tag(imi_value[102], 6)
tag(imi_value[103], 5)


[function sky_grey_mif]
tag(imi_value[101], 7)
tag(imi_value[102], 8)
tag(imi_value[103], 7)

[function crimson_mif]
tag(imi_value[101], 9)
tag(imi_value[102], 12)
tag(imi_value[103], 9)

[function black_mif]
tag(imi_value[101], 8)
tag(imi_value[102], 8)
tag(imi_value[103], 8)
tag(imi_value[135], 10)
tag(imi_value[21], 5)
tag(imi_value[47], 5)

[function polar_bear_mif]
                    
[function zostrich_green_mif]
tag(imi_value[101], 10)
tag(imi_value[102], 10)
tag(imi_value[103], 10)
tag(imi_value[134], 5)
tag(imi_value[135], 5)
tag(imi_value[136], 5)

[function zostrich_red_mif]
tag(imi_value[101], 10)
tag(imi_value[102], 10)
tag(imi_value[103], 10)
tag(imi_value[134], 5)
tag(imi_value[135], 5)
tag(imi_value[136], 5)

[function zostrich_send_mif]
tag(imi_value[101], 10)
tag(imi_value[102], 10)
tag(imi_value[103], 10)
tag(imi_value[134], 5)
tag(imi_value[135], 5)
tag(imi_value[136], 5)

[function zostrich_brown_mif]
tag(imi_value[101], 10)
tag(imi_value[102], 10)
tag(imi_value[103], 10)
tag(imi_value[134], 5)
tag(imi_value[135], 5)
tag(imi_value[136], 5)

[function ridgeback_mif]
tag(imi_value[101], 8)
tag(imi_value[102], 8)
tag(imi_value[103], 8)
tag(imi_value[134], 3)
tag(imi_value[135], 3)

[function ridgeback_savage_mif]
tag(imi_value[101], 8)
tag(imi_value[102], 8)
tag(imi_value[103], 8)
tag(imi_value[134], 4)
tag(imi_value[135], 4)

[function sejmimne]
if (<uid>==<argv(0)>) || (isplayer)
else
  if (distancefrom(argv(0))<=8)
   attack(<argv(0)>)
  endif
endif





[function jezdik]
if (findlayer.layer_horse)
 dclick
elseif (finduid(<tag(lastjezdik)>).distance > 2)
 sysmessage("zvire je prilis daleko")
elseif (finduid(<tag(lastjezdik)>).ismypet)
 finduid(<tag(lastjezdik)>).f_osedlej_2
endif

[function f_osedlej_2]
if (finduid(<src.tag(lastjezdik)>).body==c_llama_pack)
  src.newitem(i_mt_llama_pack)
 elseif (finduid(<src.tag(lastjezdik)>).body==c_horse_pack)
  src.newitem(i_mt_horse_pack)
 else
  finduid(<src.tag(lastjezdik)>).dclick
  return 1
 endif
 lastnew.tag(mount_pack_id,<finduid(<src.tag(lastjezdik)>).id>)
 lastnew.more2=<finduid(<src.tag(lastjezdik)>)>
 lastnew.color=<finduid(<src.tag(lastjezdik)>).color>
 lastnew.tag(mycolor,<finduid(<src.tag(lastjezdik)>).color>)//ne, neni to zbytecne ackoli to tak vypada
 lastnew.tag(myfood,<finduid(<src.tag(lastjezdik)>).food>*86240)		//humus
 lastnew.timer=<lastnew.tag(myfood)>						//humus
 lastnew.tag(mycurentfood,<finduid(<src.tag(lastjezdik)>).food>)		//humus
 lastnew.tag(myhits,<finduid(<src.tag(lastjezdik)>).hits>)
 lastnew.name=<finduid(<src.tag(lastjezdik)>).name>
 lastnew.tag(bagl,<finduid(<src.tag(lastjezdik)>).findlayer(21)>)
 finduid(lastnew.tag(bagl)).cont=0400b065b
 src.equip(lastnew)
endif

[function logoutdismount]
if (findlayer(25))
 dismount
 arg(mylastnewchar,<lastnewchar>)
 tag(last_logout_horse,<lastnewchar>)
 mylastnewchar.flag_stone=1
 arg(myx,<eval {4740 4770}>)
 arg(myy,<eval {101 126}>)
 mylastnewchar.go(<arg(myx)>,<arg(myy)>,0,10)
 mylastnewchar.tag(logoutfood,<mylastnewchar.food>)		
 mylastnewchar.food=1000					
// tag(last_logout_horse,<findlayer(25)>) 
// findlayer(25).attr_invis=1
// findlayer(25).attr_movenever=1
//
// findlayer(25).cont=<findlayer(21)>
// mylastnewchar.remove
elseif (tag(last_logout_horse))
 if (finduid(<tag(last_logout_horse)>))
  if !(finduid(<tag(last_logout_horse)>).flag_stone)
   tag.remove.last_logout_horse
  endif
 endif
endif

[function loginmount]
if (findid(i_hladovitko))
 consume(100 i_hladovitko)
endif
if (tag(last_logout_horse))
 newitem(i_mounter)
 act.equip
endif

[function f_loginmount]
 finduid(<tag(last_logout_horse)>).flag_stone=0
 if (finduid(<tag(last_logout_horse)>).tag(logoutfood))			
  finduid(<tag(last_logout_horse)>).food=<finduid(<tag(last_logout_horse)>).tag(logoutfood)>    
 endif
 finduid(<tag(last_logout_horse)>).p=<p>
 f_osedlej_2
// finduid(<tag(last_logout_horse)>).dclick


[itemdef i_mounter]
id=i_memory
type=t_eq_script
name=resyncer - mounter

on=@create
timer=1

on=@timer
cont.resync
cont.f_loginmount
remove


[itemdef i_resyncer]
id=i_memory
type=t_eq_script
name=resyncer 

on=@create
timer=1

on=@timer
cont.resync
remove


[function do_a_cyrcle]
arg(u,0)
while (arg(u)<16)
 newitemsafe(<argv(0)>)
 lastnew.p=<p>
 if(argv(1))
  lastnew.timer=<argv(1)>
 endif
 lastnew.move(0,-1)
 doswitch u
  lastnew.move(2,-1,0,0)
  lastnew.move(1,-2,0,0)
  lastnew.move(0,-2,0,0)
  lastnew.move(-1,-2,0,0)
  lastnew.move(-2,-1,0,0)
  lastnew.move(-3,0,0,0)
  lastnew.move(-3,1,0,0)
  lastnew.move(-3,2,0,0)
  lastnew.move(-2,3,0,0)
  lastnew.move(2,3,0,0)
  lastnew.move(3,0,0,0)
  lastnew.move(3,1,0,0)
  lastnew.move(3,2,0,0)
  lastnew.move(1,4,0,0)
  lastnew.move(0,4,0,0)
  lastnew.move(-1,4,0,0)
 enddo
 lastnew.fix
 arg(u,#+1)
endif

[function nakrm]
newitemsafe(i_krmitko)
equip(<lastnew>)

[itemdef i_krmitko]
name=target krmeni
id=i_memory
type=t_eq_script

on=@equip
target
timer=60

on=@timer
remove
return 1

on=@targon_item
return 1

on=@targon_char
src.nakrmho
return 1


[function nakrmho]
If (targ.isplayer)
 sysmessage("To neni NPC")
 return 1
endif
if (targ.food < <targ.typedef.maxfood>)
 if (findtype(<return_nth(2,<targ.showfood>)>))
  arg(ham1,<eval 10000/<targ.typedef.maxfood>>*<targ.food>)
  arg(ham2,<eval <return_nth(1,<targ.showfood>)>*<arg(ham1)>>/10000)
  if (rescount(<findtype(<return_nth(2,<targ.showfood>)>).id>)  >= <eval  <return_nth(1,<targ.showfood>)>-<arg(ham2)>>)
   consume(<eval  <return_nth(1,<targ.showfood>)>-<arg(ham2)>>,<findtype(<return_nth(2,<targ.showfood>)>).id>)
   targ.food=<targ.typedef.maxfood>
  else
   sysmessage("Nemas dost jidla")
  endif 
 elseif (findtype(<return_nth(4,<targ.showfood>)>))
  arg(ham1,<eval 10000/<targ.typedef.maxfood>>*<targ.food>)
  arg(ham2,<eval <return_nth(3,<targ.showfood>)>*<arg(ham1)>>/10000)
  if (rescount(<findtype(<return_nth(4,<targ.showfood>)>).id>)  >= <eval  <return_nth(3,<targ.showfood>)>-<arg(ham2)>>)
   consume(<eval  <return_nth(3,<targ.showfood>)>-<arg(ham2)>>,<findtype(<return_nth(4,<targ.showfood>)>).id>)
   targ.food=<targ.typedef.maxfood>
  else
   sysmessage("Nemas dost jidla")
  endif 
 elseif (findtype(<return_nth(6,<targ.showfood>)>))
  arg(ham1,<eval 10000/<targ.typedef.maxfood>>*<targ.food>)
  arg(ham2,<eval <return_nth(5,<targ.showfood>)>*<arg(ham1)>>/10000)
  if (rescount(<findtype(<return_nth(6,<targ.showfood>)>).id>)  >= <eval  <return_nth(5,<targ.showfood>)>-<arg(ham2)>>)
   consume(<eval  <return_nth(5,<targ.showfood>)>-<arg(ham2)>>,<findtype(<return_nth(6,<targ.showfood>)>).id>)
   targ.food=<targ.typedef.maxfood>
  else
   sysmessage("Nemas dost jidla")
  endif 
 elseif (findtype(<return_nth(8,<targ.showfood>)>))
  arg(ham1,<eval 10000/<targ.typedef.maxfood>>*<targ.food>)
  arg(ham2,<eval <return_nth(7,<targ.showfood>)>*<arg(ham1)>>/10000)
  if (rescount(<findtype(<return_nth(8,<targ.showfood>)>).id>)  >= <eval  <return_nth(7,<targ.showfood>)>-<arg(ham2)>>)
   consume(<eval  <return_nth(7,<targ.showfood>)>-<arg(ham2)>>,<findtype(<return_nth(8,<targ.showfood>)>).id>)
   targ.food=<targ.typedef.maxfood>
  else
   sysmessage("Nemas dost jidla")
  endif 
 else
  sysmessage("Nemas u sebe nic z toho ce zere.")
 endif
else
 sysmessage("Neni hladovy")
endif


[itemdef i_delayed_function]
id=i_memory
type=t_eq_script
name=zpozdena funkce

on=@create
attr=attr_invis|attr_decay

on=@timer
cont.<tag.funkce>
remove
return 0

/////////////////////////////////////////
////////////// Zmena Jmena //////////////
/////////////////////////////////////////

[FUNCTION newname]
if (isGM)
  newitemsafe(i_zmenjmeno_target_timer)
  equip(lastnew)
elseif (<uid> != <src>) && !(npc)
  f_newname
endif

[itemdef i_zmenjmeno_target_timer]
ID=i_memory
TYPE=t_eq_script
name=namechanger

on=@equip
dclick
return 1

on=@userdclick
timer=60
target "Zamer hrace, ktereho chces prejmenovat"
return 1

on=@targon_item
target
return 1

on=@targon_char
if !(src.targ.npc) && !(src.targ.isGM)	//jde o hrace
  src.targ.f_newname
else
  src.sysMessage("Tomuhle jmeno zmenit nelze")
endif
remove
return 1

on=@timer
remove
return 1

[function f_newname]
if (isevent(e_zmenajmena))
  if (strcmpi(<name>,"Zvol si jmeno a pis page"))
    tag.remove(starejmeno)
    tag.remove(jmenozmenil)
    tag.remove(duvodzmenyjmena)
    stone=0
    tag.remove(muted)
    events=-e_zmenajmena
    sysmessage("Jsi uvolnen, prijemnou hru")
    gohome
  else
    src.redMessage("Hraci musis pred propustenim zmenit jmeno prikazem .x name=nove_jmeno!")
  endif
else
  tag.starejmeno="<name>"
  name="Zvol si jmeno a pis page"
  dialog(d_zmenajmena_duvod)
endif

[dialog d_zmenajmena_duvod]
5,5
page 0
tag(targPlayer,<argv(1)>)
resizepic 50 50 2620 500 200
textentry 150 120 340 347 2116 1 1
text 80 80 1152 0
button 250 190 2311 2312 1 0 1


[dialog d_zmenajmena_duvod text]
Duvod a potvrzeni zmeny jmena
Misto tohoto textu uved duvod zmeny.


[dialog d_zmenajmena_duvod button]
on=0
dialog(d_zmenajmena_duvod)

on=1
events=+e_zmenajmena
tag.duvodzmenyjmena="<argtxt(1)>"
src.redmessage(Duvod zmeny je: <argtxt(1)>)
tag.jmenozmenil="<src.name>"
stone=1
tag.muted=1
newitem(i_dialog_zmena_jmena)
equip(<lastnew>)
go(<eval {5575 5587}>,<eval {798 813}>,0,250)


[dialog d_zmenajmena]
5,5
page 0
resizepic 50 50 2620 700 250
text 80 80 1152 0
text 80 105 1152 1
text 100 130 2116 2
text 80 155 1152 3
text 100 180 2116 4
text 80 205 1152 5
text 80 230 1152 6

[dialog d_zmenajmena text]
Byli jsme nuceni vam docasne znemoznit hrani z duvodu nevhodneho jmena vasi postavy.
Duvodem je:
"<?src.tag.duvodzmenyjmena?>"
Toto rozhodnuti ucinil:
<?src.tag.jmenozmenil?>
Napis prosim PAGE a do ni uved sve nove jmeno. Jmeno ti bude co nejdrive
zmeneno a tva postava bude uvolnena. Peclive prostuduj pravidla pro jmena hracu!


[dialog d_zmenajmena button]


[EVENTS e_zmenajmena]
on=@login
dialog d_zmenajmena

[itemdef i_dialog_zmena_jmena]
ID=i_memory
TYPE=t_eq_script
name=dialogshow item

on=@equip
timer=1
topobj.dialog(d_zmenajmena)

on=@timer
remove
return 1

[function f_showfoodtype]
if (return_nth(8,<targ.showfood>))
 return <typename_<return_nth(2,<targ.showfood>)>>,<typename_<return_nth(4,<targ.showfood>)>>,<typename_<return_nth(6,<targ.showfood>)>>,<typename_<return_nth(8,<targ.showfood>)>>
elseif (return_nth(6,<targ.showfood>))
 return <typename_<return_nth(2,<targ.showfood>)>>,<typename_<return_nth(4,<targ.showfood>)>>,<typename_<return_nth(6,<targ.showfood>)>>
elseif (return_nth(4,<targ.showfood>))
 return <typename_<return_nth(2,<targ.showfood>)>>,<typename_<return_nth(4,<targ.showfood>)>>
else
 return <typename_<return_nth(2,<targ.showfood>)>>
endif

[function f_alore]
if (act.ispet)
 src.message("<act.name> patri osobe jmenem <finduid(<act.master>).name>.")
else
 src.message("<act.name> je svym vlastnim panem.")
endif
if (animallore>{0 1000})&&((act.npc==1) || (act.taming > 0))
 if (<act.food> < <act.typedef.maxfood>/4)
  src.message("<act.name> ma velky hlad")
 elseif (<act.food> < <act.typedef.maxfood>/2)
  src.message("<act.name> jeste zdaleka nema plny zaludek.")
 elseif (<act.food> < <act.typedef.maxfood>)
  src.message("<act.name> jeste nejake to jidlo zvladne.")
 else
  src.message("<act.name> ma dost a funi.")
 endif
 if (animallore > <act.animallore>)
  src.message("<act.name> se zivi vecmi jako: <f_showfoodtype>.")
  if (safe.strlen(act.tag(magic_horse)))
   src.SysMessage("Jizdou na tomto zvireti ziskas tyto schopnosti:")
   src.newitemsafe(i_show_animal_bonus)
   arg(show_bonus,<lastnew>)
   show_bonus.<act.tag(magic_horse)>_mif
   show_bonus.mif_finishitem
   var(magicname,"")
   show_bonus.mif_skillinfo(<show_bonus.tag.imi_skill_present>)
   show_bonus.mif_othersinfo(<show_bonus.tag.imi_other_present>)
   show_bonus.remove
  endif
 else
  src.message("ale snad jen Yavanna vi, co ta potvora zere.")
 endif
elseif (act==<uid>)&&(flag_onhorse==1)
 if (findid(i_hladovitko).tag(myfood) < <findid(i_hladovitko).tag(mymaxfood)>/4)
  src.message("<findid(i_hladovitko).tag(myname)> ma velky hlad")
 elseif (findid(i_hladovitko).tag(myfood) < <findid(i_hladovitko).tag(mymaxfood)>/2)
  src.message("<findid(i_hladovitko).tag(myname)> jeste zdaleka nema plny zaludek")
 elseif (findid(i_hladovitko).tag(myfood) < <findid(i_hladovitko).tag(mymaxfood)>)
  src.message("<findid(i_hladovitko).tag(myname)> jeste nejake to jidlo zvladne")
 else
  src.message("<findid(i_hladovitko).tag(myname)> ma dost a funi")
 endif
else
 src.message("Nic jsi nezjistil.") 
endif

[itemdef i_show_animal_bonus]
ID=i_rune_marker
TYPE=t_eq_script
name=ShowAnimalBonus

on=@create
timer=1

on=@timer
remove

[function f_fixleech]
if (isevent(e_hitsleech))
 events=-e_hitsleech
 events=+e_hitsleech
endif
if (isevent(e_stamleech))
 events=-e_stamleech
 events=+e_stamleech
endif
if (isevent(e_manaleech))
 events=-e_manaleech
 events=+e_manaleech
endif

[function presun]
newitem(i_presouvatko)
lastnew.equip

[itemdef i_presouvatko]
id=i_memory
name=presouvatko
type=t_eq_script

ON=@Equip
Target Co chces presunout?
Timer=60

ON=@TargOn_Char
If (<More2>==0)
 link=<src.targ.uid>
 More2=1
 targetG Kam s nim?
 Return 1
ElseIf (<More2>==1)
 link.p=<SRC.TARGP>
 link.update
 remove
 Return 1
endif
Return 1

On=@TargOn_Item
If (<More2>==0)
 link=<src.targ.uid>
 More2=1
 targetG Kam s tim? 
 Return 1
ElseIf (<More2>==1)
 link.p=<SRC.TARGP>
 link.update
 remove
 Return 1
endif

On=@TargOn_Ground
If (<More2>==1)
 link.p=<SRC.TARGP>
 link.update
 remove
 Return 1
endif

On=@Timer
remove
return 1

[function gatewalk]
if (safe.tag(gatewalk))
  tag.remove(gatewalk)
  SysMessage("Nyni muzes prochazet Gate!")
else
  tag(gatewalk,1)
  RedMessage("<sex Rozhodl Rozhodla> jsi se vyhybat Gate!")
endif
return 1

[function npchome]
if (npc)
//  arg(h_position,<home>)
  src.SysMessage("NPC vyslano na Home: <home>")
  go(<home>)
else
  src.SysMessage("Tohle neni npc")
endif

[function gonpchome]
if (npc)
  src.go(<home>)
  src.updatex
else
  src.SysMessage("Tohle neni npc")
endif
  
[function physicaldodge]
if (src.npc)
  if (var(is_physical))
    if (<eval tag(ra_weapdodge)*nastaveni_ability_pvMdodge> > {0 999})
      var(ubrano,0)
      var(dodged,1)
      classmessage("Uhnul jsi uderu")
      return 0
    endif
  endif
endif
return 0

[function spoctiSkilly]
arg(u,0)
arg(spRec,<tag.level>*5)
arg(spUsd,0)
while (arg(u)<50)
	arg(skillname,<findres(skill,<arg(u)>).name>)
	if !(<arg(u)> == 7) && !(<arg(u)> == 8) && !(<arg(u)> == 11) && !(<arg(u)> == 12) && !(<arg(u)> == 13) && !(<arg(u)> == 18) && !(<arg(u)> == 23) && !(<arg(u)> == 24) && !(<arg(u)> == 34) && !(<arg(u)> == 35) && !(<arg(u)> == 37) && !(<arg(u)> == 44) && !(<arg(u)> == 45)	//7 == blacksmith, 8 == bowcraft, 11 == carpentry, 12 == cartography, 13 == cooking, 18 == fishing, 23 == inscription, 24 == lockpick, 34 == tailoring, 35 == taming, 37 == tinkering, 44 == lumberjacking, 45 == mining
	  if (<eval safe.base_<tag.class>_<arg(skillname)>> < <eval arg(skillname)>)
	    if (<eval arg(skillname)> < 300)
	      arg(spUsd,#+<eval <eval arg(skillname)>/50>)
	      src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	    else
	      if (<eval safe.base_<tag.class>_<arg(skillname)>> == 0)
	        if (<eval arg(skillname)> < 550)
	          arg(spUsd,#+<eval <eval arg(skillname)>/50>-1)	// skill zacina na 0 a ma hodnotu mezi 30 a 50ti. Odecitame 5% od mistra -> neztracej se skillbody
	          src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	        elseif (<eval arg(skillname)> < 800)
	          arg(i,<eval (<eval <eval arg(skillname)>-550> / 50)> * 2)
	          arg(spUsd,#+<eval 9+<arg(i)>>)
	          src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	        else
	          arg(i,<eval (<eval <eval arg(skillname)>-800> / 50)> * 3)
	          arg(spUsd,#+<eval 17+<arg(i)>>)
	          src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	        endif
	      else
	        if (<eval arg(skillname)> < 550)
	          arg(i,<eval (<eval <eval arg(skillname)>-300> / 50)>)	// skill zacina na 30 a ma hodnotu mezi 30 a 50ti.
	          arg(spUsd,#+<arg(i)>)
	          src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	        elseif (<eval arg(skillname)> < 800)
	          arg(i,<eval (<eval <eval arg(skillname)>-550> / 50)> * 2)
	          arg(spUsd,#+<eval 4+<arg(i)>>)
	          src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	        else
	          arg(i,<eval (<eval <eval arg(skillname)>-800> / 50)> * 3)
	          arg(spUsd,#+<eval 12+<arg(i)>>)
	          src.sysMessage("po <arg(skillname)> mam <arg(spUsd)> bodu")
	        endif
	      endif
	    endif	    
	    src.sysMessage("_________")
	  endif
	endif
	arg(u,#+1)
endwhile
src.sysMessage("Hrac rozdelil <eval arg(spUsd)> bodu a dostal jich <eval arg(spRec)>")


[eof]